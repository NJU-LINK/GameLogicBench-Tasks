extends RefCounted
#
# view.gd -- vector rendering shared by the F5 preview (world_runtime.gd). Draws the station disc,
# the dock port and its approach sector, and the craft as an oriented triangle with its velocity
# vector. Pure presentation: nothing here feeds the simulation.

const SimCore = preload("res://sim_core.gd")

static func render(canvas: CanvasItem, spec: Dictionary, vs: Dictionary) -> void:
	var station: Vector2 = spec["station_pos"]
	var station_r: float = float(spec["station_r"])
	var dock: Vector2 = spec["dock_pos"]
	var n: Vector2 = spec["dock_normal"]

	# field
	canvas.draw_rect(Rect2(0, 0, spec["world_w"], spec["world_h"]), Color(0.07, 0.08, 0.10), true)

	# station hull + dock port + approach sector wedge
	canvas.draw_circle(station, station_r, Color(0.25, 0.27, 0.33))
	canvas.draw_arc(station, station_r, 0.0, TAU, 64, Color(0.5, 0.55, 0.65), 2.0)
	var sector_half := acos(SimCore.DOCK_SECTOR_COS)
	var a0 := n.angle() - sector_half
	var a1 := n.angle() + sector_half
	canvas.draw_line(station + Vector2(cos(a0), sin(a0)) * station_r,
		station + Vector2(cos(a0), sin(a0)) * (station_r + 70.0), Color(0.3, 0.6, 0.4, 0.5), 1.0)
	canvas.draw_line(station + Vector2(cos(a1), sin(a1)) * station_r,
		station + Vector2(cos(a1), sin(a1)) * (station_r + 70.0), Color(0.3, 0.6, 0.4, 0.5), 1.0)
	canvas.draw_circle(dock, SimCore.DOCK_CAPTURE, Color(0.2, 0.8, 0.5, 0.25))
	canvas.draw_circle(dock, 4.0, Color(0.3, 1.0, 0.6))
	canvas.draw_line(dock, dock + n * 26.0, Color(0.3, 1.0, 0.6), 2.0)

	# craft: oriented triangle (nose = heading) + velocity vector
	var pos: Vector2 = vs["self_pos"]
	var heading: float = float(vs["heading"])
	var f := Vector2(cos(heading), sin(heading))
	var s := Vector2(-f.y, f.x)
	var r := SimCore.CRAFT_R
	var pts := PackedVector2Array([
		pos + f * r * 1.8,
		pos - f * r + s * r,
		pos - f * r - s * r,
	])
	canvas.draw_colored_polygon(pts, Color(0.95, 0.8, 0.3))
	canvas.draw_line(pos, pos + vs["vel"] * 0.4, Color(0.9, 0.4, 0.3), 1.5)
