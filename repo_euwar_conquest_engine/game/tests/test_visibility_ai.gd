extends SceneTree

const CombatRules := preload("res://scripts/combat/combat_rules.gd")
const HexCoord := preload("res://scripts/grid/hex_coord.gd")
const Visibility := preload("res://scripts/grid/visibility.gd")

var fails := 0

func ok(cond: bool, msg: String) -> void:
	if cond:
		print("  PASS: ", msg)
	else:
		fails += 1
		printerr("  FAIL: ", msg)

class FakeTarget:
	extends RefCounted
	var faction_id: String
	var coord: Vector2i
	func _init(f: String, c: Vector2i) -> void:
		faction_id = f
		coord = c
	func is_alive() -> bool:
		return true

class OpenMap:
	extends RefCounted
	var units: Array = []
	var occupants: Dictionary = {}
	func terrain_at(_c: Vector2i) -> String:
		return "plain"
	func blocks_los_at(_c: Vector2i) -> bool:
		return false
	func move_cost_at(_c: Vector2i) -> int:
		return 1
	func terrain_impassable(_terrain_id: String) -> bool:
		return false
	func unit_at(c: Vector2i):
		return occupants.get(c)

func _init() -> void:
	_run.call_deferred()

func _run() -> void:
	var dl = root.get_node("DataLoader")
	await process_frame
	var AIController = load("res://scripts/turn/ai_controller.gd")
	var UnitScript = load("res://scripts/units/unit.gd")

	var cannon = UnitScript.new()
	cannon.configure("field_cannon", "blue", Color.WHITE, Vector2i(0, 0), "Test cannon")
	var target := FakeTarget.new("red", Vector2i(3, 0))
	var map := OpenMap.new()
	map.units = [cannon, target]
	map.occupants = {cannon.coord: cannon, target.coord: target}
	var cannon_def: Dictionary = dl.get_unit_def("field_cannon")

	var base_vis := Visibility.compute_visible_hexes([cannon], "blue", map, dl.units, dl.generals)
	ok(not base_vis.has(target.coord), "base cannon vision does not see range-3 target")
	ok(not CombatRules.can_attack_from_coord(cannon.coord, "blue", target, cannon_def, map, base_vis),
		"range-3 target is not legal before vision bonus")

	cannon.active_effects.append({"self_mods": {"vision": 1}})
	var boosted_vis := Visibility.compute_visible_hexes([cannon], "blue", map, dl.units, dl.generals)
	ok(boosted_vis.has(target.coord), "active vision bonus expands actual visible hexes")
	ok(CombatRules.can_attack_from_coord(cannon.coord, "blue", target, cannon_def, map, boosted_vis),
		"vision bonus makes an in-range target attack-legal")

	var bow = UnitScript.new()
	bow.configure("longbowmen", "blue", Color.WHITE, Vector2i(0, 0), "AI longbow")
	var enemy = UnitScript.new()
	enemy.configure("men_at_arms", "red", Color.WHITE, Vector2i(5, 0), "Hidden target")
	map.units = [bow, enemy]
	map.occupants = {bow.coord: bow, enemy.coord: enemy}

	var ai = AIController.new("hard")
	ai.begin_turn("blue", map.units, map, {"blue": {"posture": "aggressive"}, "red": {}}, {})
	var order = ai.plan_unit(bow, map.units, map, {"blue": {"posture": "aggressive"}, "red": {}})
	ok(order.action == "attack", "AI plans an attack after moving into visibility")
	ok(order.target == enemy, "AI move-after-vision attack chooses the newly visible target")
	ok(HexCoord.distance(order.dest, enemy.coord) <= int(dl.get_unit_def("longbowmen").get("range", 1)),
		"AI destination is within weapon range after movement")

	var musket = UnitScript.new()
	musket.configure("musketeers", "blue", Color.WHITE, Vector2i(0, 0), "AI muskets")
	var close_enemy = UnitScript.new()
	close_enemy.configure("men_at_arms", "red", Color.WHITE, Vector2i(1, 0), "Close target")
	map.units = [musket, close_enemy]
	map.occupants = {musket.coord: musket, close_enemy.coord: close_enemy}
	var direct_ctx: Dictionary = ai._candidate_context(musket, Vector2i(-1, 0), map.units, map, "blue")
	ok(CombatRules.can_attack_from_coord(
		Vector2i(-1, 0), "blue", close_enemy, dl.get_unit_def("musketeers"),
		direct_ctx["map"], direct_ctx["visible"]),
		"candidate map clears the mover's original hex for direct fire LOS")

	cannon.free()
	bow.free()
	enemy.free()
	musket.free()
	close_enemy.free()

	if fails == 0:
		print("test_visibility_ai: ok")
		quit(0)
	else:
		printerr("test_visibility_ai: %d failures" % fails)
		quit(1)
