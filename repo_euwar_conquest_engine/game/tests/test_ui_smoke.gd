extends SceneTree

# Loads every screen headlessly and drives a short battle turn to catch runtime
# errors (bad node paths, missing signals, AI crashes). Any SCRIPT ERROR printed
# during instantiation fails the run (the test runner greps for it).

const SCENES := [
	"res://scenes/main_menu.tscn",
	"res://scenes/scenario_select.tscn",
	"res://scenes/briefing.tscn",
	"res://scenes/deployment.tscn",
	"res://scenes/battle.tscn",
	"res://scenes/help.tscn",
]

func _init() -> void:
	_run.call_deferred()

func _run() -> void:
	var gs = root.get_node_or_null("GameState")
	if gs == null:
		printerr("FAIL: GameState autoload missing")
		quit(1)
		return
	gs.current_scenario_id = "00_tutorial"
	for path in SCENES:
		var ps = load(path)
		if ps == null:
			printerr("FAIL: could not load %s" % path)
			quit(1)
			return
		var inst = ps.instantiate()
		root.add_child(inst)
		await process_frame
		await process_frame
		inst.queue_free()
		await process_frame

	# Drive an AI turn on a Hard difficulty battle so the AIController path runs.
	gs.difficulty = "hard"
	gs.current_scenario_id = "02_crecy_1346"
	var battle = load("res://scenes/battle.tscn").instantiate()
	root.add_child(battle)
	for _i in range(20):
		await process_frame
	# End the player's turn to trigger the AI turn coroutine.
	if battle.has_method("_advance_turn"):
		battle._advance_turn()
	for _i in range(120):
		await process_frame
	battle.queue_free()
	await process_frame

	print("test_ui_smoke: ok")
	quit(0)
