extends Node

# Autoload singleton (registered in project.godot as DataLoader).
# Loads JSON catalogs once at startup; everything else reads from here.

const UNITS_PATH := "res://data/units.json"
const TERRAINS_PATH := "res://data/terrains.json"
const GENERALS_PATH := "res://data/generals.json"
const CAMPAIGNS_PATH := "res://data/campaigns.json"
const TECHS_PATH := "res://data/techs.json"
const CONQUEST_PATH := "res://data/conquest.json"
const HELP_PATH := "res://data/help.json"
const SCENARIOS_DIR := "res://data/scenarios/"

var units: Dictionary = {}
var terrains: Dictionary = {}
var generals: Dictionary = {}
var campaigns: Dictionary = {}
var techs: Dictionary = {}
var conquests: Dictionary = {}
var help: Dictionary = {}
var scenarios: Array[Dictionary] = []

func _ready() -> void:
	units = _with_catalog_ids(_load_json(UNITS_PATH))
	terrains = _with_catalog_ids(_load_json(TERRAINS_PATH))
	generals = _with_catalog_ids(_load_json(GENERALS_PATH))
	campaigns = _with_catalog_ids(_load_json(CAMPAIGNS_PATH))
	techs = _with_catalog_ids(_load_json(TECHS_PATH))
	conquests = _with_catalog_ids(_load_json(CONQUEST_PATH))
	help = _load_json(HELP_PATH)
	scenarios = _load_scenarios()
	print("[DataLoader] loaded %d units, %d terrains, %d commanders, %d scenarios, %d campaigns, %d techs, %d conquests" % [
		units.size(), terrains.size(), generals.size(), scenarios.size(), campaigns.size(), techs.size(), conquests.size(),
	])

func get_campaign(campaign_id: String) -> Dictionary:
	return campaigns.get(campaign_id, {})

func get_conquest(conquest_id: String) -> Dictionary:
	return conquests.get(conquest_id, {})

func get_unit_def(type_id: String) -> Dictionary:
	if not units.has(type_id):
		push_error("Unknown unit type: " + type_id)
		return {}
	return units[type_id]

func get_terrain_def(terrain_id: String) -> Dictionary:
	if not terrains.has(terrain_id):
		push_error("Unknown terrain: " + terrain_id)
		return {}
	return terrains[terrain_id]

func get_general_def(general_id: String) -> Dictionary:
	if general_id == "":
		return {}
	if not generals.has(general_id):
		push_error("Unknown commander: " + general_id)
		return {}
	return generals[general_id]

func get_scenario(scenario_id: String) -> Dictionary:
	for s in scenarios:
		if s.get("id", "") == scenario_id:
			return s
	push_error("Unknown scenario: " + scenario_id)
	return {}

func _load_json(path: String) -> Dictionary:
	if not FileAccess.file_exists(path):
		push_error("Missing JSON: " + path)
		return {}
	var f := FileAccess.open(path, FileAccess.READ)
	var text := f.get_as_text()
	f.close()
	var parsed: Variant = JSON.parse_string(text)
	if typeof(parsed) != TYPE_DICTIONARY:
		push_error("Invalid JSON dict: " + path)
		return {}
	return parsed

func _with_catalog_ids(data: Dictionary) -> Dictionary:
	for key in data.keys():
		if data[key] is Dictionary:
			data[key]["id"] = String(key)
	return data

func _load_scenarios() -> Array[Dictionary]:
	var out: Array[Dictionary] = []
	var dir := DirAccess.open(SCENARIOS_DIR)
	if dir == null:
		return out
	dir.list_dir_begin()
	var name := dir.get_next()
	while name != "":
		if not dir.current_is_dir() and name.ends_with(".json"):
			var data := _load_json(SCENARIOS_DIR + name)
			if not data.is_empty():
				out.append(data)
		name = dir.get_next()
	dir.list_dir_end()
	out.sort_custom(func(a, b): return a.get("id", "") < b.get("id", ""))
	return out
