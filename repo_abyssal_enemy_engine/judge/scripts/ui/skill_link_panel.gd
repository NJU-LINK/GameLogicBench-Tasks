class_name SkillLinkPanel
extends Control

const PLAYER_BUILD_FACADE := preload("res://scripts/core/player/player_build_facade.gd")

## 技能連結面板 - 顯示技能寶石與輔助寶石

signal closed
signal navigate_to(panel_id: String)

@onready var panel_vbox: VBoxContainer = $PanelContainer/VBox
@onready var panel_root: PanelContainer = $PanelContainer
@onready var header_row: HBoxContainer = $PanelContainer/VBox/Header
@onready var inventory_row: HBoxContainer = $PanelContainer/VBox/InventoryRow
@onready var title_label: Label = $PanelContainer/VBox/Header/Title
@onready var close_button: Button = $PanelContainer/VBox/Header/CloseButton
@onready var skill_section_label: Label = $PanelContainer/VBox/SkillSection/Label
@onready var skill_slot: Button = $PanelContainer/VBox/SkillSection/SkillSlot
@onready var support_section_label: Label = $PanelContainer/VBox/SupportSection/Label
@onready var support_grid: GridContainer = $PanelContainer/VBox/SupportSection/SupportGrid
@onready var skill_inventory_label: Label = $PanelContainer/VBox/InventoryRow/SkillInventorySection/Label
@onready var skill_inventory_grid: GridContainer = $PanelContainer/VBox/InventoryRow/SkillInventorySection/SkillInventoryScroll/SkillInventoryGrid
@onready var support_inventory_label: Label = $PanelContainer/VBox/InventoryRow/SupportInventorySection/Label
@onready var support_inventory_grid: GridContainer = $PanelContainer/VBox/InventoryRow/SupportInventorySection/SupportInventoryScroll/SupportInventoryGrid
@onready var skill_inventory_scroll: ScrollContainer = $PanelContainer/VBox/InventoryRow/SkillInventorySection/SkillInventoryScroll
@onready var support_inventory_scroll: ScrollContainer = $PanelContainer/VBox/InventoryRow/SupportInventorySection/SupportInventoryScroll
@onready var item_tooltip: PanelContainer = $ItemTooltip
@onready var tooltip_label: RichTextLabel = $ItemTooltip/MarginContainer/TooltipText

var player: Player = null
var build = null
var support_buttons: Array[Button] = []
var skill_inventory_buttons: Array[Button] = []
var support_inventory_buttons: Array[Button] = []

var drag_active: bool = false
var drag_source_type: String = ""
var drag_source_index: int = -1
var hover_target_type: String = ""
var hover_target_index: int = -1
var pause_tree_on_open: bool = true
var allow_crafting_navigation: bool = true

const SLOT_SIZE := Vector2(56, 56)
const PANEL_HALF_SIZE := Vector2(450, 250)
const TAB_ACTIVE_BG := Color(0.24, 0.35, 0.55, 0.95)
const TAB_INACTIVE_BG := Color(0.2, 0.2, 0.25, 0.92)
const TAB_BORDER := Color(0.45, 0.45, 0.55)
const TAB_ACTIVE_BORDER := Color(0.72, 0.82, 1.0)


func _ready() -> void:
	process_mode = Node.PROCESS_MODE_ALWAYS
	_apply_visual_style()
	_setup_nav_tabs()
	_apply_localized_texts()
	_setup_skill_slot()
	_create_support_slots()
	_create_skill_inventory_slots()
	_create_support_inventory_slots()
	if item_tooltip:
		item_tooltip.mouse_filter = Control.MOUSE_FILTER_IGNORE
		item_tooltip.top_level = true
	hide_tooltip()
	visible = false
	if not LocalizationService.locale_changed.is_connected(_on_locale_changed):
		LocalizationService.locale_changed.connect(_on_locale_changed)


func _process(_delta: float) -> void:
	if item_tooltip != null and item_tooltip.visible:
		_position_tooltip()


func _setup_nav_tabs() -> void:
	if panel_vbox == null:
		return
	var nav := HBoxContainer.new()
	nav.name = "NavTabs"
	nav.alignment = BoxContainer.ALIGNMENT_CENTER
	nav.add_theme_constant_override("separation", 6)

	var tabs := [
		{"id": "equipment", "label": "裝備"},
		{"id": "module", "label": "模組"},
		{"id": "skill", "label": "技能"},
		{"id": "crafting", "label": "打造"},
	]

	for tab in tabs:
		var tab_id: String = str(tab["id"])
		if tab_id == "crafting" and not allow_crafting_navigation:
			continue
		var btn := Button.new()
		btn.name = "Nav_%s" % tab_id
		btn.text = str(tab["label"])
		_style_nav_button(btn, tab_id == "skill")
		btn.pressed.connect(func() -> void:
			if tab_id != "skill":
				navigate_to.emit(tab_id)
		)
		nav.add_child(btn)

	panel_vbox.add_child(nav)
	panel_vbox.move_child(nav, 1)


func _apply_visual_style() -> void:
	if panel_root:
		panel_root.anchor_left = 0.5
		panel_root.anchor_top = 0.5
		panel_root.anchor_right = 0.5
		panel_root.anchor_bottom = 0.5
		panel_root.offset_left = -PANEL_HALF_SIZE.x
		panel_root.offset_top = -PANEL_HALF_SIZE.y
		panel_root.offset_right = PANEL_HALF_SIZE.x
		panel_root.offset_bottom = PANEL_HALF_SIZE.y
		var panel_style := StyleBoxFlat.new()
		panel_style.bg_color = Color(0.12, 0.12, 0.16, 0.97)
		panel_style.border_color = Color(0.45, 0.45, 0.55)
		panel_style.set_border_width_all(2)
		panel_style.set_corner_radius_all(4)
		panel_style.content_margin_left = 12
		panel_style.content_margin_top = 12
		panel_style.content_margin_right = 12
		panel_style.content_margin_bottom = 12
		panel_root.add_theme_stylebox_override("panel", panel_style)
	if panel_vbox:
		panel_vbox.add_theme_constant_override("separation", 8)
	if header_row:
		header_row.custom_minimum_size.y = 34
	if inventory_row:
		inventory_row.add_theme_constant_override("separation", 12)
	if skill_inventory_scroll:
		skill_inventory_scroll.custom_minimum_size = Vector2(0, 220)
	if support_inventory_scroll:
		support_inventory_scroll.custom_minimum_size = Vector2(0, 220)
	if title_label:
		title_label.add_theme_font_size_override("font_size", 15)
	if close_button:
		close_button.text = "X"
		close_button.custom_minimum_size = Vector2(30, 30)


func _style_nav_button(btn: Button, active: bool) -> void:
	var normal := StyleBoxFlat.new()
	normal.bg_color = TAB_ACTIVE_BG if active else TAB_INACTIVE_BG
	normal.border_color = TAB_ACTIVE_BORDER if active else TAB_BORDER
	normal.set_border_width_all(1)
	normal.set_corner_radius_all(4)

	var hover := normal.duplicate() as StyleBoxFlat
	if not active:
		hover.border_color = TAB_ACTIVE_BORDER

	btn.add_theme_stylebox_override("normal", normal)
	btn.add_theme_stylebox_override("hover", hover)
	btn.add_theme_stylebox_override("pressed", normal)
	btn.add_theme_color_override("font_color", Color(0.95, 0.97, 1.0))
	btn.custom_minimum_size = Vector2(72, 26)


func _input(event: InputEvent) -> void:
	if not visible:
		return
	if event.is_action_pressed("ui_cancel"):
		close()
		get_viewport().set_input_as_handled()
		return
	if event is InputEventKey and event.pressed and not event.echo:
		var key_event := event as InputEventKey
		if key_event.keycode == KEY_K:
			close()
			get_viewport().set_input_as_handled()
		elif key_event.keycode == KEY_I:
			navigate_to.emit("equipment")
			get_viewport().set_input_as_handled()
		elif key_event.keycode == KEY_M:
			navigate_to.emit("module")
			get_viewport().set_input_as_handled()
		elif key_event.keycode == KEY_C and allow_crafting_navigation:
			navigate_to.emit("crafting")
			get_viewport().set_input_as_handled()


func open(p: Player) -> void:
	player = p
	build = PLAYER_BUILD_FACADE.new(p)
	_apply_localized_texts()
	visible = true
	refresh()
	if pause_tree_on_open:
		get_tree().paused = true


func close() -> void:
	visible = false
	hide_tooltip()
	player = null
	build = null
	if pause_tree_on_open:
		get_tree().paused = false
	closed.emit()


func set_pause_tree_on_open(enabled: bool) -> void:
	pause_tree_on_open = enabled


func set_allow_crafting_navigation(enabled: bool) -> void:
	allow_crafting_navigation = enabled
	var nav := panel_vbox.get_node_or_null("NavTabs") if panel_vbox != null else null
	if nav != null:
		var crafting_button := nav.get_node_or_null("Nav_crafting") as Control
		if crafting_button != null:
			crafting_button.visible = enabled


func refresh() -> void:
	if player == null:
		return
	_refresh_skill()
	_refresh_supports()
	_refresh_skill_inventory()
	_refresh_support_inventory()


func _setup_skill_slot() -> void:
	if skill_slot == null:
		return

	skill_slot.custom_minimum_size = SLOT_SIZE
	skill_slot.clip_contents = true
	skill_slot.add_theme_font_size_override("font_size", 12)
	skill_slot.text = ""

	var style := StyleBoxFlat.new()
	style.bg_color = Color(0.12, 0.18, 0.12, 0.9)
	style.border_color = Color(0.2, 0.6, 0.2)
	style.set_border_width_all(2)
	style.set_corner_radius_all(4)
	skill_slot.add_theme_stylebox_override("normal", style)

	var hover_style := style.duplicate()
	hover_style.border_color = Color(0.4, 0.9, 0.4)
	skill_slot.add_theme_stylebox_override("hover", hover_style)

	skill_slot.mouse_entered.connect(_on_skill_slot_hovered)
	skill_slot.mouse_entered.connect(_on_hover_target.bind("skill_slot", 0))
	skill_slot.mouse_exited.connect(_on_hover_exit.bind("skill_slot", 0))
	skill_slot.mouse_exited.connect(hide_tooltip)
	skill_slot.button_down.connect(_on_drag_start.bind("skill_slot", 0))
	skill_slot.button_up.connect(_on_drag_end.bind("skill_slot", 0))


func _create_support_slots() -> void:
	if support_grid == null:
		return

	for child in support_grid.get_children():
		child.queue_free()
	support_buttons.clear()

	for i in range(Constants.MAX_SUPPORT_GEMS):
		var btn := _create_support_button(i)
		support_grid.add_child(btn)
		support_buttons.append(btn)


func _create_skill_inventory_slots() -> void:
	if skill_inventory_grid == null:
		return

	for child in skill_inventory_grid.get_children():
		child.queue_free()
	skill_inventory_buttons.clear()

	for i in range(Constants.MAX_SKILL_GEM_INVENTORY):
		var btn := _create_inventory_button(i, true)
		skill_inventory_grid.add_child(btn)
		skill_inventory_buttons.append(btn)


func _create_support_inventory_slots() -> void:
	if support_inventory_grid == null:
		return

	for child in support_inventory_grid.get_children():
		child.queue_free()
	support_inventory_buttons.clear()

	for i in range(Constants.MAX_SUPPORT_GEM_INVENTORY):
		var btn := _create_inventory_button(i, false)
		support_inventory_grid.add_child(btn)
		support_inventory_buttons.append(btn)


func _create_inventory_button(index: int, is_skill: bool) -> Button:
	var btn := Button.new()
	btn.custom_minimum_size = SLOT_SIZE
	btn.clip_contents = true
	btn.add_theme_font_size_override("font_size", 12)
	btn.text = ""

	var style := StyleBoxFlat.new()
	style.bg_color = Color(0.16, 0.16, 0.18, 0.9)
	style.border_color = Color(0.3, 0.3, 0.35)
	style.set_border_width_all(1)
	style.set_corner_radius_all(2)
	btn.add_theme_stylebox_override("normal", style)

	var hover_style := style.duplicate()
	hover_style.border_color = Color(0.6, 0.6, 0.7)
	btn.add_theme_stylebox_override("hover", hover_style)

	if is_skill:
		btn.button_down.connect(_on_drag_start.bind("skill_inv", index))
		btn.button_up.connect(_on_drag_end.bind("skill_inv", index))
		btn.mouse_entered.connect(_on_hover_target.bind("skill_inv", index))
		btn.mouse_entered.connect(_on_skill_inventory_hovered.bind(index))
		btn.mouse_exited.connect(_on_hover_exit.bind("skill_inv", index))
	else:
		btn.button_down.connect(_on_drag_start.bind("support_inv", index))
		btn.button_up.connect(_on_drag_end.bind("support_inv", index))
		btn.mouse_entered.connect(_on_hover_target.bind("support_inv", index))
		btn.mouse_entered.connect(_on_support_inventory_hovered.bind(index))
		btn.mouse_exited.connect(_on_hover_exit.bind("support_inv", index))
	btn.mouse_exited.connect(hide_tooltip)

	return btn


func _create_support_button(index: int) -> Button:
	var btn := Button.new()
	btn.custom_minimum_size = SLOT_SIZE
	btn.clip_contents = true
	btn.add_theme_font_size_override("font_size", 12)
	btn.text = ""

	var style := StyleBoxFlat.new()
	style.bg_color = Color(0.12, 0.14, 0.2, 0.9)
	style.border_color = Color(0.3, 0.45, 0.65)
	style.set_border_width_all(2)
	style.set_corner_radius_all(4)
	btn.add_theme_stylebox_override("normal", style)

	var hover_style := style.duplicate()
	hover_style.border_color = Color(0.5, 0.7, 1.0)
	btn.add_theme_stylebox_override("hover", hover_style)

	btn.button_down.connect(_on_drag_start.bind("support_slot", index))
	btn.button_up.connect(_on_drag_end.bind("support_slot", index))
	btn.mouse_entered.connect(_on_support_slot_hovered.bind(index))
	btn.mouse_entered.connect(_on_hover_target.bind("support_slot", index))
	btn.mouse_exited.connect(_on_hover_exit.bind("support_slot", index))
	btn.mouse_exited.connect(hide_tooltip)

	return btn


func _refresh_skill() -> void:
	var gem: SkillGem = build.get_skill_gem()
	if gem:
		var compatible: bool = gem.can_use_with_weapon(build.get_weapon_type())
		skill_slot.text = _format_gem_button_text(gem.display_name, gem.level)
		if compatible:
			skill_slot.add_theme_color_override("font_color", Color(0.6, 1.0, 0.6))
			skill_slot.modulate = Color.WHITE
		else:
			skill_slot.add_theme_color_override("font_color", Color(1.0, 0.45, 0.45))
			skill_slot.modulate = Color(1.0, 0.7, 0.7)
	else:
		skill_slot.text = _t("ui.skill.empty", "Empty")
		skill_slot.remove_theme_color_override("font_color")
		skill_slot.modulate = Color.WHITE


func _refresh_supports() -> void:
	var skill_gem: SkillGem = build.get_skill_gem()
	for i in range(support_buttons.size()):
		var btn: Button = support_buttons[i]
		var gem: SupportGem = null
		if i < build.get_support_gem_count():
			gem = build.get_support_gem(i)

		if gem:
			var compatible: bool = skill_gem != null and gem.can_support_skill(skill_gem)
			btn.text = _format_gem_button_text(gem.display_name, gem.level)
			if compatible:
				btn.add_theme_color_override("font_color", Color(0.6, 0.85, 1.0))
				btn.modulate = Color.WHITE
			else:
				btn.add_theme_color_override("font_color", Color(1.0, 0.45, 0.45))
				btn.modulate = Color(1.0, 0.75, 0.75)
		else:
			btn.text = _t("ui.skill.empty", "Empty")
			btn.remove_theme_color_override("font_color")
			btn.modulate = Color.WHITE


func _refresh_skill_inventory() -> void:
	var weapon_type: int = build.get_weapon_type()
	for i in range(skill_inventory_buttons.size()):
		var btn: Button = skill_inventory_buttons[i]
		var gem: SkillGem = player.get_skill_gem_in_inventory(i)
		if gem:
			btn.text = _format_gem_button_text(gem.display_name, gem.level)
			if gem.can_use_with_weapon(weapon_type):
				btn.add_theme_color_override("font_color", Color(0.6, 1.0, 0.6))
				btn.modulate = Color.WHITE
			else:
				btn.add_theme_color_override("font_color", Color(1.0, 0.45, 0.45))
				btn.modulate = Color(1.0, 0.75, 0.75)
		else:
			btn.text = ""
			btn.remove_theme_color_override("font_color")
			btn.modulate = Color.WHITE


func _refresh_support_inventory() -> void:
	var skill_gem: SkillGem = build.get_skill_gem()
	for i in range(support_inventory_buttons.size()):
		var btn: Button = support_inventory_buttons[i]
		var gem: SupportGem = player.get_support_gem_in_inventory(i)
		if gem:
			var compatible: bool = skill_gem != null and gem.can_support_skill(skill_gem)
			btn.text = _format_gem_button_text(gem.display_name, gem.level)
			if compatible:
				btn.add_theme_color_override("font_color", Color(0.6, 0.85, 1.0))
				btn.modulate = Color.WHITE
			else:
				btn.add_theme_color_override("font_color", Color(1.0, 0.45, 0.45))
				btn.modulate = Color(1.0, 0.75, 0.75)
		else:
			btn.text = ""
			btn.remove_theme_color_override("font_color")
			btn.modulate = Color.WHITE


func _on_skill_slot_hovered() -> void:
	var gem: SkillGem = build.get_skill_gem()
	if gem:
		var tooltip := gem.get_tooltip()
		if not gem.can_use_with_weapon(build.get_weapon_type()):
			tooltip = "[color=#ff6666]%s[/color]\n\n%s" % [_t("ui.skill.incompatible_weapon", "This skill gem cannot be used with your current weapon."), tooltip]
		show_tooltip(tooltip)
	else:
		show_tooltip("[color=gray]%s[/color]" % _t("ui.skill.no_skill_equipped", "No skill gem equipped"))


func _on_skill_slot_pressed() -> void:
	if player == null:
		return
	if player.unequip_skill_to_inventory():
		refresh()


func _on_support_slot_hovered(index: int) -> void:
	var gem: SupportGem = null
	if index < build.get_support_gem_count():
		gem = build.get_support_gem(index)

	if gem:
		var tooltip := gem.get_tooltip()
		var skill_gem: SkillGem = build.get_skill_gem()
		if skill_gem == null or not gem.can_support_skill(skill_gem):
			tooltip = "[color=#ff6666]%s[/color]\n\n%s" % [_t("ui.skill.incompatible_support", "This support gem cannot support the current skill."), tooltip]
		show_tooltip(tooltip)
	else:
		show_tooltip("[color=gray]%s[/color]" % _t("ui.skill.empty_support", "Empty support slot"))


func _on_support_slot_pressed(index: int) -> void:
	if player == null:
		return
	if player.unequip_support_to_inventory(index):
		refresh()


func _on_skill_inventory_pressed(index: int) -> void:
	if player == null:
		return
	if player.equip_skill_from_inventory(index):
		refresh()


func _on_support_inventory_pressed(index: int) -> void:
	if player == null:
		return
	if player.equip_support_from_inventory(index):
		refresh()


func _on_skill_inventory_hovered(index: int) -> void:
	var gem: SkillGem = player.get_skill_gem_in_inventory(index)
	if gem:
		var tooltip := gem.get_tooltip()
		if not gem.can_use_with_weapon(build.get_weapon_type()):
			tooltip = "[color=#ff6666]%s[/color]\n\n%s" % [_t("ui.skill.incompatible_weapon", "This skill gem cannot be used with your current weapon."), tooltip]
		show_tooltip(tooltip)
	else:
		hide_tooltip()


func _on_support_inventory_hovered(index: int) -> void:
	var gem: SupportGem = player.get_support_gem_in_inventory(index)
	if gem:
		var tooltip := gem.get_tooltip()
		var skill_gem: SkillGem = build.get_skill_gem()
		if skill_gem == null:
			tooltip = "[color=#ffaa44]%s[/color]\n\n%s" % [_t("ui.skill.equip_skill_first", "Equip a skill gem first."), tooltip]
		elif not gem.can_support_skill(skill_gem):
			tooltip = "[color=#ff6666]%s[/color]\n\n%s" % [_t("ui.skill.incompatible_support", "This support gem cannot support the current skill."), tooltip]
		show_tooltip(tooltip)
	else:
		hide_tooltip()


func _on_drag_start(source_type: String, index: int) -> void:
	drag_active = true
	drag_source_type = source_type
	drag_source_index = index


func _on_drag_end(source_type: String, index: int) -> void:
	if not drag_active:
		return

	var target_type := hover_target_type
	var target_index := hover_target_index

	drag_active = false
	drag_source_type = ""
	drag_source_index = -1
	hover_target_type = ""
	hover_target_index = -1

	if target_type == "" or (target_type == source_type and target_index == index):
		_handle_click(source_type, index)
		return

	_perform_drag(source_type, index, target_type, target_index)


func _on_hover_target(target_type: String, index: int) -> void:
	hover_target_type = target_type
	hover_target_index = index


func _on_hover_exit(target_type: String, index: int) -> void:
	if hover_target_type == target_type and hover_target_index == index:
		hover_target_type = ""
		hover_target_index = -1


func _handle_click(source_type: String, index: int) -> void:
	match source_type:
		"skill_slot":
			_on_skill_slot_pressed()
		"support_slot":
			_on_support_slot_pressed(index)
		"skill_inv":
			if player and player.equip_skill_from_inventory(index):
				refresh()
		"support_inv":
			if player and player.equip_support_from_inventory(index):
				refresh()


func _perform_drag(source_type: String, source_index: int, target_type: String, target_index: int) -> void:
	if player == null:
		return

	if source_type == "skill_inv" and target_type == "skill_slot":
		player.equip_skill_from_inventory(source_index)
	elif source_type == "skill_slot" and target_type == "skill_inv":
		player.swap_skill_with_inventory(target_index)
	elif source_type == "skill_inv" and target_type == "skill_inv":
		player.swap_skill_gem_inventory(source_index, target_index)
	elif source_type == "support_inv" and target_type == "support_slot":
		if not player.swap_support_with_inventory(target_index, source_index):
			player.equip_support_from_inventory(source_index)
	elif source_type == "support_slot" and target_type == "support_inv":
		player.swap_support_with_inventory(source_index, target_index)
	elif source_type == "support_slot" and target_type == "support_slot":
		build.swap_support_slots(source_index, target_index)
	elif source_type == "support_inv" and target_type == "support_inv":
		player.swap_support_gem_inventory(source_index, target_index)

	refresh()


func show_tooltip(text: String) -> void:
	if item_tooltip == null or tooltip_label == null:
		return

	tooltip_label.text = text
	item_tooltip.visible = true
	_position_tooltip()


func _position_tooltip() -> void:
	if item_tooltip == null:
		return
	var mouse_pos := get_global_mouse_position()
	item_tooltip.global_position = mouse_pos + Vector2(15, 15)

	var screen_size := get_viewport_rect().size
	var tip_size := item_tooltip.get_combined_minimum_size()
	if item_tooltip.global_position.x + tip_size.x > screen_size.x:
		item_tooltip.global_position.x = mouse_pos.x - tip_size.x - 15
	if item_tooltip.global_position.y + tip_size.y > screen_size.y:
		item_tooltip.global_position.y = mouse_pos.y - tip_size.y - 15


func hide_tooltip() -> void:
	if item_tooltip:
		item_tooltip.visible = false


func _format_gem_button_text(gem_name: String, level: int) -> String:
	var short_name := gem_name.substr(0, 4) if gem_name.length() > 4 else gem_name
	return _fmt("ui.skill.gem_button", {"name": short_name, "level": level}, "{name}\nLv{level}")


func _on_locale_changed(_locale: String) -> void:
	_apply_localized_texts()
	if visible:
		refresh()


func _apply_localized_texts() -> void:
	if title_label != null:
		title_label.text = _t("ui.panel.skills", "Skills")
	if skill_section_label != null:
		skill_section_label.text = _t("ui.panel.skills", "Skills")
	if support_section_label != null:
		support_section_label.text = _t("ui.lobby.category.support_gems", "Support Gems")
	if skill_inventory_label != null:
		skill_inventory_label.text = _t("ui.lobby.category.skill_gems", "Skill Gems")
	if support_inventory_label != null:
		support_inventory_label.text = _t("ui.lobby.category.support_gems", "Support Gems")
	var nav := panel_vbox.get_node_or_null("NavTabs") as HBoxContainer
	if nav != null:
		for tab in _nav_tabs():
			var tab_id := str(tab.get("id", ""))
			var button := nav.get_node_or_null("Nav_%s" % tab_id) as Button
			if button != null:
				button.text = _t(str(tab.get("label_key", "")), str(tab.get("fallback", tab_id)))


func _nav_tabs() -> Array[Dictionary]:
	return [
		{"id": "equipment", "label_key": "ui.panel.equipment", "fallback": "Equipment"},
		{"id": "module", "label_key": "ui.panel.modules", "fallback": "Modules"},
		{"id": "skill", "label_key": "ui.panel.skills", "fallback": "Skills"},
		{"id": "crafting", "label_key": "ui.panel.crafting", "fallback": "Crafting"},
	]


func _t(key: String, fallback: String) -> String:
	return LocalizationService.text(key, fallback)


func _fmt(key: String, replacements: Dictionary, fallback: String) -> String:
	return LocalizationService.format(key, replacements, fallback)
