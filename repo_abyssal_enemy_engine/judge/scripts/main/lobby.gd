extends Control

const GAME_SCENE := "res://scenes/main/game.tscn"
const LOBBY_BUILD_PANEL_COORDINATOR := preload("res://scripts/main/lobby/lobby_build_panel_coordinator.gd")
const LOBBY_BUILD_SESSION_SERVICE := preload("res://scripts/main/lobby/lobby_build_session_service.gd")
const LOBBY_GRID_RENDERER := preload("res://scripts/main/lobby/lobby_grid_renderer.gd")
const LOBBY_GUIDE_PANEL_SCENE := preload("res://scenes/ui/lobby_guide_panel.tscn")
const LOBBY_PRESENTER := preload("res://scripts/main/lobby/lobby_presenter.gd")
const LOBBY_PREP_SERVICE := preload("res://scripts/main/lobby/lobby_prep_service.gd")
const LOBBY_STATE_SERVICE := preload("res://scripts/main/lobby/lobby_state_service.gd")
const LOBBY_TOOLTIP_PRESENTER := preload("res://scripts/main/lobby/lobby_tooltip_presenter.gd")
const LOBBY_VIEW_BINDER := preload("res://scripts/main/lobby/lobby_view_binder.gd")
const SLOT_SIZE := Vector2(48, 48)
const GRID_COLUMNS := 7
const MIN_GRID_SLOTS := 63
const BEACON_GRID_COLUMNS := 3
const BEACON_CARD_SIZE := Vector2(190, 96)
const MIN_BEACON_SLOTS := 6

@onready var title_label: Label = $RootPanel/Content/Title
@onready var subtitle_label: Label = $RootPanel/Content/Subtitle
@onready var operation_level_text: Label = $RootPanel/Content/ConfigRow/OperationCard/OperationCardBody/OperationLevelText
@onready var depth_text: Label = $RootPanel/Content/ConfigRow/DepthCard/DepthCardBody/DepthText
@onready var lives_text: Label = $RootPanel/Content/ConfigRow/LivesCard/LivesCardBody/LivesText
const LOOT_CATEGORIES: Array[Dictionary] = [
	{"key": "equipment", "label_key": "ui.lobby.category.equipment", "fallback": "Equipment"},
	{"key": "skill_gems", "label_key": "ui.lobby.category.skill_gems", "fallback": "Skill Gems"},
	{"key": "support_gems", "label_key": "ui.lobby.category.support_gems", "fallback": "Support Gems"},
	{"key": "modules", "label_key": "ui.lobby.category.modules", "fallback": "Modules"},
]

@onready var stash_total_label: Label = $PrepOverlay/PrepPanel/PrepContent/SummaryRow/SummaryLeft/SummaryLeftBody/StashTotalLabel
@onready var stash_material_list: RichTextLabel = $PrepOverlay/PrepPanel/PrepContent/SummaryRow/SummaryLeft/SummaryLeftBody/StashMaterialList
@onready var stash_loot_total_label: Label = $PrepOverlay/PrepPanel/PrepContent/SummaryRow/SummaryRight/SummaryRightBody/StashLootTotalLabel
@onready var loadout_total_label: Label = $PrepOverlay/PrepPanel/PrepContent/SummaryRow/SummaryRight/SummaryRightBody/LoadoutTotalLabel
@onready var root_content: VBoxContainer = $RootPanel/Content
@onready var buttons_row: HBoxContainer = $RootPanel/Content/ButtonsRow
@onready var config_row: HBoxContainer = $RootPanel/Content/ConfigRow
@onready var beacon_title_label: Label = $RootPanel/Content/BeaconCard/BeaconCardBody/BeaconTitle
@onready var beacon_count_label: Label = $RootPanel/Content/BeaconCard/BeaconCardBody/BeaconCount
@onready var beacon_grid: GridContainer = $RootPanel/Content/BeaconCard/BeaconCardBody/BeaconScroll/BeaconGrid
@onready var beacon_summary_label: Label = $RootPanel/Content/BeaconCard/BeaconCardBody/BeaconSummary
@onready var prep_title_label: Label = $PrepOverlay/PrepPanel/PrepContent/PrepHeader/PrepTitle
@onready var stash_category: OptionButton = $PrepOverlay/PrepPanel/PrepContent/LootRow/StashSide/StashBody/StashCategory
@onready var loadout_category: OptionButton = $PrepOverlay/PrepPanel/PrepContent/LootRow/LoadoutSide/LoadoutBody/LoadoutCategory
@onready var stash_title_label: Label = $PrepOverlay/PrepPanel/PrepContent/LootRow/StashSide/StashBody/StashTitle
@onready var stash_grid: GridContainer = $PrepOverlay/PrepPanel/PrepContent/LootRow/StashSide/StashBody/StashScroll/StashGrid
@onready var loadout_title_label: Label = $PrepOverlay/PrepPanel/PrepContent/LootRow/LoadoutSide/LoadoutBody/LoadoutTitle
@onready var loadout_equipped_label: Label = $PrepOverlay/PrepPanel/PrepContent/LootRow/LoadoutSide/LoadoutBody/LoadoutScroll/LoadoutSections/EquippedLabel
@onready var loadout_equipped_grid: GridContainer = $PrepOverlay/PrepPanel/PrepContent/LootRow/LoadoutSide/LoadoutBody/LoadoutScroll/LoadoutSections/LoadoutEquippedGrid
@onready var loadout_inventory_label: Label = $PrepOverlay/PrepPanel/PrepContent/LootRow/LoadoutSide/LoadoutBody/LoadoutScroll/LoadoutSections/InventoryLabel
@onready var loadout_inventory_grid: GridContainer = $PrepOverlay/PrepPanel/PrepContent/LootRow/LoadoutSide/LoadoutBody/LoadoutScroll/LoadoutSections/LoadoutInventoryGrid
@onready var add_button: Button = $PrepOverlay/PrepPanel/PrepContent/LootRow/ActionSide/ActionBody/AddButton
@onready var quick_equip_button: Button = $PrepOverlay/PrepPanel/PrepContent/LootRow/ActionSide/ActionBody/QuickEquipButton
@onready var remove_button: Button = $PrepOverlay/PrepPanel/PrepContent/LootRow/ActionSide/ActionBody/RemoveButton
@onready var clear_loadout_button: Button = $PrepOverlay/PrepPanel/PrepContent/LootRow/ActionSide/ActionBody/ClearLoadoutButton
@onready var transfer_hint_label: Label = $PrepOverlay/PrepPanel/PrepContent/LootRow/ActionSide/ActionBody/TransferHint
@onready var start_button: Button = $RootPanel/Content/ButtonsRow/StartButton
@onready var quit_button: Button = $RootPanel/Content/ButtonsRow/QuitButton
@onready var prep_toggle_button: Button = $RootPanel/Content/PrepToggleRow/PrepToggleButton
@onready var prep_overlay: Control = $PrepOverlay
@onready var prep_close_button: Button = $PrepOverlay/PrepPanel/PrepContent/PrepHeader/PrepCloseButton

var _lobby_grid_renderer: LobbyGridRenderer = LOBBY_GRID_RENDERER.new()
var _lobby_build_panel_coordinator = LOBBY_BUILD_PANEL_COORDINATOR.new()
var _lobby_build_session = LOBBY_BUILD_SESSION_SERVICE.new()
var _lobby_presenter = LOBBY_PRESENTER.new()
var _lobby_prep_service: LobbyPrepService = LOBBY_PREP_SERVICE.new()
var _lobby_state_service: LobbyStateService = LOBBY_STATE_SERVICE.new()
var _lobby_tooltip_presenter: LobbyTooltipPresenter = LOBBY_TOOLTIP_PRESENTER.new()
var _lobby_view_binder: LobbyViewBinder = LOBBY_VIEW_BINDER.new()
var _build_buttons: Dictionary = {}
var _guide_button: Button = null
var _guide_panel: Control = null
var _top_tools_row: HBoxContainer = null
var _language_row: HBoxContainer = null
var _language_label: Label = null
var _language_option: OptionButton = null


func _ready() -> void:
	prep_overlay.visible = false
	_setup_build_tools()
	_setup_guide_panel()
	_setup_language_selector()
	_apply_localized_texts()
	_lobby_presenter.setup(
		self,
		_lobby_prep_service,
		_lobby_state_service,
		_lobby_grid_renderer,
		_lobby_tooltip_presenter,
		_lobby_view_binder,
		LOOT_CATEGORIES,
		SLOT_SIZE,
		GRID_COLUMNS,
		MIN_GRID_SLOTS,
		BEACON_GRID_COLUMNS,
		BEACON_CARD_SIZE,
		MIN_BEACON_SLOTS,
		stash_total_label,
		stash_material_list,
		stash_loot_total_label,
		loadout_total_label,
		config_row,
		beacon_count_label,
		beacon_grid,
		beacon_summary_label,
		stash_category,
		loadout_category,
		stash_grid,
		loadout_equipped_grid,
		loadout_inventory_grid,
		add_button,
		quick_equip_button,
		remove_button,
		clear_loadout_button,
		start_button,
		prep_toggle_button
	)
	_lobby_presenter.setup_controls()
	_bind_actions()
	_lobby_presenter.refresh_beacon_inventory()
	_lobby_presenter.refresh_all()
	if not LocalizationService.locale_changed.is_connected(_on_locale_changed):
		LocalizationService.locale_changed.connect(_on_locale_changed)
	TutorialService.register_lobby(self)


func _process(_delta: float) -> void:
	_lobby_presenter.process()


func _bind_actions() -> void:
	stash_category.item_selected.connect(Callable(_lobby_presenter, "on_stash_category_selected"))
	loadout_category.item_selected.connect(Callable(_lobby_presenter, "on_loadout_category_selected"))
	add_button.pressed.connect(Callable(_lobby_presenter, "move_selected_stash_to_loadout"))
	quick_equip_button.pressed.connect(Callable(_lobby_presenter, "quick_equip_selected_stash"))
	remove_button.pressed.connect(Callable(_lobby_presenter, "move_selected_loadout_to_stash"))
	clear_loadout_button.pressed.connect(Callable(_lobby_presenter, "clear_loadout"))
	start_button.pressed.connect(_on_start_pressed)
	quit_button.pressed.connect(_on_quit_pressed)
	prep_toggle_button.pressed.connect(_on_prep_toggle_pressed)
	prep_close_button.pressed.connect(_on_prep_close_pressed)
	EventBus.beacon_inventory_changed.connect(Callable(_lobby_presenter, "on_beacon_inventory_changed"))
	if _language_option != null and not _language_option.item_selected.is_connected(_on_language_option_selected):
		_language_option.item_selected.connect(_on_language_option_selected)


func _on_prep_toggle_pressed() -> void:
	prep_overlay.visible = true
	_lobby_presenter.refresh_all()
	TutorialService.notify_lobby_build_prep_opened(self)


func _on_prep_close_pressed() -> void:
	prep_overlay.visible = false
	_lobby_presenter.hide_tooltip()
	TutorialService.notify_lobby_build_prep_closed(self)


func _on_start_pressed() -> void:
	_commit_lobby_build_session()
	if not _lobby_presenter.start_selected_beacon(GameManager.OperationType.NORMAL):
		return
	TutorialService.notify_operation_started()
	get_tree().change_scene_to_file(GAME_SCENE)


func _on_quit_pressed() -> void:
	_commit_lobby_build_session()
	get_tree().quit()


func _on_guide_pressed() -> void:
	if _guide_panel != null and _guide_panel.has_method("open"):
		_guide_panel.call("open")


func _on_guide_replay_requested() -> void:
	TutorialService.restart_lobby_intro(self)


func _on_guide_reset_requested() -> void:
	TutorialService.reset_all_progress()
	TutorialService.restart_lobby_intro(self)


func _setup_build_tools() -> void:
	_lobby_build_session.setup(self)
	_lobby_prep_service.set_build_session_service(_lobby_build_session)
	_lobby_build_panel_coordinator.setup(
		self,
		Callable(self, "_on_build_panel_navigate"),
		Callable(self, "_on_build_panel_closed")
	)
	_create_build_button_row()


func _setup_guide_panel() -> void:
	if _guide_panel == null:
		_guide_panel = LOBBY_GUIDE_PANEL_SCENE.instantiate() as Control
		if _guide_panel != null:
			add_child(_guide_panel)
			if _guide_panel.has_signal("replay_requested") and not _guide_panel.is_connected("replay_requested", Callable(self, "_on_guide_replay_requested")):
				_guide_panel.connect("replay_requested", Callable(self, "_on_guide_replay_requested"))
			if _guide_panel.has_signal("reset_requested") and not _guide_panel.is_connected("reset_requested", Callable(self, "_on_guide_reset_requested")):
				_guide_panel.connect("reset_requested", Callable(self, "_on_guide_reset_requested"))
	_ensure_guide_button()


func _ensure_guide_button() -> void:
	if buttons_row == null or _guide_button != null:
		return
	_guide_button = Button.new()
	_guide_button.custom_minimum_size = Vector2(120, 44)
	if prep_toggle_button != null:
		for style_name in ["normal", "hover", "pressed", "focus"]:
			var stylebox := prep_toggle_button.get_theme_stylebox(style_name)
			if stylebox != null:
				_guide_button.add_theme_stylebox_override(style_name, stylebox)
	_guide_button.pressed.connect(_on_guide_pressed)
	buttons_row.add_child(_guide_button)
	if quit_button != null:
		buttons_row.move_child(_guide_button, quit_button.get_index())


func _setup_language_selector() -> void:
	if root_content == null:
		return
	if root_content.get_node_or_null("TopToolsRow") != null:
		_top_tools_row = root_content.get_node("TopToolsRow") as HBoxContainer
		_language_row = _top_tools_row.get_node_or_null("LanguageRow") as HBoxContainer
		_language_label = _language_row.get_node_or_null("LanguageLabel") as Label
		_language_option = _language_row.get_node_or_null("LanguageOption") as OptionButton
		return

	_top_tools_row = HBoxContainer.new()
	_top_tools_row.name = "TopToolsRow"
	_top_tools_row.alignment = BoxContainer.ALIGNMENT_END
	_top_tools_row.size_flags_horizontal = Control.SIZE_EXPAND_FILL
	_top_tools_row.add_theme_constant_override("separation", 0)
	root_content.add_child(_top_tools_row)
	root_content.move_child(_top_tools_row, 0)

	_language_row = HBoxContainer.new()
	_language_row.name = "LanguageRow"
	_language_row.alignment = BoxContainer.ALIGNMENT_END
	_language_row.add_theme_constant_override("separation", 8)
	_top_tools_row.add_child(_language_row)

	_language_label = Label.new()
	_language_label.name = "LanguageLabel"
	_language_label.vertical_alignment = VERTICAL_ALIGNMENT_CENTER
	_language_label.add_theme_color_override("font_color", Color(0.72, 0.86, 0.98, 0.96))
	_language_row.add_child(_language_label)

	_language_option = OptionButton.new()
	_language_option.name = "LanguageOption"
	_language_option.custom_minimum_size = Vector2(112, 28)
	_language_row.add_child(_language_option)


func _get_build_button_entries() -> Array[Dictionary]:
	return [
		{"id": "equipment", "label_key": "ui.panel.equipment", "fallback": "Equipment"},
		{"id": "skill", "label_key": "ui.panel.skills", "fallback": "Skills"},
		{"id": "module", "label_key": "ui.panel.modules", "fallback": "Modules"},
		{"id": "crafting", "label_key": "ui.panel.crafting", "fallback": "Crafting"},
	]


func _create_build_button_row() -> void:
	if root_content == null or buttons_row == null:
		return
	if root_content.get_node_or_null("BuildButtonsRow") != null:
		return
	var build_row := HFlowContainer.new()
	build_row.name = "BuildButtonsRow"
	build_row.alignment = FlowContainer.ALIGNMENT_CENTER
	build_row.add_theme_constant_override("separation", 8)
	root_content.add_child(build_row)
	root_content.move_child(build_row, buttons_row.get_index())

	for entry in _get_build_button_entries():
		var panel_id: String = str(entry.get("id", ""))
		var btn := Button.new()
		btn.text = _t(str(entry.get("label_key", "")), str(entry.get("fallback", panel_id)))
		btn.custom_minimum_size = Vector2(76, 30)
		btn.pressed.connect(_on_build_panel_pressed.bind(panel_id))
		build_row.add_child(btn)
		_build_buttons[panel_id] = btn


func _on_build_panel_pressed(panel_id: String) -> void:
	var preview_player: Player = _lobby_build_session.begin_preview_session()
	if preview_player == null:
		return
	_lobby_build_panel_coordinator.toggle(panel_id, preview_player)


func _on_build_panel_navigate(panel_id: String) -> void:
	var preview_player: Player = _lobby_build_session.begin_preview_session()
	if preview_player == null:
		return
	_lobby_build_panel_coordinator.handle_navigation(panel_id, preview_player)


func _on_build_panel_closed() -> void:
	call_deferred("_finalize_build_session_if_idle")


func _finalize_build_session_if_idle() -> void:
	if _lobby_build_panel_coordinator.has_visible_panel():
		return
	_lobby_build_session.commit_preview()
	_lobby_presenter.refresh_all()


func _commit_lobby_build_session() -> void:
	_lobby_build_panel_coordinator.close_all()
	_lobby_build_session.commit_and_discard_preview()


func _on_locale_changed(_locale: String) -> void:
	_apply_localized_texts()
	_lobby_presenter.refresh_localized_text()
	_lobby_presenter.refresh_all()


func _on_language_option_selected(index: int) -> void:
	if _language_option == null:
		return
	var locale := str(_language_option.get_item_metadata(index))
	if locale.is_empty():
		return
	LocalizationService.set_locale(locale)


func _apply_localized_texts() -> void:
	if title_label != null:
		title_label.text = "Abyssal Walker"
	if subtitle_label != null:
		subtitle_label.text = _t("ui.lobby.subtitle", "Quick Deploy")
	if operation_level_text != null:
		operation_level_text.text = _t("ui.lobby.operation_level", "Base Difficulty")
	if depth_text != null:
		depth_text.text = _t("ui.lobby.max_depth", "Max Depth")
	if lives_text != null:
		lives_text.text = _t("ui.lobby.lives", "Lives")
	if beacon_title_label != null:
		beacon_title_label.text = _t("ui.lobby.beacon_inventory", "Beacon Inventory")
	if prep_title_label != null:
		prep_title_label.text = _t("ui.lobby.build_preparation", "Build Preparation")
	if prep_close_button != null:
		prep_close_button.text = _t("common.close", "Close")
	if stash_title_label != null:
		stash_title_label.text = _t("ui.lobby.stash", "Stash")
	if loadout_title_label != null:
		loadout_title_label.text = _t("ui.lobby.current_build", "Current Build")
	if loadout_equipped_label != null:
		loadout_equipped_label.text = _t("ui.lobby.equipped", "Equipped")
	if loadout_inventory_label != null:
		loadout_inventory_label.text = _t("ui.lobby.build_inventory", "Build Inventory")
	if transfer_hint_label != null:
		transfer_hint_label.text = _t("ui.lobby.transfer_hint", "Select an item then transfer.")
	if quit_button != null:
		quit_button.text = _t("ui.lobby.quit", "Quit")
	if _guide_button != null:
		_guide_button.text = _guide_button_text()
	_refresh_language_selector()
	_refresh_build_button_labels()


func _refresh_build_button_labels() -> void:
	for entry in _get_build_button_entries():
		var panel_id := str(entry.get("id", ""))
		var button := _build_buttons.get(panel_id) as Button
		if button == null:
			continue
		button.text = _t(str(entry.get("label_key", "")), str(entry.get("fallback", panel_id)))


func _refresh_language_selector() -> void:
	if _language_label != null:
		_language_label.text = _t("ui.lobby.language", "Language")
	if _language_option == null:
		return
	_language_option.set_block_signals(true)
	_language_option.clear()
	var locales := LocalizationService.get_supported_locales()
	for locale in locales:
		_language_option.add_item(_locale_display_name(locale))
		_language_option.set_item_metadata(_language_option.item_count - 1, locale)
	var current_locale := LocalizationService.get_locale()
	for i in range(_language_option.item_count):
		if str(_language_option.get_item_metadata(i)) == current_locale:
			_language_option.select(i)
			break
	_language_option.set_block_signals(false)


func _locale_display_name(locale: String) -> String:
	return _t("ui.locale.%s" % locale, locale)


func _t(key: String, fallback: String) -> String:
	return LocalizationService.text(key, fallback)


func _guide_button_text() -> String:
	return _t("ui.lobby.guide", "Guide")


func get_tutorial_anchor(anchor_id: String) -> Control:
	match anchor_id:
		"prep_toggle":
			return prep_toggle_button
		"quick_equip":
			return quick_equip_button
		"start_button":
			return start_button
		"beacon_card":
			return $RootPanel/Content/BeaconCard
	return null


func is_build_prep_open() -> bool:
	return prep_overlay != null and prep_overlay.visible
