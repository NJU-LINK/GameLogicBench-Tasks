class_name LobbyViewBinder
extends RefCounted


func apply_summary(
	stash_total_label: Label,
	stash_material_list: RichTextLabel,
	stash_loot_total_label: Label,
	loadout_total_label: Label,
	model: Dictionary
) -> void:
	if stash_total_label != null:
		stash_total_label.text = _fmt(
			"ui.lobby.crafting_materials",
			{"count": int(model.get("stash_material_total", 0))},
			"Crafting Materials: {count}"
		)
	if stash_material_list != null:
		stash_material_list.text = "\n".join(model.get("material_lines", []))

	var stash_counts: Dictionary = model.get("stash_counts", {})
	var loadout_counts: Dictionary = model.get("loadout_counts", {})
	if stash_loot_total_label != null:
		stash_loot_total_label.text = _fmt(
			"ui.lobby.stash_loot_summary",
			{
				"equipment": int(stash_counts.get("equipment", 0)),
				"skill_gems": int(stash_counts.get("skill_gems", 0)),
				"support_gems": int(stash_counts.get("support_gems", 0)),
				"modules": int(stash_counts.get("modules", 0)),
			},
			"Stash Loot: Eq {equipment} | Skill {skill_gems} | Support {support_gems} | Module {modules}"
		)
	if loadout_total_label != null:
		loadout_total_label.text = _fmt(
			"ui.lobby.build_loot_summary",
			{
				"equipment": int(loadout_counts.get("equipment", 0)),
				"skill_gems": int(loadout_counts.get("skill_gems", 0)),
				"support_gems": int(loadout_counts.get("support_gems", 0)),
				"modules": int(loadout_counts.get("modules", 0)),
			},
			"Build: Eq {equipment} | Skill {skill_gems} | Support {support_gems} | Module {modules}"
		)


func apply_beacon_inventory_count(label: Label, inventory_count: int) -> void:
	if label == null:
		return
	label.text = _fmt(
		"ui.lobby.owned_beacons",
		{"count": inventory_count},
		"Owned Beacons: {count}"
	)
	if inventory_count == 0:
		label.text += "  |  %s" % _t("ui.lobby.baseline_ready", "Baseline Ready")


func apply_beacon_preview(label: Label, preview_text: String) -> void:
	if label != null:
		label.text = preview_text


func apply_start_button(button: Button, has_selected_beacon: bool, beacon_consumes: bool) -> void:
	if button == null:
		return
	button.disabled = not has_selected_beacon
	button.text = _t("ui.lobby.activate_beacon", "Activate Beacon") if beacon_consumes else _t("ui.lobby.start_baseline", "Start Baseline Dive")


func _t(key: String, fallback: String) -> String:
	return LocalizationService.text(key, fallback)


func _fmt(key: String, replacements: Dictionary, fallback: String) -> String:
	return LocalizationService.format(key, replacements, fallback)
