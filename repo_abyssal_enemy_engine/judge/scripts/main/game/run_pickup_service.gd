class_name RunPickupService
extends RefCounted


func try_pickup_item(player: Player, item_data: Variant) -> bool:
	if player == null:
		return false

	if item_data is EquipmentData:
		return _pickup_equipment(player, item_data as EquipmentData)
	if item_data is SkillGem:
		return _pickup_skill_gem(player, item_data as SkillGem)
	if item_data is SupportGem:
		return _pickup_support_gem(player, item_data as SupportGem)
	if item_data is Module:
		return _pickup_module(player, item_data as Module)
	if item_data is Dictionary:
		return _pickup_material(player, item_data as Dictionary)
	return false


func _pickup_equipment(player: Player, equipment: EquipmentData) -> bool:
	if not player.add_to_inventory(equipment):
		print("Inventory full: %s" % equipment.display_name)
		return false
	var rarity_name: String = StatTypes.RARITY_NAMES.get(equipment.rarity, "Unknown")
	print("Picked equipment: %s [%s]" % [equipment.display_name, rarity_name])
	GameManager.add_loot_to_run_backpack(equipment)
	EventBus.item_picked_up.emit(equipment)
	return true


func _pickup_skill_gem(player: Player, gem: SkillGem) -> bool:
	if not player.add_skill_gem_to_inventory(gem):
		print("Skill Gem inventory full: %s" % gem.display_name)
		return false
	print("Picked skill gem: %s" % gem.display_name)
	GameManager.add_loot_to_run_backpack(gem)
	EventBus.item_picked_up.emit(gem)
	return true


func _pickup_support_gem(player: Player, gem: SupportGem) -> bool:
	if not player.add_support_gem_to_inventory(gem):
		print("Support Gem inventory full: %s" % gem.display_name)
		return false
	print("Picked support gem: %s" % gem.display_name)
	GameManager.add_loot_to_run_backpack(gem)
	EventBus.item_picked_up.emit(gem)
	return true


func _pickup_module(player: Player, mod: Module) -> bool:
	if not player.add_module_to_inventory(mod):
		print("Module inventory full: %s" % mod.display_name)
		return false
	print("Picked module: %s (load %d)" % [mod.display_name, mod.load_cost])
	GameManager.add_loot_to_run_backpack(mod)
	EventBus.item_picked_up.emit(mod)
	return true


func _pickup_material(player: Player, item_data: Dictionary) -> bool:
	var mat_id: String = str(item_data.get("material_id", ""))
	var amount: int = int(item_data.get("amount", 1))
	if mat_id == "":
		return false
	player.add_material(mat_id, amount)
	var mat_data: Dictionary = DataManager.get_crafting_material(mat_id)
	var name: String = str(mat_data.get("display_name", mat_id))
	print("Picked material: %s x%d" % [name, amount])
	EventBus.item_picked_up.emit({
		"material_id": mat_id,
		"amount": amount,
	})
	return true
