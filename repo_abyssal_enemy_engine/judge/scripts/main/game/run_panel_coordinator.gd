class_name RunPanelCoordinator
extends RefCounted

var ui_layer: CanvasLayer = null
var navigate_handler: Callable = Callable()
var equipment_panel: EquipmentPanel = null
var skill_link_panel: SkillLinkPanel = null
var crafting_panel: CraftingPanel = null
var module_panel: ModulePanel = null


func setup(
	target_ui_layer: CanvasLayer,
	equipment_scene: PackedScene,
	skill_link_scene: PackedScene,
	crafting_scene: PackedScene,
	module_scene: PackedScene,
	on_navigate: Callable = Callable()
) -> void:
	ui_layer = target_ui_layer
	navigate_handler = on_navigate
	equipment_panel = _instantiate_panel(equipment_scene) as EquipmentPanel
	skill_link_panel = _instantiate_panel(skill_link_scene) as SkillLinkPanel
	module_panel = _instantiate_panel(module_scene) as ModulePanel
	_configure_run_only_panels()


func handle_navigation(panel_id: String, player: Player) -> void:
	close_all()
	open(panel_id, player)


func close_all() -> void:
	for panel in [equipment_panel, skill_link_panel, crafting_panel, module_panel]:
		if panel and panel.visible and panel.has_method("close"):
			panel.call("close")


func toggle(panel_id: String, player: Player) -> void:
	var panel := panel_by_id(panel_id)
	if panel == null:
		return
	if panel.visible:
		if panel.has_method("close"):
			panel.call("close")
		return
	open(panel_id, player)


func open(panel_id: String, player: Player) -> void:
	match panel_id:
		"equipment":
			if equipment_panel:
				equipment_panel.open(player)
		"skill":
			if skill_link_panel:
				skill_link_panel.open(player)
		"module":
			if module_panel:
				module_panel.open(player)


func panel_by_id(panel_id: String) -> Control:
	match panel_id:
		"equipment":
			return equipment_panel
		"skill":
			return skill_link_panel
		"module":
			return module_panel
	return null


func _instantiate_panel(scene: PackedScene) -> Control:
	if scene == null or ui_layer == null:
		return null
	var panel := scene.instantiate() as Control
	if panel == null:
		return null
	ui_layer.add_child(panel)
	if navigate_handler.is_valid() and panel.has_signal("navigate_to"):
		panel.connect("navigate_to", navigate_handler)
	return panel


func _configure_run_only_panels() -> void:
	for panel in [equipment_panel, skill_link_panel, module_panel]:
		if panel != null and panel.has_method("set_allow_crafting_navigation"):
			panel.call("set_allow_crafting_navigation", false)
