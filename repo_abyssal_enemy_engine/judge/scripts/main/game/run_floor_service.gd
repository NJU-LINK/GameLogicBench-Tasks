class_name RunFloorService
extends RefCounted

const BEACON_MODIFIER_SYSTEM := preload("res://scripts/abyss/beacon_modifier_system.gd")


func start_floor(
	enemy_spawner: EnemySpawner,
	player: Player,
	floor_number: int,
	progression_service: RunProgressionService,
	default_objective_type: int,
	boss_objective_type: int
) -> void:
	if enemy_spawner == null or player == null:
		return

	progression_service.begin_floor(floor_number, default_objective_type)
	GameManager.enter_floor(floor_number)

	var effective_level: int = GameManager.get_effective_drop_level(floor_number)
	var config: Dictionary = DataManager.get_floor_config(effective_level)
	if config.is_empty():
		config = DataManager.get_floor_config(1)
	config = config.duplicate(true)

	progression_service.configure_floor_objective(
		floor_number,
		GameManager.get_max_depth(),
		boss_objective_type,
		config
	)
	config = BEACON_MODIFIER_SYSTEM.apply_floor_config_modifiers(config, GameManager.get_modifier_ids())
	config = BEACON_MODIFIER_SYSTEM.apply_floor_event(config)
	_announce_floor_event(config)

	enemy_spawner.setup(config, player)
	enemy_spawner.set_floor_number(floor_number)
	enemy_spawner.set_effective_level(effective_level)
	enemy_spawner.spawn_wave()

	progression_service.activate_floor()


func _announce_floor_event(config: Dictionary) -> void:
	var event_name: String = str(config.get("active_floor_event_name", ""))
	if event_name.is_empty():
		return
	var event_summary: String = str(config.get("active_floor_event_summary", ""))
	var message: String = "Floor Event: %s" % event_name
	if not event_summary.is_empty():
		message += " - %s" % event_summary
	EventBus.notification_requested.emit(message, "warning")
