class_name PlayerAttackTargetingService
extends RefCounted


func get_melee_targets(player: Player, support_mods: Dictionary) -> Array[Node2D]:
	if player == null:
		return []
	var target_node: Node2D = player._get_current_target_node2d()
	if target_node == null:
		return []

	if player.gem_link == null or player.gem_link.skill_gem == null:
		if is_target_in_melee_range(player, target_node, player.get_attack_range()):
			return single_target_array(target_node)
		return []

	var skill: SkillGem = player.gem_link.skill_gem
	var area_multiplier: float = float(support_mods.get("area_multiplier", 1.0))
	var radius: float = player.get_attack_range() * maxf(area_multiplier, 0.1)

	if skill.id == "shadow_strike":
		if is_target_in_melee_range(player, target_node, player.get_attack_range()):
			return single_target_array(target_node)
		return []

	if not skill.has_tag(StatTypes.SkillTag.AOE):
		if is_target_in_melee_range(player, target_node, player.get_attack_range()):
			return single_target_array(target_node)
		return []

	if skill.id == "whirlwind":
		return get_enemies_in_circle(player, player.global_position, radius)

	var forward: Vector2 = (target_node.global_position - player.global_position).normalized()
	if forward == Vector2.ZERO:
		forward = Vector2.RIGHT
	return get_enemies_in_cone(player, player.global_position, forward, radius, 120.0)


func is_target_in_melee_range(player: Player, target_node: Node2D, max_range: float) -> bool:
	if player == null:
		return false
	if target_node == null or not is_instance_valid(target_node):
		return false
	var effective_range: float = get_melee_target_reach_distance(player, target_node, max_range)
	return player.global_position.distance_squared_to(target_node.global_position) <= effective_range * effective_range


func single_target_array(target_node: Node2D) -> Array[Node2D]:
	var result: Array[Node2D] = []
	if target_node != null and is_instance_valid(target_node):
		result.append(target_node)
	return result


func get_alive_enemies(player: Player) -> Array[Node2D]:
	var result: Array[Node2D] = []
	if player == null:
		return result
	for enemy: Node in player.get_tree().get_nodes_in_group("enemies"):
		if enemy is Node2D and is_alive_enemy(enemy):
			result.append(enemy as Node2D)
	return result


func is_alive_enemy(enemy: Node) -> bool:
	if enemy == null or not is_instance_valid(enemy):
		return false
	if enemy.has_method("is_dead") and enemy.is_dead():
		return false
	return true


func get_enemies_in_circle(player: Player, center: Vector2, radius: float) -> Array[Node2D]:
	var result: Array[Node2D] = []
	for enemy_node: Node2D in get_alive_enemies(player):
		var enemy_radius: float = get_body_radius(enemy_node)
		var effective_radius: float = radius + enemy_radius
		if enemy_node.global_position.distance_squared_to(center) <= effective_radius * effective_radius:
			result.append(enemy_node)
	return result


func get_enemies_in_cone(
	player: Player,
	center: Vector2,
	forward: Vector2,
	radius: float,
	angle_deg: float
) -> Array[Node2D]:
	var result: Array[Node2D] = []
	var dir: Vector2 = forward.normalized()
	var min_dot: float = cos(deg_to_rad(angle_deg * 0.5))

	for enemy_node: Node2D in get_alive_enemies(player):
		var to_enemy: Vector2 = enemy_node.global_position - center
		var enemy_radius: float = get_body_radius(enemy_node)
		var effective_radius: float = radius + enemy_radius
		var dist_sq: float = to_enemy.length_squared()
		if dist_sq > effective_radius * effective_radius:
			continue

		var dist: float = sqrt(dist_sq)
		if dist <= enemy_radius:
			result.append(enemy_node)
			continue

		var ratio: float = clampf(enemy_radius / dist, 0.0, 0.95)
		var angle_slack: float = asin(ratio)
		var dot_threshold: float = cos(deg_to_rad(angle_deg * 0.5) + angle_slack)
		var dot: float = dir.dot(to_enemy / dist)
		if dot >= minf(min_dot, dot_threshold):
			result.append(enemy_node)

	return result


func resolve_melee_range(player: Player, max_range: float) -> float:
	if player == null:
		return maxf(max_range, 0.0)
	if max_range > 0.0:
		return max_range
	return maxf(player.get_attack_range(), 0.0)


func get_melee_target_reach_distance(player: Player, target_node: Node2D, max_range: float) -> float:
	var resolved_range: float = maxf(max_range, 0.0)
	if target_node == null or not is_instance_valid(target_node):
		return resolved_range
	return resolved_range + get_body_radius(target_node)


func get_body_radius(node: Node) -> float:
	if node == null:
		return 10.0
	var shape_node: Node = node.get_node_or_null("CollisionShape2D")
	if shape_node is CollisionShape2D:
		var collision_node: CollisionShape2D = shape_node as CollisionShape2D
		if collision_node.shape is CircleShape2D:
			return (collision_node.shape as CircleShape2D).radius
	return 10.0


func find_arc_chain_target(player: Player, from_target: Node2D, hit_targets: Dictionary) -> Node2D:
	var best: Node2D = null
	var best_dist_sq: float = INF
	var max_dist_sq: float = player.ARC_CHAIN_SEARCH_RADIUS * player.ARC_CHAIN_SEARCH_RADIUS

	for enemy_node: Node2D in get_alive_enemies(player):
		if enemy_node == from_target:
			continue
		var key: String = str(enemy_node.get_instance_id())
		if hit_targets.has(key):
			continue
		var dist_sq: float = enemy_node.global_position.distance_squared_to(from_target.global_position)
		if dist_sq > max_dist_sq:
			continue
		if dist_sq < best_dist_sq:
			best_dist_sq = dist_sq
			best = enemy_node

	return best