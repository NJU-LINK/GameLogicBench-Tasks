class_name LootFilterService
extends RefCounted

const MODE_ALL := 0
const MODE_MAGIC_PLUS := 1
const MODE_RARE_ONLY := 2
const MODE_GEMS_AND_MODULES := 3

var loot_filter_mode: int = MODE_ALL


func set_mode(mode: int) -> int:
	loot_filter_mode = clampi(mode, MODE_ALL, MODE_GEMS_AND_MODULES)
	return loot_filter_mode


func cycle_mode() -> int:
	var next_mode: int = loot_filter_mode + 1
	if next_mode > MODE_GEMS_AND_MODULES:
		next_mode = MODE_ALL
	return set_mode(next_mode)


func get_mode_name() -> String:
	match loot_filter_mode:
		MODE_ALL:
			return "All"
		MODE_MAGIC_PLUS:
			return "Magic+"
		MODE_RARE_ONLY:
			return "Rare+"
		MODE_GEMS_AND_MODULES:
			return "Gems/Modules"
		_:
			return "All"


func should_show_loot(item: Variant) -> bool:
	match loot_filter_mode:
		MODE_ALL:
			return true
		MODE_MAGIC_PLUS:
			if item is EquipmentData:
				return item.rarity >= StatTypes.Rarity.BLUE
			return true
		MODE_RARE_ONLY:
			if item is EquipmentData:
				return item.rarity >= StatTypes.Rarity.YELLOW
			return true
		MODE_GEMS_AND_MODULES:
			return item is SkillGem or item is SupportGem or item is Module
		_:
			return true
