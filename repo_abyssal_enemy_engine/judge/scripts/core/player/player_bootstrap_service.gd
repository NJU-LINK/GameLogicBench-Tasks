class_name PlayerBootstrapService
extends RefCounted

const STARTER_AUTO_EQUIP_SLOTS: Array[int] = [
	StatTypes.EquipmentSlot.HELMET,
	StatTypes.EquipmentSlot.ARMOR,
	StatTypes.EquipmentSlot.BOOTS,
	StatTypes.EquipmentSlot.BELT,
]

func setup_initial_build(
	player: Player,
	debug_grant_all_skill_gems: bool,
	debug_grant_all_support_gems: bool
) -> void:
	if player == null:
		return

	if GameManager.has_persistent_player_build():
		GameManager.apply_persistent_player_build_to_player(player)
		_apply_debug_gem_grants(player, debug_grant_all_skill_gems, debug_grant_all_support_gems)
		GameManager.save_persistent_player_build_from_player(player)
		return

	_grant_starter_equipment(player)

	for id in DataManager.get_starter_skill_gem_ids():
		var gem: SkillGem = DataManager.create_skill_gem(id)
		if gem != null:
			player.add_skill_gem_to_inventory(gem)

	for id in DataManager.get_starter_support_gem_ids():
		var support: SupportGem = DataManager.create_support_gem(id)
		if support != null:
			player.add_support_gem_to_inventory(support)

	_apply_debug_gem_grants(player, debug_grant_all_skill_gems, debug_grant_all_support_gems)

	for id in DataManager.get_starter_module_ids():
		var mod: Module = DataManager.create_module(id)
		if mod != null:
			player.add_module_to_inventory(mod)

	GameManager.save_persistent_player_build_from_player(player)


func _apply_debug_gem_grants(
	player: Player,
	debug_grant_all_skill_gems: bool,
	debug_grant_all_support_gems: bool
) -> void:
	if debug_grant_all_skill_gems:
		_grant_all_skill_gems_for_testing(player)
	if debug_grant_all_support_gems:
		_grant_all_support_gems_for_testing(player)


func _grant_starter_equipment(player: Player) -> void:
	var starter_ids: Array[String] = DataManager.get_starter_equipment_ids()
	var primary_weapon_equipped: bool = false
	for i in range(starter_ids.size()):
		var base_id: String = starter_ids[i]
		var equipment: EquipmentData = ItemGenerator.generate_equipment(base_id, StatTypes.Rarity.WHITE, 1)
		if equipment == null:
			continue
		if equipment.slot == StatTypes.EquipmentSlot.MAIN_HAND and not primary_weapon_equipped:
			var replaced_item: EquipmentData = player.equip(equipment)
			if replaced_item != null:
				player.add_to_inventory(replaced_item)
			primary_weapon_equipped = true
			continue
		if _should_auto_equip_starter_item(player, equipment):
			var displaced_item: EquipmentData = player.equip(equipment)
			if displaced_item != null:
				player.add_to_inventory(displaced_item)
			continue
		player.add_to_inventory(equipment)


func _should_auto_equip_starter_item(player: Player, equipment: EquipmentData) -> bool:
	if player == null or equipment == null:
		return false
	if not STARTER_AUTO_EQUIP_SLOTS.has(equipment.slot):
		return false
	return player.get_equipped(equipment.slot) == null


func _grant_all_skill_gems_for_testing(player: Player) -> void:
	for id in DataManager.get_all_skill_gem_ids():
		if player.has_skill_gem_with_id(id):
			continue
		var gem: SkillGem = DataManager.create_skill_gem(id)
		if gem != null:
			player.add_skill_gem_to_inventory(gem)


func _grant_all_support_gems_for_testing(player: Player) -> void:
	for id in DataManager.get_all_support_gem_ids():
		if player.has_support_gem_with_id(id):
			continue
		var gem: SupportGem = DataManager.create_support_gem(id)
		if gem != null:
			player.add_support_gem_to_inventory(gem)
