class_name PlayerRuntimeBridge
extends RefCounted


func emit_event_bus(owner: Node, signal_name: StringName, args: Array = []) -> void:
	var event_bus: Node = _get_event_bus(owner)
	if event_bus == null:
		return
	var parameters: Array = [signal_name]
	parameters.append_array(args)
	event_bus.callv("emit_signal", parameters)


func set_stash_material_count(owner: Node, material_id: String, count: int) -> void:
	var game_manager: Node = _get_game_manager(owner)
	if game_manager == null:
		return
	game_manager.call("set_stash_material_count", material_id, count)


func _resolve_tree(owner: Node) -> SceneTree:
	if owner != null and owner.is_inside_tree():
		return owner.get_tree()
	var main_loop: MainLoop = Engine.get_main_loop()
	return main_loop as SceneTree


func _get_event_bus(owner: Node) -> Node:
	var tree: SceneTree = _resolve_tree(owner)
	if tree == null or tree.root == null:
		return null
	return tree.root.get_node_or_null(^"/root/EventBus")


func _get_game_manager(owner: Node) -> Node:
	var tree: SceneTree = _resolve_tree(owner)
	if tree == null or tree.root == null:
		return null
	return tree.root.get_node_or_null(^"/root/GameManager")