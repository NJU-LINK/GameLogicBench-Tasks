class_name LobbyTooltipPresenter
extends RefCounted

var tooltip_panel: PanelContainer = null
var tooltip_label: RichTextLabel = null


func setup(owner: Control) -> void:
	if owner == null or tooltip_panel != null:
		return

	tooltip_panel = PanelContainer.new()
	tooltip_panel.mouse_filter = Control.MOUSE_FILTER_IGNORE
	tooltip_panel.top_level = true
	tooltip_panel.visible = false
	tooltip_panel.z_index = 100

	var tip_style := StyleBoxFlat.new()
	tip_style.bg_color = Color(0.05, 0.05, 0.1, 0.95)
	tip_style.border_color = Color(0.5, 0.5, 0.7)
	tip_style.set_border_width_all(1)
	tip_style.set_corner_radius_all(4)
	tooltip_panel.add_theme_stylebox_override("panel", tip_style)

	var margin := MarginContainer.new()
	margin.add_theme_constant_override("margin_left", 10)
	margin.add_theme_constant_override("margin_right", 10)
	margin.add_theme_constant_override("margin_top", 10)
	margin.add_theme_constant_override("margin_bottom", 10)
	tooltip_panel.add_child(margin)

	tooltip_label = RichTextLabel.new()
	tooltip_label.bbcode_enabled = true
	tooltip_label.fit_content = true
	tooltip_label.scroll_active = false
	tooltip_label.custom_minimum_size = Vector2(260, 0)
	margin.add_child(tooltip_label)

	owner.add_child(tooltip_panel)


func is_visible() -> bool:
	return tooltip_panel != null and tooltip_panel.visible


func show_item(item: Variant, mouse_pos: Vector2, screen_size: Vector2) -> void:
	if tooltip_panel == null or tooltip_label == null:
		return
	tooltip_label.text = build_item_tooltip(item)
	tooltip_panel.visible = true
	update_position(mouse_pos, screen_size)


func hide() -> void:
	if tooltip_panel != null:
		tooltip_panel.visible = false


func update_position(mouse_pos: Vector2, screen_size: Vector2) -> void:
	if tooltip_panel == null:
		return
	var tip_size := tooltip_panel.get_combined_minimum_size()
	var pos := mouse_pos + Vector2(16, 16)
	if pos.x + tip_size.x > screen_size.x:
		pos.x = mouse_pos.x - tip_size.x - 16
	if pos.y + tip_size.y > screen_size.y:
		pos.y = mouse_pos.y - tip_size.y - 16
	tooltip_panel.global_position = pos


func build_item_tooltip(item: Variant) -> String:
	if item is EquipmentData:
		return (item as EquipmentData).get_tooltip()
	if item is SkillGem:
		return (item as SkillGem).get_tooltip()
	if item is SupportGem:
		return (item as SupportGem).get_tooltip()
	if item is Module:
		return _build_module_tooltip(item as Module)
	return _t("common.unknown", "Unknown")


func _build_module_tooltip(mod: Module) -> String:
	if mod == null:
		return _t("common.unknown", "Unknown")
	var lines: Array[String] = []
	lines.append("[color=#%s][b]%s[/b][/color]" % [mod.get_type_color().to_html(false), mod.display_name])
	lines.append("[color=gray]%s[/color]" % _fmt("ui.module.tooltip_line", {"type": mod.get_type_name(), "load": mod.load_cost}, "{type} | Load {load}"))
	if mod.description != "":
		lines.append("")
		lines.append(mod.description)
	if not mod.modifiers.is_empty():
		lines.append("")
		lines.append("%s:" % _t("common.modifiers", "Modifiers"))
		for stat_mod in mod.modifiers:
			if stat_mod is StatModifier:
				lines.append("- %s" % (stat_mod as StatModifier).get_description())
	return "\n".join(lines)


func _t(key: String, fallback: String) -> String:
	return LocalizationService.text(key, fallback)


func _fmt(key: String, replacements: Dictionary, fallback: String) -> String:
	return LocalizationService.format(key, replacements, fallback)
