class_name RunSceneFlowService
extends RefCounted

var extraction_window_active: bool = false
var extraction_selected: bool = false
var extraction_decided: bool = false
var run_summary_waiting_return: bool = false
var run_summary_return_confirmed: bool = false


func handle_key_input(keycode: int) -> bool:
	if run_summary_waiting_return and (
		keycode == KEY_E
		or keycode == KEY_ENTER
		or keycode == KEY_ESCAPE
		or keycode == KEY_SPACE
	):
		run_summary_return_confirmed = true
		return true

	if extraction_window_active and keycode == KEY_E:
		extraction_selected = true
		extraction_decided = true
		return true

	if extraction_window_active and keycode == KEY_F:
		extraction_selected = false
		extraction_decided = true
		return true

	return false


func confirm_run_summary() -> void:
	if run_summary_waiting_return:
		run_summary_return_confirmed = true


func reset_after_death() -> void:
	extraction_window_active = false
	extraction_selected = false
	extraction_decided = false


func prepare_for_lobby_return(player: Player) -> void:
	extraction_window_active = false
	extraction_selected = false
	extraction_decided = false
	run_summary_waiting_return = false
	run_summary_return_confirmed = false
	if player != null and is_instance_valid(player):
		player.clamp_health_to_max()


func run_extraction_window(
	owner: Node,
	hud: Control,
	floor_number: int,
	duration: float,
	player: Player
) -> bool:
	extraction_window_active = true
	extraction_selected = false
	extraction_decided = false
	GameManager.open_extraction_window(floor_number, duration)
	if hud != null and hud.has_method("set_extraction_prompt"):
		hud.set_extraction_prompt(
			true,
			_format_extraction_prompt(int(ceili(duration)))
		)
	await TutorialService.maybe_show_first_extraction_tip(hud)

	var elapsed: float = 0.0
	while elapsed < duration and not extraction_decided:
		var remaining_sec: int = maxi(0, int(ceili(duration - elapsed)))
		if hud != null and hud.has_method("set_extraction_prompt"):
			hud.set_extraction_prompt(
				true,
				_format_extraction_prompt(remaining_sec)
			)
		await owner.get_tree().process_frame
		elapsed += owner.get_process_delta_time()

	extraction_window_active = false
	if hud != null and hud.has_method("set_extraction_prompt"):
		hud.set_extraction_prompt(false, "")
	GameManager.close_extraction_window(floor_number, extraction_selected, player)
	return extraction_selected


func _format_extraction_prompt(seconds: int) -> String:
	return LocalizationService.format(
		"ui.hud.extraction_prompt_window",
		{"seconds": seconds},
		"Extraction window ({seconds}s left)\n[E] Extract now    [F] Continue\nNo choice: auto-continue"
	)


func wait_for_run_summary(
	owner: Node,
	hud: Control,
	title: String,
	body: String,
	timeout_ms: int
) -> void:
	run_summary_waiting_return = true
	run_summary_return_confirmed = false
	if hud != null and hud.has_method("show_run_summary"):
		hud.show_run_summary(title, body)

	var started_ms: int = Time.get_ticks_msec()
	while not run_summary_return_confirmed:
		if Time.get_ticks_msec() - started_ms >= timeout_ms:
			push_warning("Run summary confirm timeout, continue to lobby automatically.")
			run_summary_return_confirmed = true
			break
		await owner.get_tree().process_frame

	run_summary_waiting_return = false
	if hud != null and hud.has_method("hide_run_summary"):
		hud.hide_run_summary()


func build_failure_summary_body(summary: Dictionary) -> String:
	var lost_loot: Dictionary = summary.get("loot_lost", {})
	return "Lost loot:\n- Equipment: %d\n- Gems: %d\n- Modules: %d" % [
		int(lost_loot.get("equipment", 0)),
		int(lost_loot.get("total_gems", 0)),
		int(lost_loot.get("modules", 0)),
	]


func build_extraction_summary_body(summary: Dictionary) -> String:
	var moved: Dictionary = summary.get("loot_moved", {})
	var stash_total: int = int(summary.get("stash_total", 0))
	return "Moved to stash:\n- Equipment: %d\n- Gems: %d\n- Modules: %d\n- Materials in stash: %d" % [
		int(moved.get("equipment", 0)),
		int(moved.get("total_gems", 0)),
		int(moved.get("modules", 0)),
		stash_total,
	]
