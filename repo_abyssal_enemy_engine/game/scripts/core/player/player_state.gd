class_name PlayerState
extends RefCounted

var equipment: Dictionary = {}
var inventory: Array = []
var skill_gem_inventory: Array = []
var support_gem_inventory: Array = []
var equipped_skill_gem: SkillGem = null
var equipped_support_gems: Array = []
var module_inventory: Array = []
var equipped_modules: Array = []


func is_empty() -> bool:
	if not equipment.is_empty():
		return false
	if equipped_skill_gem != null:
		return false
	return (
		inventory.is_empty()
		and skill_gem_inventory.is_empty()
		and support_gem_inventory.is_empty()
		and equipped_support_gems.is_empty()
		and module_inventory.is_empty()
		and equipped_modules.is_empty()
	)


func clear() -> void:
	equipment.clear()
	inventory.clear()
	skill_gem_inventory.clear()
	support_gem_inventory.clear()
	equipped_skill_gem = null
	equipped_support_gems.clear()
	module_inventory.clear()
	equipped_modules.clear()


func capture_from_player(player: Player) -> void:
	if player == null or not player.can_snapshot_build():
		clear()
		return

	clear()
	for slot_id in Player.EQUIPMENT_SLOT_ORDER:
		var equipped_item: EquipmentData = player.get_equipped(slot_id)
		if equipped_item != null:
			equipment[slot_id] = equipped_item.duplicate(true)

	inventory = _clone_resource_array(player.inventory)
	skill_gem_inventory = _clone_resource_array(player.skill_gem_inventory)
	support_gem_inventory = _clone_resource_array(player.support_gem_inventory)
	if player.gem_link != null and player.gem_link.skill_gem != null:
		equipped_skill_gem = player.gem_link.skill_gem.duplicate(true)
	equipped_support_gems = _clone_resource_array(player.gem_link.support_gems)
	module_inventory = _clone_resource_array(player.module_inventory)
	equipped_modules = _clone_resource_array(player.core_board.slots)


func load_snapshot(snapshot: Dictionary) -> void:
	clear()
	if snapshot.is_empty():
		return

	equipment = _clone_equipment_map(snapshot.get("equipment", {}))
	inventory = _clone_resource_array(snapshot.get("inventory", []))
	skill_gem_inventory = _clone_resource_array(snapshot.get("skill_gem_inventory", []))
	support_gem_inventory = _clone_resource_array(snapshot.get("support_gem_inventory", []))

	var skill_data: Variant = snapshot.get("equipped_skill_gem", null)
	if skill_data is SkillGem:
		equipped_skill_gem = (skill_data as SkillGem).duplicate(true)

	equipped_support_gems = _clone_resource_array(snapshot.get("equipped_support_gems", []))
	module_inventory = _clone_resource_array(snapshot.get("module_inventory", []))
	equipped_modules = _clone_resource_array(snapshot.get("equipped_modules", []))


func to_snapshot() -> Dictionary:
	return {
		"equipment": _clone_equipment_map(equipment),
		"inventory": _clone_resource_array(inventory),
		"skill_gem_inventory": _clone_resource_array(skill_gem_inventory),
		"support_gem_inventory": _clone_resource_array(support_gem_inventory),
		"equipped_skill_gem": _duplicate_item(equipped_skill_gem),
		"equipped_support_gems": _clone_resource_array(equipped_support_gems),
		"module_inventory": _clone_resource_array(module_inventory),
		"equipped_modules": _clone_resource_array(equipped_modules),
	}


func duplicate_state() -> PlayerState:
	var copy := PlayerState.new()
	copy.load_snapshot(to_snapshot())
	return copy


func apply_to_player(player: Player) -> void:
	if player == null or not player.can_snapshot_build():
		return

	player.clear_build_state()

	for slot_id in Player.EQUIPMENT_SLOT_ORDER:
		var item_data: Variant = equipment.get(slot_id, null)
		if item_data is EquipmentData:
			var equipped_item: EquipmentData = (item_data as EquipmentData).duplicate(true)
			equipped_item.slot = slot_id
			player.equip(equipped_item)

	for item_data: Variant in inventory:
		if item_data is EquipmentData:
			player.add_to_inventory((item_data as EquipmentData).duplicate(true))

	for i in range(mini(skill_gem_inventory.size(), player.MAX_SKILL_GEM_INVENTORY)):
		var skill_item: Variant = skill_gem_inventory[i]
		if skill_item is SkillGem:
			player.set_skill_gem_in_inventory(i, (skill_item as SkillGem).duplicate(true))

	for i in range(mini(support_gem_inventory.size(), player.MAX_SUPPORT_GEM_INVENTORY)):
		var support_item: Variant = support_gem_inventory[i]
		if support_item is SupportGem:
			player.set_support_gem_in_inventory(i, (support_item as SupportGem).duplicate(true))

	if equipped_skill_gem != null:
		player.gem_link.set_skill_gem(equipped_skill_gem.duplicate(true))

	for i in range(mini(equipped_support_gems.size(), Constants.MAX_SUPPORT_GEMS)):
		var support_data: Variant = equipped_support_gems[i]
		if support_data is SupportGem:
			player.gem_link.set_support_gem(i, (support_data as SupportGem).duplicate(true))

	for module_data: Variant in equipped_modules:
		if module_data is Module:
			var mod: Module = (module_data as Module).duplicate_module()
			if not player.core_board.equip(mod, player.stats):
				player.add_module_to_inventory(mod)

	for module_data: Variant in module_inventory:
		if module_data is Module:
			player.add_module_to_inventory((module_data as Module).duplicate_module())

	player.restore_health_to_max()


func _clone_equipment_map(source: Variant) -> Dictionary:
	var cloned: Dictionary = {}
	if not (source is Dictionary):
		return cloned
	for slot_key in source.keys():
		var slot_id: int = int(slot_key)
		var value: Variant = source[slot_key]
		if value is EquipmentData:
			cloned[slot_id] = (value as EquipmentData).duplicate(true)
	return cloned


func _clone_resource_array(source: Variant) -> Array:
	var cloned: Array = []
	if not (source is Array):
		return cloned
	for value: Variant in source:
		cloned.append(_duplicate_item(value))
	return cloned


func _duplicate_item(value: Variant) -> Variant:
	if value == null:
		return null
	if value is Module:
		return (value as Module).duplicate_module()
	if value is Resource:
		return (value as Resource).duplicate(true)
	return value
