class_name RunFlowService
extends RefCounted

var extraction_interval: int = 3
var extraction_window_open: bool = false


func _init(default_extraction_interval: int = 3) -> void:
	extraction_interval = maxi(1, default_extraction_interval)


func reset() -> void:
	extraction_window_open = false


func should_open_extraction_window(floor_number: int) -> bool:
	if floor_number <= 0:
		return false
	return floor_number % extraction_interval == 0


func open_extraction_window() -> void:
	extraction_window_open = true


func close_extraction_window(
	floor_number: int,
	extracted: bool,
	player: Player,
	inventory_service,
	records_service
) -> Dictionary:
	extraction_window_open = false
	if not extracted:
		return {}
	inventory_service.preserve_equipped_run_equipment(player)
	inventory_service.strip_run_backpack_loot_from_player(player)
	var moved_loot: Dictionary = inventory_service.deposit_run_backpack_loot_to_stash()
	var summary: Dictionary = {
		"floor": floor_number,
		"materials_carried": 0,
		"stash_total": inventory_service.get_stash_material_total(),
		"loot_moved": moved_loot,
		"stash_loot": inventory_service.get_stash_loot_counts(),
	}
	records_service.record_extracted_summary(summary)
	return records_service.get_last_run_extracted_summary()


func apply_death_material_penalty(player: Player, inventory_service, records_service) -> Dictionary:
	if player == null:
		return {
			"kept": 0,
			"lost": 0,
		}
	var kept_total: int = player.get_total_material_count()
	inventory_service.strip_run_backpack_loot_from_player(player)
	var lost_loot: Dictionary = inventory_service.lose_run_backpack_loot()
	var summary: Dictionary = {
		"kept": kept_total,
		"lost": 0,
		"stash_total": inventory_service.get_stash_material_total(),
		"loot_lost": lost_loot,
	}
	records_service.record_failed_summary(summary)
	return records_service.get_last_run_failed_summary()
