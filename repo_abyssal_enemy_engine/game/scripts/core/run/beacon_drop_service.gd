class_name BeaconDropService
extends RefCounted

const BEACON_MODIFIER_SYSTEM := preload("res://scripts/abyss/beacon_modifier_system.gd")


func collect_beacon_drops(
	floor_number: int,
	enemy: EnemyBase,
	modifier_ids: PackedStringArray
) -> Array[Resource]:
	if enemy == null:
		return []

	var gained_beacons: Array[Resource] = []
	var rolled_beacon: Resource = DropSystem.roll_beacon_drop_for_floor(floor_number, enemy)
	if rolled_beacon != null:
		gained_beacons.append(rolled_beacon)

	if not enemy.is_boss:
		return gained_beacons

	var summary: Dictionary = BEACON_MODIFIER_SYSTEM.summarize(modifier_ids)
	var extra_boss_beacons: int = maxi(0, int(summary.get("boss_bonus_beacons", 0)))
	for i in range(extra_boss_beacons):
		var extra_beacon: Resource = DropSystem.create_guaranteed_beacon_for_floor(floor_number, enemy)
		if extra_beacon != null:
			gained_beacons.append(extra_beacon)

	return gained_beacons


func build_inventory_notification(beacons: Array[Resource]) -> String:
	if beacons.is_empty():
		return ""
	if beacons.size() == 1:
		var display_name: String = _get_beacon_display_name(beacons[0])
		return "信標入庫：%s" % display_name
	return "信標入庫：%d" % beacons.size()


func _get_beacon_display_name(beacon: Resource) -> String:
	if beacon == null:
		return "Abyss Beacon"
	var value: Variant = beacon.get("display_name")
	if value == null:
		return "Abyss Beacon"
	return str(value)
