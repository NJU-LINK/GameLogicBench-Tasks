extends RefCounted
# 黑盒运行时可观测量：只读世界的可观测量（单位格、目标格、墙、边界），从不读控制器内部。
# judge.gd 用这些量拼出 pass / deadlock / timeout 判定。step 机制在 sim_core.gd。

const DIRS := [Vector2i(0, -1), Vector2i(0, 1), Vector2i(-1, 0), Vector2i(1, 0)]

# 全员是否同时位于各自目标格（到达 == 完成目标编队）。
static func all_arrived(pos: Array, goals: Array) -> bool:
	for i in range(pos.size()):
		if pos[i] != goals[i]:
			return false
	return true

# 从 goal 出发、在自由格（非墙、界内）上做 BFS，得到 cell -> 到 goal 的最短步数。
# 单位到目标的"净进展"用它度量：dist_map[pos_i] 越小越接近目标。纯几何、与其他单位无关。
static func bfs_dist_map(goal: Vector2i, walls: Dictionary, gw: int, gh: int) -> Dictionary:
	var dist := {goal: 0}
	var q: Array = [goal]
	var qi := 0
	while qi < q.size():
		var cur: Vector2i = q[qi]
		qi += 1
		var dc: int = dist[cur]
		for v in DIRS:
			var nb: Vector2i = cur + v
			if nb.x < 0 or nb.x >= gw or nb.y < 0 or nb.y >= gh:
				continue
			if walls.has(nb) or dist.has(nb):
				continue
			dist[nb] = dc + 1
			q.append(nb)
	return dist
