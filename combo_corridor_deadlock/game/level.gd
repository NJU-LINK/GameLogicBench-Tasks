extends RefCounted
#
# level.gd -- builds the practice arena for the F5 preview (framework code; your AI does not read
# this file, it only ever sees the per-tick `state`). Two units cross a walled integer grid to
# their goal cells. The seed decides small placement details, so the layout differs from one play
# to the next.
#
# The arena is an integer grid with solid boundary + interior walls. `build()` returns a spec dict.
# This preview arena lays out two separate lanes so the two units never contend for a cell -- the
# simple example you develop and debug against.

const GRID_W := 16
const GRID_H := 9
const MAX_TICKS := 50

static func build(rng: RandomNumberGenerator, _scenario: String = "", _press: String = "") -> Dictionary:
	var ytop := 2
	var ybot := 6
	var free: Array = []
	free.append_array(_hline(1, GRID_W - 2, ytop))
	free.append_array(_hline(1, GRID_W - 2, ybot))
	var g0 := (GRID_W - 2) - rng.randi_range(0, 1)   # 目标列微调（seed 安全带）
	var g1 := 1 + rng.randi_range(0, 1)
	var starts := [Vector2i(1, ytop), Vector2i(GRID_W - 2, ybot)]
	var goals := [Vector2i(g0, ytop), Vector2i(g1, ybot)]
	return _assemble(GRID_W, GRID_H, free, starts, goals, MAX_TICKS)

static func _hline(x0: int, x1: int, y: int) -> Array:
	var s: Array = []
	for x in range(x0, x1 + 1):
		s.append(Vector2i(x, y))
	return s

# 由自由格清单反推墙集（界内非自由 == 墙），组装 spec。
static func _assemble(gw: int, gh: int, free: Array, starts: Array, goals: Array, max_ticks: int) -> Dictionary:
	var freeset := {}
	for c in free:
		freeset[c] = true
	var walls: Array = []
	for y in range(gh):
		for x in range(gw):
			var c := Vector2i(x, y)
			if not freeset.has(c):
				walls.append(c)
	return {
		"grid_w": gw, "grid_h": gh, "walls": walls,
		"starts": starts, "goals": goals, "max_ticks": max_ticks,
	}
