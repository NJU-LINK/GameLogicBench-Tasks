extends RefCounted
#
# level.gd -- builds the production scenario for the preview (framework code; the arena the game
# lays out when you press F5). One factory building on an open field, a starting fund, and the
# stream of order events that will arrive while you play. The rng perturbs the numbers from one
# play to the next: how much money you start with, which items get ordered, and when.

const SimCore = preload("res://sim_core.gd")

const KINDS := ["cheap", "mid", "heavy"]

static func build(rng: RandomNumberGenerator) -> Dictionary:
	# funds always cover any three items (max 3 * heavy 8 = 24)
	var funds: int = rng.randi_range(24, 32)
	var orders: Array = []
	var f: int = rng.randi_range(10, 20)
	for i in 3:
		var kind: String = KINDS[rng.randi_range(0, 2)]
		orders.append({"frame": f, "op": "enqueue", "kind": kind})
		# next order arrives well after the longest possible build (240f) has finished
		f += 250 + rng.randi_range(0, 10)
	return {
		"world_w": SimCore.WORLD_W,
		"world_h": SimCore.WORLD_H,
		"factory_pos": SimCore.FACTORY_POS,
		"factory_half": SimCore.FACTORY_HALF,
		"funds": funds,
		"orders": orders,
	}
