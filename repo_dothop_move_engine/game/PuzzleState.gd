@tool
extends Object
class_name PuzzleState

# ------------------------------------------------------------------------------------------------
# PuzzleState — the grid-hop puzzle's WORLD (board + hoppers). Framework scaffolding vendored from
# russmatney/dothop (MIT, see NOTICE_UPSTREAM.md); build your engine on top, it is not part of your
# deliverable.
#
# This is the DATA MODEL and the low-level world API only: the grid of cells (each holding dots /
# a goal / a hopper / undo markers), the list of hoppers, grid geometry queries, and the primitive
# "set this cell's contents" mutations. It holds NO rules: it does not know how a hop slides, how a
# move is undone, how several hoppers move together, or when the puzzle is won. Those rules are the
# move engine you deliver (res://engine/move_engine.gd), which drives THIS world through the API
# below.
#
# The vendored data classes (Cell / Player / Move) and the MoveResult / MoveType enums are kept as
# the shared vocabulary between the world and the engine; the engine may use them or ignore them.
# ------------------------------------------------------------------------------------------------

## player

class Player:
	var coord: Vector2
	var stuck := false
	var move_history: Array = []

	signal move_to_cell(c: Cell)
	signal undo_to_cell(c: Cell)
	signal undo_to_same_cell(c: Cell)
	signal move_attempt_stuck(dir: Vector2)

	func _init(crd: Vector2) -> void:
		coord = crd

	func to_pretty() -> Variant:
		return ["P", coord, stuck, move_history]

	func previous_undo_coord(skip_coord: Vector2, start_at: int = 0) -> Variant:
		# pulls the first coord from player history that does not match `skip_coord`,
		# starting after `start_at`
		for m: Vector2 in move_history.slice(start_at):
			if m != skip_coord:
				return m
		return


## cell

class Cell:
	var objs: Array[DHData.Obj]
	var coord: Vector2

	signal mark_dotted
	signal mark_undotted

	signal show_possible_next_move
	signal show_possible_undo
	signal remove_possible_next_move

	func _init(_coord: Vector2, _objs: Array[DHData.Obj]) -> void:
		coord = _coord
		objs = _objs

	func to_pretty() -> Variant:
		return [coord, objs]

	func has_player() -> bool:
		return DHData.Obj.Player in objs
	func has_dot() -> bool:
		return DHData.Obj.Dot in objs
	func has_dotted() -> bool:
		return DHData.Obj.Dotted in objs
	func has_dot_or_dotted() -> bool:
		return has_dot() or has_dotted()
	func has_goal() -> bool:
		return DHData.Obj.Goal in objs
	func has_dot_or_dotted_or_goal() -> bool:
		return has_dot_or_dotted() or has_goal()
	func has_undo() -> bool:
		return DHData.Obj.Undo in objs


## move (a per-hopper move attempt — shared vocabulary; the engine may use it or its own)

class Move:
	var move_direction: Vector2
	var player: Player
	var type: MoveType
	var cell: Cell

	var hopped_cells: Array[Cell] = []

	func _init(dir: Vector2, p: Player) -> void:
		move_direction = dir
		type = MoveType.unknown
		player = p

	func to_pretty() -> Variant:
		return [type, player, cell]

	func mark_undo(c: Cell) -> void:
		type = MoveType.undo
		cell = c
	func mark_stuck() -> void:
		type = MoveType.stuck
	func mark_blocked_by_player() -> void:
		type = MoveType.blocked_by_player
	func move_to_dot(c: Cell) -> void:
		type = MoveType.move_to_dot
		cell = c
	func move_to_goal(c: Cell) -> void:
		type = MoveType.move_to_goal
		cell = c
	func add_dot_to_hop(c: Cell) -> void:
		hopped_cells.append(c)

	func is_move() -> bool:
		return type in [MoveType.move_to_dot, MoveType.move_to_goal]
	func is_undo() -> bool:
		return type == MoveType.undo
	func is_stuck() -> bool:
		return type == MoveType.stuck
	func is_blocked() -> bool:
		return type in [MoveType.blocked_by_player]

enum MoveType {
	unknown=0,
	stuck=1,
	blocked_by_player=2,
	move_to_dot=3,
	move_to_goal=4,
	undo=5,
	}

enum MoveResult {
	zero=0,
	move_not_allowed=1,
	stuck=2, # no legal destination in direction
	undo=3,
	moved=4,
	}

## state vars

var puzzle_def: PuzzleDef
var win := false
var grid_width: int
var grid_height: int

var players: Array[Player] = []
var cells_by_coord: Dictionary[Vector2, Cell] = {}

## win-condition knobs (read by the engine; the WORLD only stores them)
var _require_all_dotted := true
var _require_all_players_at_goal := true

func set_require_all_dots(required: bool) -> void:
	_require_all_dotted = required
func set_require_all_players_at_goal(required: bool) -> void:
	_require_all_players_at_goal = required
func require_all_dots() -> bool:
	return _require_all_dotted
func require_all_players_at_goal() -> bool:
	return _require_all_players_at_goal

## init — builds the board and the hopper list from a PuzzleDef. Holds no rules.

func _init(puzz_def: PuzzleDef) -> void:
	puzzle_def = puzz_def

	grid_height = puzzle_def.height
	grid_width = puzzle_def.width
	if grid_height <= 0:
		Log.warn("zero-heighted puzzle?", puzzle_def)
		Log.error("could not set grid_height! probably an empty world rn")

	for cell: Cell in puzzle_def.state_cells():
		cells_by_coord[cell.coord] = cell
		if cell.has_player():
			players.append(Player.new(cell.coord))

## state

func to_pretty() -> Variant:
	var _grid: Array = []
	for row in range(grid_height):
		_grid.append(get_grid_row_objs(row))
	return {state="state", grid=_grid}

## getters / geometry (read-only world queries)

func get_grid_row_objs(row: int) -> Array:
	var row_objs: Array = []
	for x in range(grid_width):
		var cell: Cell = cells_by_coord.get(Vector2(x, row))
		if cell == null:
			Log.warn("get_grid_row_objs: coord not in grid", Vector2(x, row))
			continue
		row_objs.append(cell.objs)
	return row_objs

func all_cells() -> Array[Cell]:
	return cells_by_coord.values()

# Returns a list of cell-object-arrays (an array per cell)
func all_cell_objs() -> Array:
	var cs: Array = []
	for cell: Cell in cells_by_coord.values():
		cs.append(cell.objs)
	return cs

func dot_count(only_undotted: bool = false) -> int:
	return len(all_cells().filter(func(c: Cell) -> bool:
		if only_undotted and c.has_dot():
			return true
		elif not only_undotted and c.has_dot_or_dotted():
			return true
		return false))

# returns true if the passed coord is in the level's grid
func coord_in_grid(coord: Vector2) -> bool:
	return coord.x >= 0 and coord.y >= 0 and \
		coord.x < grid_width and coord.y < grid_height

func cell_at_coord(coord: Vector2) -> Cell:
	return cells_by_coord.get(coord)

# returns a list of cells from the passed position in the passed direction
func cells_in_direction(coord: Vector2, dir: Vector2) -> Array[Cell]:
	if dir == Vector2.ZERO:
		return []
	var cells: Array[Cell] = []
	var cursor: Vector2 = coord + dir
	var last_cursor: Variant = null
	while coord_in_grid(cursor) and last_cursor != cursor:
		var c: Cell = cell_at_coord(cursor)
		if c != null:
			cells.append(c)
		last_cursor = cursor
		cursor += dir
	return cells

## primitive world mutations (the low-level "set a cell's contents" API the engine drives)
# these carry NO rules — they only edit the object list of one cell.

func mark_dotted(coord: Vector2) -> void:
	cells_by_coord[coord].objs.erase(DHData.Obj.Dot)
	cells_by_coord[coord].objs.append(DHData.Obj.Dotted)

func mark_undotted(coord: Vector2) -> void:
	cells_by_coord[coord].objs.erase(DHData.Obj.Dotted)
	cells_by_coord[coord].objs.append(DHData.Obj.Dot)

func mark_undo(coord: Vector2) -> void:
	if not cells_by_coord[coord].has_undo():
		cells_by_coord[coord].objs.append(DHData.Obj.Undo)

func drop_undo(coord: Vector2) -> void:
	cells_by_coord[coord].objs.erase(DHData.Obj.Undo)

func mark_player(coord: Vector2) -> void:
	cells_by_coord[coord].objs.append(DHData.Obj.Player)

func drop_player(coord: Vector2) -> void:
	cells_by_coord[coord].objs.erase(DHData.Obj.Player)
