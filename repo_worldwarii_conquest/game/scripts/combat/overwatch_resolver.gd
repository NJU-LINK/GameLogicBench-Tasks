class_name OverwatchResolver
extends RefCounted

const HexCoord := preload("res://scripts/grid/hex_coord.gd")
const HexMap := preload("res://scripts/grid/hex_map.gd")
const CombatResolver := preload("res://scripts/combat/combat_resolver.gd")
const CombatModifiers := preload("res://scripts/combat/combat_modifiers.gd")
const CombatEffects := preload("res://scripts/combat/combat_effects.gd")
const DamagePopup := preload("res://scripts/ui/damage_popup.gd")

static func trigger_along_path(
	mover,
	path: Array,
	units: Array,
	visibility_by_faction: Dictionary,
	hex_map,
	data_loader,
	action_log,
	turn_number: int,
	prompt_callback: Callable = Callable()
) -> int:
	# As `mover` passes through each hex along `path`, every watcher that
	# sees the hex and has it in attack range snap-shots the mover. Each
	# watcher fires at most once (on_overwatch consumed). Returns the
	# path index at which the mover died, or -1 if it survived.
	for i in range(1, path.size()):
		if not mover.is_alive():
			return i - 1
		var step: Vector2i = path[i]
		var step_world := HexCoord.to_pixel(step, HexMap.HEX_SIZE)
		for u in units:
			var watcher = u
			if not watcher.is_alive() or not watcher.on_overwatch:
				continue
			if watcher.faction_id == mover.faction_id:
				continue
			var w_vis: Dictionary = visibility_by_faction.get(watcher.faction_id, {})
			if not w_vis.has(step):
				continue
			var w_def: Dictionary = data_loader.get_unit_def(watcher.type_id)
			var w_rng := int(w_def.get("range", 1))
			if HexCoord.distance(watcher.coord, step) > w_rng:
				continue
			var dmg: int = compute_damage(watcher, mover, step, hex_map, data_loader)
			watcher.play_attack_animation(step_world)
			AudioBank.play("attack")
			mover.take_damage(dmg)
			var suppression := CombatEffects.suppression_for_attack(w_def, dmg, not mover.is_alive())
			mover.add_suppression(suppression)
			DamagePopup.spawn(hex_map, step_world, dmg, Color(1.0, 0.85, 0.4))
			if action_log != null:
				action_log.record_overwatch(watcher, mover, dmg, turn_number)
			watcher.on_overwatch = false
			watcher.queue_redraw()
			if prompt_callback.is_valid():
				prompt_callback.call("警戒射擊", "%s 射擊 %s @(%d,%d) → -%d" % [
					watcher.display_name, mover.display_name, step.x, step.y, dmg,
				])
			if not mover.is_alive():
				return i
	return -1

static func compute_damage(watcher, target, target_step: Vector2i, hex_map, data_loader) -> int:
	var w_def: Dictionary = data_loader.get_unit_def(watcher.type_id)
	var t_def: Dictionary = data_loader.get_unit_def(target.type_id)
	var w_terr: Dictionary = data_loader.get_terrain_def(hex_map.terrain_at(watcher.coord))
	var t_terr: Dictionary = data_loader.get_terrain_def(hex_map.terrain_at(target_step))
	var d := HexCoord.distance(watcher.coord, target_step)
	var w_general: Dictionary = data_loader.get_general_def(watcher.general_id)
	var t_general: Dictionary = data_loader.get_general_def(target.general_id)
	var w_mods: Dictionary = CombatModifiers.for_unit(watcher, w_general)
	var t_mods: Dictionary = CombatModifiers.for_unit(target, t_general)
	w_mods.attack -= CombatEffects.attack_penalty(watcher.suppression)
	var result := CombatResolver.resolve(
		w_def, t_def, watcher.hp, target.hp,
		w_terr, t_terr, d, target.dig_in_level,
		w_mods, t_mods,
	)
	return CombatEffects.overwatch_damage(result.damage_to_defender, w_def)
