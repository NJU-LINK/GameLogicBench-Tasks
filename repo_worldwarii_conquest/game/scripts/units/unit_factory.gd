class_name UnitFactory
extends RefCounted

const HexCoord := preload("res://scripts/grid/hex_coord.gd")
const HexMap := preload("res://scripts/grid/hex_map.gd")
const Unit := preload("res://scripts/units/unit.gd")
const CombatEffects := preload("res://scripts/combat/combat_effects.gd")
const CombatModifiers := preload("res://scripts/combat/combat_modifiers.gd")

# Builds Unit instances from a scenario JSON.
# Returns: { "units": Array[Unit], "factions": Dictionary[String, Dictionary] }

static func build(scenario: Dictionary, hex_map: HexMap) -> Dictionary:
	var factions := {}
	for f in scenario.get("factions", []):
		var fid := String(f.get("id", ""))
		factions[fid] = {
			"id": fid,
			"name": String(f.get("name", fid)),
			"controller": String(f.get("controller", "ai")),
			"color": Color(String(f.get("color", "#cccccc"))),
			"ai": String(f.get("ai", "")),
		}

	var units: Array = []
	for u in scenario.get("units", []):
		var unit := create_unit(u, factions)
		if unit != null:
			units.append(unit)

	return {"units": units, "factions": factions}

static func create_unit(data: Dictionary, factions: Dictionary) -> Unit:
	# Builds one Unit from a single scenario / reinforcement entry.
	# Returns null if `data` doesn't have the minimal `type` + `faction` keys.
	var type_id := String(data.get("type", ""))
	var faction_id := String(data.get("faction", ""))
	if type_id == "" or faction_id == "":
		return null
	var at_arr: Array = data.get("at", [0, 0])
	# scenario coords come in odd-r offset (col, row); convert to axial
	var col := int(at_arr[0])
	var row := int(at_arr[1])
	var coord := Vector2i(col - (row >> 1), row)
	var unit_name := String(data.get("name", ""))

	var unit: Unit = Unit.new()
	var color: Color = factions.get(faction_id, {}).get("color", Color.WHITE)
	unit.configure(type_id, faction_id, color, coord, unit_name)
	unit.scenario_unit_id = String(data.get("id", ""))
	unit.position = HexCoord.to_pixel(coord, HexMap.HEX_SIZE)
	# Optional: attach a named general (data lookup in CombatModifiers).
	var general_id := String(data.get("general", ""))
	if general_id != "":
		unit.general_id = general_id
	unit.roster_id = int(data.get("roster_id", -1))
	unit.hp = clampi(int(data.get("hp", unit.max_hp)), 1, unit.max_hp)
	unit.xp = max(0, int(data.get("xp", 0)))
	unit.rank = clampi(int(data.get("rank", 0)), 0, CombatModifiers.MAX_RANK)
	unit.suppression = clampi(int(data.get("suppression", 0)), 0, CombatEffects.MAX_SUPPRESSION)
	unit.dig_in_level = clampi(int(data.get("dig_in", 0)), 0, Unit.MAX_DIG_IN)
	unit.on_overwatch = bool(data.get("on_overwatch", false))
	unit.refresh_morale()  # seed morale_max from the final rank, filled
	return unit
