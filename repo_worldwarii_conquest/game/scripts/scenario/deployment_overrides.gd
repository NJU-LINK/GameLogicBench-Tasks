class_name DeploymentOverrides
extends RefCounted

static func unit_key(unit) -> String:
	return "%s|%s" % [String(unit.faction_id), String(unit.display_name)]

static func apply(units: Array, hex_map, overrides: Dictionary, hex_size: float) -> void:
	if overrides.is_empty():
		return
	var original_coords := {}
	var units_to_override: Array = []
	for u in units:
		var key := unit_key(u)
		if not overrides.has(key):
			continue
		original_coords[u] = u.coord
		units_to_override.append(u)
		if hex_map.occupants.get(u.coord) == u:
			hex_map.occupants.erase(u.coord)
	for u in units_to_override:
		var key := unit_key(u)
		var data: Dictionary = overrides.get(key, {})
		var coord := Vector2i(int(data.get("q", u.coord.x)), int(data.get("r", u.coord.y)))
		var fallback: Vector2i = original_coords.get(u, u.coord)
		if not hex_map.tiles.has(coord) or hex_map.occupants.has(coord):
			coord = fallback
		u.general_id = String(data.get("general_id", u.general_id))
		u.coord = coord
		u.position = _axial_to_pixel(coord, hex_size)
		hex_map.occupants[coord] = u
		u.queue_redraw()

static func _axial_to_pixel(coord: Vector2i, hex_size: float) -> Vector2:
	var x := hex_size * sqrt(3.0) * (coord.x + coord.y / 2.0)
	var y := hex_size * 1.5 * coord.y
	return Vector2(x, y)
