extends Control

const ConquestBattleSetup := preload("res://scripts/scenario/conquest_battle_setup.gd")
const HelpContent := preload("res://scripts/ui/help_content.gd")

@onready var title_label: Label = $Margin/VBox/Title
@onready var briefing_label: RichTextLabel = $Margin/VBox/BriefingScroll/Briefing
@onready var begin_button: Button = $Margin/VBox/Buttons/BeginButton
@onready var back_button: Button = $Margin/VBox/Buttons/BackButton

func _ready() -> void:
	if GameState.campaign_mode:
		back_button.text = "返回戰役地圖"
	elif GameState.conquest_mode:
		back_button.text = "返回征服地圖"
	else:
		back_button.text = "返回作戰列表"
	var scenario := DataLoader.get_scenario(GameState.current_scenario_id)
	if scenario.is_empty():
		title_label.text = "(找不到作戰)"
		briefing_label.text = "Scenario id 無效:" + GameState.current_scenario_id
	elif GameState.conquest_mode and not GameState.pending_conquest_battle.is_empty():
		# Conquest battles reuse a historical map but not its narrative — show a
		# briefing that matches the actual attacker/defender matchup instead.
		_show_conquest_briefing(scenario)
	else:
		title_label.text = String(scenario.get("title", scenario.get("id", "")))
		var briefing_text := String(scenario.get("briefing", "(無簡報)"))
		var tutorial_hints := HelpContent.tutorial_hint_bbcode(scenario.get("tutorial_mechanics", []))
		if tutorial_hints != "":
			briefing_text += "\n\n" + tutorial_hints
		briefing_label.text = briefing_text
	begin_button.pressed.connect(_on_begin_pressed)
	begin_button.tooltip_text = "開始戰鬥。" if GameState.campaign_mode else "進入戰前部署。"
	back_button.pressed.connect(_on_back_pressed)
	back_button.tooltip_text = back_button.text

func _show_conquest_briefing(scenario: Dictionary) -> void:
	var p := GameState.pending_conquest_battle
	var loc := String(p.get("battle_location", scenario.get("title", "")))
	var me := String(p.get("player_name", "我軍"))
	var foe := String(p.get("enemy_name", "敵軍"))
	var my_n: int = (p.get("attacker_garrison", []) as Array).size()
	var foe_n: int = (p.get("defender_types", []) as Array).size()
	if String(p.get("role", "attack")) == "attack":
		foe_n += (p.get("defender_support_types", []) as Array).size()
	# The themed scenario's own briefing describes the region's terrain — surface
	# it so each theatre's battlefield reads distinctly even though the matchup
	# text is generated.
	var terrain := String(scenario.get("briefing", ""))
	var terrain_line := ("\n\n地形:%s" % terrain) if terrain != "" else ""
	var prep_notes: Array = p.get("preparation_notes", [])
	var trait_notes: Array = p.get("region_trait_notes", [])
	var prep_line := ""
	var trait_line := ""
	var parts: Array[String] = []
	if not prep_notes.is_empty():
		for note in prep_notes:
			parts.append(String(note))
	if not trait_notes.is_empty():
		var trait_parts: Array[String] = []
		for note in trait_notes:
			trait_parts.append(String(note))
		trait_line = "\n\n區域特性:%s" % "、".join(trait_parts)
	if String(p.get("role", "attack")) == "defend":
		title_label.text = "%s 來犯 — 防守 %s" % [foe, loc]
		if not parts.is_empty():
			prep_line = "\n\n防禦準備:%s" % "、".join(parts)
		briefing_label.text = "戰場:%s%s%s%s\n\n%s 出動約 %d 支部隊進攻;你以 %d 支部隊據守。\n\n守住 12 回合或殲滅來犯者即可保住此地;守軍被殲滅則該地失守。" % [
			loc, terrain_line, prep_line, trait_line, foe, foe_n, my_n,
		]
	else:
		title_label.text = "%s 進攻 %s" % [me, foe]
		if not parts.is_empty():
			prep_line = "\n\n戰前準備:%s" % "、".join(parts)
		var attack_objective := ConquestBattleSetup.conquest_attack_objective_text(scenario)
		var limit := ConquestBattleSetup.conquest_attack_turn_limit(scenario)
		briefing_label.text = "戰場:%s%s%s%s\n\n%s 出動 %d 支部隊,向 %s 的守軍發起進攻;敵軍約 %d 支部隊據守地形。\n\n任務:%s。守軍撐過第 %d 回合則擊退你。" % [
			loc, terrain_line, prep_line, trait_line, me, my_n, foe, foe_n, attack_objective, limit,
		]

func _on_begin_pressed() -> void:
	if GameState.campaign_mode:
		GameState.clear_deployment_overrides()
		get_tree().change_scene_to_file("res://scenes/battle.tscn")
		return
	# Conquest still routes through deployment; the editor builds the recruited
	# army onto the themed map (see deployment.gd / ConquestBattleSetup).
	get_tree().change_scene_to_file("res://scenes/deployment.tscn")

func _on_back_pressed() -> void:
	if GameState.campaign_mode:
		get_tree().change_scene_to_file("res://scenes/campaign.tscn")
	elif GameState.conquest_mode:
		GameState.clear_conquest_battle()
		get_tree().change_scene_to_file("res://scenes/conquest.tscn")
	else:
		get_tree().change_scene_to_file("res://scenes/scenario_select.tscn")
