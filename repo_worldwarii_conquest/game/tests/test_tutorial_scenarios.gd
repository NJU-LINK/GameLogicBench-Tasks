extends SceneTree

const SCENARIOS_DIR := "res://data/scenarios/"
const REQUIRED_MECHANICS := {
	"movement": true,
	"attack": true,
	"counterattack": true,
	"capture": true,
	"secondary_objective": true,
	"terrain_defense": true,
	"zoc": true,
	"overwatch": true,
	"suppression": true,
	"rally": true,
	"dig_in": true,
	"direct_fire_los": true,
	"indirect_fire": true,
	"spotting": true,
	"armor": true,
	"anti_armor": true,
	"armor_standoff": true,
	"engineer_bridge": true,
	"engineer_breach": true,
	"airdrop": true,
	"general_skill": true,
	"veteran": true,
	"reinforcements": true,
	"splash_damage": true,
}

func _init() -> void:
	var pass_count := 0
	var fail_count := 0

	var tutorials := _load_tutorial_scenarios()
	var tutorial_order := _tutorial_campaign_order()
	if tutorials.size() >= 6:
		pass_count += 1
	else:
		fail_count += 1
		printerr("FAIL: expected at least 6 tutorial scenarios, got %d" % tutorials.size())
	if tutorial_order.size() == tutorials.size() and String(tutorial_order[0]) == "tut_00_basic_turn":
		pass_count += 1
	else:
		fail_count += 1
		printerr("FAIL: tutorial campaign should contain every tut_* scenario and start at tut_00")

	var covered := {}
	for scenario in tutorials:
		var scenario_id := String(scenario.get("id", ""))
		var mechanics: Array = scenario.get("tutorial_mechanics", [])
		if mechanics.is_empty():
			fail_count += 1
			printerr("FAIL: %s missing tutorial_mechanics" % scenario_id)
		else:
			pass_count += 1
		for mechanic in mechanics:
			covered[String(mechanic)] = true
		if _has_actionable_briefing(scenario):
			pass_count += 1
		else:
			fail_count += 1
			printerr("FAIL: %s briefing should mention mission and tested mechanics" % scenario_id)

	for mechanic in REQUIRED_MECHANICS.keys():
		if covered.has(mechanic):
			pass_count += 1
		else:
			fail_count += 1
			printerr("FAIL: tutorial coverage missing mechanic %s" % mechanic)

	print("Tutorial scenario tests: %d pass, %d fail" % [pass_count, fail_count])
	quit(0 if fail_count == 0 else 1)

func _tutorial_campaign_order() -> Array:
	var campaigns := _load_json("res://data/campaigns.json")
	var campaign: Dictionary = campaigns.get("00_tutorial", {})
	return campaign.get("scenario_order", [])

func _load_tutorial_scenarios() -> Array[Dictionary]:
	var out: Array[Dictionary] = []
	var dir := DirAccess.open(SCENARIOS_DIR)
	if dir == null:
		printerr("FAIL: cannot open scenarios dir")
		return out
	dir.list_dir_begin()
	var name := dir.get_next()
	while name != "":
		if not dir.current_is_dir() and name.ends_with(".json"):
			var scenario := _load_json(SCENARIOS_DIR + name)
			if String(scenario.get("id", "")).begins_with("tut_"):
				out.append(scenario)
		name = dir.get_next()
	dir.list_dir_end()
	out.sort_custom(func(a, b): return a.get("id", "") < b.get("id", ""))
	return out

func _load_json(path: String) -> Dictionary:
	var f := FileAccess.open(path, FileAccess.READ)
	if f == null:
		printerr("FAIL: cannot read ", path)
		return {}
	var parsed: Variant = JSON.parse_string(f.get_as_text())
	f.close()
	if typeof(parsed) != TYPE_DICTIONARY:
		printerr("FAIL: invalid scenario json ", path)
		return {}
	return parsed

func _has_actionable_briefing(scenario: Dictionary) -> bool:
	var briefing := String(scenario.get("briefing", ""))
	return briefing.contains("任務:") and briefing.contains("測試機制:")
