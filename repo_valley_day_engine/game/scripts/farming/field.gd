class_name Field
extends RefCounted

var id: String = ""
var hexes: Array[Vector2i] = []
var crop_id: String = ""
var growth_days: int = 0
var tended: bool = false
var planted_turn: int = 0


func is_empty() -> bool:
	return crop_id.is_empty()


func is_mature(crop) -> bool:
	return not is_empty() and crop != null and growth_days >= crop.grow_days


func clear_crop() -> void:
	crop_id = ""
	growth_days = 0
	tended = false


func clear() -> void:
	clear_crop()


func hex_count() -> int:
	return hexes.size()


func to_dict() -> Dictionary:
	var hex_data: Array = []
	for coords in hexes:
		hex_data.append({"x": coords.x, "y": coords.y})
	return {
		"id": id,
		"hexes": hex_data,
		"crop_id": crop_id,
		"growth_days": growth_days,
		"tended": tended,
		"planted_turn": planted_turn,
	}


static func from_dict(data: Dictionary) -> Field:
	var field := Field.new()
	field.id = str(data.get("id", ""))
	field.crop_id = str(data.get("crop_id", ""))
	field.growth_days = int(data.get("growth_days", 0))
	field.tended = bool(data.get("tended", false))
	field.planted_turn = int(data.get("planted_turn", 0))
	field.hexes.clear()
	for entry in data.get("hexes", []):
		field.hexes.append(Vector2i(int(entry["x"]), int(entry["y"])))
	return field
