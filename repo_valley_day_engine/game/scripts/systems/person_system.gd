extends RefCounted

const Person = preload("res://scripts/entities/person.gd")
const Field = preload("res://scripts/farming/field.gd")


func resolve_day(persons: Array, rng: RandomNumberGenerator, game_state: Object) -> void:
	var sorted: Array = persons.duplicate()
	sorted.sort_custom(func(a: Person, b: Person) -> bool:
		return a.id < b.id
	)
	for person in sorted:
		var action: String = person.roll_action(rng)
		match action:
			"tend":
				_resolve_tend(person, rng, game_state)
			"idle":
				pass


func _resolve_tend(person: Person, rng: RandomNumberGenerator, game_state: Object) -> void:
	var candidates := _fields_needing_tend(game_state)
	if candidates.is_empty():
		return
	var field_id: String = candidates[rng.randi_range(0, candidates.size() - 1)]
	_tend_field(game_state, field_id)
	game_state.player_message("%s tended the crops." % person.display_name)


func _fields_needing_tend(game_state: Object) -> Array[String]:
	var out: Array[String] = []
	for field_id in game_state.fields:
		var field: Field = game_state.fields[field_id]
		if field.is_empty():
			continue
		var crop = game_state.get_crop(field.crop_id)
		if field.tended or field.is_mature(crop):
			continue
		if game_state.weather == game_state.Weather.DROUGHT:
			out.append(field_id)
		elif game_state.weather == game_state.Weather.FROST and not crop.frost_tolerant:
			out.append(field_id)
	return out


func _tend_field(game_state: Object, field_id: String) -> void:
	var field: Field = game_state.fields.get(field_id)
	if field == null or field.is_empty():
		return
	field.tended = true
