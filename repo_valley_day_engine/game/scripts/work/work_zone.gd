class_name WorkZone
extends RefCounted

enum ZoneType { FORAGE, CLEAR, TRAP, COLLECT_WATER, BUILD }

var id: String = ""
var type: ZoneType = ZoneType.FORAGE
var hexes: Array[Vector2i] = []
var crop_id: String = ""
var structure_kind: String = ""
var work: int = 0


func to_dict() -> Dictionary:
	var hex_data: Array = []
	for coords in hexes:
		hex_data.append({"x": coords.x, "y": coords.y})
	return {
		"id": id,
		"type": type,
		"hexes": hex_data,
		"crop_id": crop_id,
		"structure_kind": structure_kind,
		"work": work,
	}


static func from_dict(data: Dictionary) -> WorkZone:
	var zone := WorkZone.new()
	zone.id = str(data.get("id", ""))
	zone.type = int(data.get("type", ZoneType.FORAGE)) as ZoneType
	zone.crop_id = str(data.get("crop_id", ""))
	zone.structure_kind = str(data.get("structure_kind", ""))
	zone.work = int(data.get("work", 0))
	zone.hexes.clear()
	for entry in data.get("hexes", []):
		zone.hexes.append(Vector2i(int(entry["x"]), int(entry["y"])))
	return zone
