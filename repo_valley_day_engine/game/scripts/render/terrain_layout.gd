class_name TerrainLayout
extends RefCounted

const HexGrid = preload("res://scripts/world/hex_grid.gd")

## World-space meters per map elevation unit.
const ELEVATION_WORLD_SCALE := 0.35
const HEX_RADIUS := 28.0
const SQRT_3 := 1.7320508075688772

static func depth_key(coords: Vector2i) -> int:
	var axial := HexGrid.map_to_axial(coords)
	return axial.x + axial.y


static func planar_position(coords: Vector2i) -> Vector2:
	var axial := HexGrid.map_to_axial(coords)
	var x := HEX_RADIUS * SQRT_3 * (float(axial.x) + float(axial.y) * 0.5)
	var z := HEX_RADIUS * 1.5 * float(axial.y)
	return Vector2(x, z)


static func hex_to_world_3d(coords: Vector2i, elevation: float = 0.0) -> Vector3:
	var planar := planar_position(coords)
	return Vector3(planar.x, elevation * ELEVATION_WORLD_SCALE, planar.y)


static func camera_position_for_hex(coords: Vector2i) -> Vector2:
	return planar_position(coords)


static func hex_to_screen(coords: Vector2i, elevation: float = 0.0) -> Vector2:
	var world := hex_to_world_3d(coords, elevation)
	return Vector2(world.x, world.z)


static func screen_to_hex(screen_pos: Vector2) -> Vector2i:
	var qf := (SQRT_3 / 3.0 * screen_pos.x - screen_pos.y / 3.0) / HEX_RADIUS
	var rf := (2.0 / 3.0 * screen_pos.y) / HEX_RADIUS
	var axial := _axial_round(qf, rf)
	return HexGrid.axial_to_map(axial.x, axial.y)


static func edge_height(elev_a: float, elev_b: float) -> float:
	return (elev_a + elev_b) * 0.5 * ELEVATION_WORLD_SCALE


## Nearest hex by planar distance; elevation-aware for screen picking.
static func pick_hex_at_screen(
	screen_pos: Vector2,
	hexes: Dictionary,
	max_dist: float = -1.0
) -> Vector2i:
	if hexes.is_empty():
		return Vector2i(999999, 999999)
	if max_dist < 0.0:
		max_dist = HEX_RADIUS * 1.75
	var rough := screen_to_hex(screen_pos)
	var axial_rough := HexGrid.map_to_axial(rough)
	var best := Vector2i(999999, 999999)
	var best_d_sq := max_dist * max_dist
	for dq in range(-2, 3):
		for dr in range(-2, 3):
			var coords := HexGrid.axial_to_map(axial_rough.x + dq, axial_rough.y + dr)
			if not hexes.has(coords):
				continue
			var hex: HexState = hexes[coords]
			var elev: float = hex.elevation if hex != null else 0.0
			var center := hex_to_screen(coords, elev)
			var d_sq := screen_pos.distance_squared_to(center)
			if d_sq < best_d_sq:
				best_d_sq = d_sq
				best = coords
	return best


static func top_corners(center: Vector2) -> PackedVector2Array:
	var r := HEX_RADIUS
	var w := r * sqrt(3.0) * 0.5
	return PackedVector2Array([
		center + Vector2(0.0, -r),
		center + Vector2(w, -r * 0.5),
		center + Vector2(w, r * 0.5),
		center + Vector2(0.0, r),
		center + Vector2(-w, r * 0.5),
		center + Vector2(-w, -r * 0.5),
	])


static func side_corners(coords: Vector2i) -> PackedVector2Array:
	return top_corners(planar_position(coords))


static func cells_in_screen_rect(screen_rect: Rect2, margin_hexes: int = 2) -> Array[Vector2i]:
	var samples: Array[Vector2] = [
		screen_rect.position,
		screen_rect.position + Vector2(screen_rect.size.x, 0.0),
		screen_rect.end,
		screen_rect.position + Vector2(0.0, screen_rect.size.y),
		screen_rect.get_center(),
	]
	var min_ax := Vector2i(999999, 999999)
	var max_ax := Vector2i(-999999, -999999)
	for point in samples:
		var axial := HexGrid.map_to_axial(screen_to_hex(point))
		min_ax.x = mini(min_ax.x, axial.x)
		min_ax.y = mini(min_ax.y, axial.y)
		max_ax.x = maxi(max_ax.x, axial.x)
		max_ax.y = maxi(max_ax.y, axial.y)
	var out: Array[Vector2i] = []
	for q in range(min_ax.x - margin_hexes, max_ax.x + margin_hexes + 1):
		for r in range(min_ax.y - margin_hexes, max_ax.y + margin_hexes + 1):
			out.append(HexGrid.axial_to_map(q, r))
	return out


static func cells_in_planar_rect(planar_rect: Rect2, margin_hexes: int = 2) -> Array[Vector2i]:
	return cells_in_screen_rect(planar_rect, margin_hexes)


static func sort_coords_back_to_front(coords_list: Array[Vector2i]) -> Array[Vector2i]:
	var sorted := coords_list.duplicate()
	sorted.sort_custom(func(a: Vector2i, b: Vector2i) -> bool:
		return depth_key(a) < depth_key(b)
	)
	return sorted


static func _axial_round(q: float, r: float) -> Vector2i:
	var s := -q - r
	var rq := int(round(q))
	var rr := int(round(r))
	var rs := int(round(s))
	var dq := absf(float(rq) - q)
	var dr := absf(float(rr) - r)
	var ds := absf(float(rs) - s)
	if dq > dr and dq > ds:
		rq = -rr - rs
	elif dr > ds:
		rr = -rq - rs
	return Vector2i(rq, rr)
