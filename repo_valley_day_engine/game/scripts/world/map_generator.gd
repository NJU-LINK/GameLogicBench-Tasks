class_name MapGenerator
extends RefCounted

const HexGrid = preload("res://scripts/world/hex_grid.gd")
const HexStateRes = preload("res://scripts/world/hex_state.gd")
const HexTopology = preload("res://scripts/world/hex_topology.gd")
const HexHydrology = preload("res://scripts/world/hex_hydrology.gd")
const BucketAssigner = preload("res://scripts/world/bucket_assigner.gd")

const MAP_RADIUS := 20
const ORIGIN := Vector2i.ZERO

const WATER_ELEVATION := -0.18
const WOOD_MOISTURE := 0.15
const WOOD_ELEVATION_MAX := 0.42


static func generate_world(rng: RandomNumberGenerator) -> Dictionary:
	var elevation_noise := FastNoiseLite.new()
	elevation_noise.seed = rng.seed
	elevation_noise.noise_type = FastNoiseLite.TYPE_SIMPLEX
	elevation_noise.frequency = 0.055
	elevation_noise.fractal_octaves = 4

	var moisture_noise := FastNoiseLite.new()
	moisture_noise.seed = rng.seed + 7919
	moisture_noise.noise_type = FastNoiseLite.TYPE_SIMPLEX
	moisture_noise.frequency = 0.08
	moisture_noise.fractal_octaves = 3

	var hexes: Dictionary = {}
	for coords in HexGrid.cells_in_radius(ORIGIN, MAP_RADIUS):
		var axial: Vector2i = HexGrid.map_to_axial(coords)
		var sample := Vector2(axial.x * 1.03, axial.y * 1.03)
		var elev_norm := elevation_noise.get_noise_2d(sample.x, sample.y)
		var moist := moisture_noise.get_noise_2d(sample.x + 40.0, sample.y - 25.0)

		var hex := HexStateRes.new()
		hex.coords = coords
		hex.patch_id = BucketAssigner.patch_id_from_coords(coords)
		hex.block_id = BucketAssigner.block_id_from_coords(coords)
		hex.zone_id = BucketAssigner.zone_id_from_coords(coords)
		hex.chunk_id = Vector2i(coords.x >> 4, coords.y >> 4)
		hex.elevation = elev_norm * HexTopology.ELEVATION_SCALE
		hex.moisture = moist

		if elev_norm < WATER_ELEVATION:
			hex.terrain = HexStateRes.TERRAIN_WATER
			hex.water_depth = 1.0 + abs(elev_norm) * 2.0
			hex.veg_class = HexStateRes.VegClass.BARE
		elif moist > WOOD_MOISTURE and elev_norm < WOOD_ELEVATION_MAX:
			hex.terrain = HexStateRes.TERRAIN_WOOD
			hex.veg_class = HexStateRes.VegClass.WOODLAND
			hex.veg_density = 0.6 + moist * 0.3
			hex.standing_timber = 8.0 + moist * 12.0
			hex.forage_mask = HexStateRes.FORAGE_MUSHROOMS if moist > 0.35 else 0
		else:
			hex.terrain = HexStateRes.TERRAIN_GRASS
			hex.veg_class = HexStateRes.VegClass.GRASS
			hex.veg_density = 0.3 + moist * 0.4
			if moist > 0.1 and elev_norm < 0.2:
				hex.forage_mask |= HexStateRes.FORAGE_BERRIES
			if moist > -0.05:
				hex.forage_mask |= HexStateRes.FORAGE_ROOTS

		_derive_ground(hex, elev_norm, moist)
		hex.sync_terrain()
		hexes[coords] = hex

	HexTopology.apply(hexes)
	HexHydrology.apply(hexes)
	_apply_springs(hexes, rng)
	_apply_lore_tags(hexes, rng)
	return hexes


static func _apply_springs(hexes: Dictionary, rng: RandomNumberGenerator) -> void:
	var candidates: Array[Vector2i] = []
	for coords in hexes:
		var hex: HexStateRes = hexes[coords]
		if hex.is_water() or hex.is_riparian or hex.is_spring:
			continue
		if hex.moisture < 0.1:
			continue
		if not HexTopology.is_settleable(hex):
			continue
		candidates.append(coords)
	candidates.shuffle()
	var target := mini(maxi(3, candidates.size() / 150), 6)
	for i in range(mini(target, candidates.size())):
		hexes[candidates[i]].is_spring = true


static func _apply_lore_tags(hexes: Dictionary, rng: RandomNumberGenerator) -> void:
	var candidates: Array[Vector2i] = []
	for coords in hexes:
		var hex: HexStateRes = hexes[coords]
		if hex.is_water():
			continue
		candidates.append(coords)
	candidates.shuffle()
	var tagged := 0
	var target := maxi(int(candidates.size() * 0.08), 1)
	for coords in candidates:
		if tagged >= target:
			break
		var hex: HexStateRes = hexes[coords]
		if hex.lore_tag != HexStateRes.LoreTag.NONE:
			continue
		if hex.is_riparian or hex.is_spring:
			hex.lore_tag = HexStateRes.LoreTag.ACEQUIA
		elif hex.veg_class == HexStateRes.VegClass.WOODLAND:
			hex.lore_tag = HexStateRes.LoreTag.HUNTING_GROUND
		elif hex.trail_level > 0 or hex.river_flow >= 0:
			hex.lore_tag = HexStateRes.LoreTag.WAGON_RUT
		else:
			var roll := rng.randi() % 3
			match roll:
				0:
					hex.lore_tag = HexStateRes.LoreTag.MISSION_TRAIL
				1:
					hex.lore_tag = HexStateRes.LoreTag.WAGON_RUT
				_:
					hex.lore_tag = HexStateRes.LoreTag.HUNTING_GROUND
		tagged += 1


static func generate_terrain(rng: RandomNumberGenerator) -> Dictionary:
	var hexes := generate_world(rng)
	var terrain: Dictionary = {}
	for coords in hexes:
		terrain[coords] = hexes[coords].terrain
	return terrain


static func prepare_tile_map(tile_map: TileMapLayer) -> void:
	if tile_map.tile_set == null:
		tile_map.tile_set = HexGrid.create_tileset()
	tile_map.clear()


static func is_settleable_hex(hexes: Dictionary, coords: Vector2i) -> bool:
	if not hexes.has(coords):
		return false
	return HexTopology.is_settleable(hexes[coords])


static func _derive_ground(hex: HexStateRes, elev_norm: float, moist: float) -> void:
	if hex.is_water():
		hex.soil_type = HexStateRes.SoilType.PEAT
		hex.rockiness = 0.1
		hex.fertility = 0.2
		return
	if elev_norm > 0.35:
		hex.soil_type = HexStateRes.SoilType.ROCKY
		hex.rockiness = 0.5 + elev_norm * 0.3
		hex.fertility = 0.25
	elif moist > 0.3:
		hex.soil_type = HexStateRes.SoilType.PEAT
		hex.rockiness = 0.15
		hex.fertility = 0.65
	elif moist < -0.1:
		hex.soil_type = HexStateRes.SoilType.SAND
		hex.rockiness = 0.25
		hex.fertility = 0.35
	elif moist > 0.05:
		hex.soil_type = HexStateRes.SoilType.CLAY
		hex.rockiness = 0.2
		hex.fertility = 0.55
	else:
		hex.soil_type = HexStateRes.SoilType.LOAM
		hex.rockiness = 0.2
		hex.fertility = 0.5
