extends RefCounted

const BucketAssigner = preload("res://scripts/world/bucket_assigner.gd")
const AggregateBucket = preload("res://scripts/world/aggregate_bucket.gd")

var patches: Dictionary = {}
var blocks: Dictionary = {}
var zones: Dictionary = {}


func mark_hex_dirty(hex) -> void:
	_get_or_create(patches, hex.patch_id, 1).dirty = true
	_get_or_create(blocks, hex.block_id, 2).dirty = true
	_get_or_create(zones, hex.zone_id, 3).dirty = true


func flush(hexes: Dictionary, plot_coords: Dictionary = {}) -> void:
	for key in patches:
		if patches[key].dirty:
			_recompute_patch(key, hexes, plot_coords)
	for key in blocks:
		if blocks[key].dirty:
			_recompute_block(key)
	for key in zones:
		if zones[key].dirty:
			_recompute_zone(key)


func _get_or_create(store: Dictionary, id: Vector2i, level: int) -> AggregateBucket:
	var key := BucketAssigner.bucket_key(id)
	if not store.has(key):
		var bucket := AggregateBucket.new()
		bucket.id = id
		bucket.level = level
		store[key] = bucket
	return store[key]


func _recompute_patch(key: String, hexes: Dictionary, plot_coords: Dictionary) -> void:
	var patch: AggregateBucket = patches[key]
	patch.clear()
	patch.id = _id_from_key(key)
	patch.level = 1
	var terrain_counts: Dictionary = {}
	for coords in hexes:
		var hex = hexes[coords]
		if BucketAssigner.bucket_key(hex.patch_id) != key:
			continue
		patch.food += hex.food
		patch.population += hex.population
		terrain_counts[hex.terrain] = terrain_counts.get(hex.terrain, 0) + 1
		if plot_coords.has(coords):
			patch.plot_count += 1
	patch.terrain = _majority(terrain_counts)
	patch.dirty = false


func _recompute_block(key: String) -> void:
	var block: AggregateBucket = blocks[key]
	block.clear()
	block.id = _id_from_key(key)
	block.level = 2
	var terrain_counts: Dictionary = {}
	for patch_key in patches:
		var patch: AggregateBucket = patches[patch_key]
		if BucketAssigner.bucket_key(_block_id_from_patch(patch.id)) != key:
			continue
		block.food += patch.food
		block.population += patch.population
		block.plot_count += patch.plot_count
		terrain_counts[patch.terrain] = terrain_counts.get(patch.terrain, 0) + 1
	block.terrain = _majority(terrain_counts)
	block.dirty = false


func _recompute_zone(key: String) -> void:
	var zone: AggregateBucket = zones[key]
	zone.clear()
	zone.id = _id_from_key(key)
	zone.level = 3
	var terrain_counts: Dictionary = {}
	for block_key in blocks:
		var block: AggregateBucket = blocks[block_key]
		if BucketAssigner.bucket_key(_zone_id_from_block(block.id)) != key:
			continue
		zone.food += block.food
		zone.population += block.population
		zone.plot_count += block.plot_count
		terrain_counts[block.terrain] = terrain_counts.get(block.terrain, 0) + 1
	zone.terrain = _majority(terrain_counts)
	zone.dirty = false


func _majority(counts: Dictionary) -> int:
	if counts.is_empty():
		return 0
	var best_terrain := 0
	var best_count := -1
	for terrain in counts:
		var n: int = counts[terrain]
		if n > best_count:
			best_count = n
			best_terrain = terrain
	return best_terrain


func _id_from_key(key: String) -> Vector2i:
	var parts := key.split(",")
	return Vector2i(int(parts[0]), int(parts[1]))


func _block_id_from_patch(patch_id: Vector2i) -> Vector2i:
	return Vector2i(
		_floor_div_patch_to_block(patch_id.x),
		_floor_div_patch_to_block(patch_id.y),
	)


func _zone_id_from_block(block_id: Vector2i) -> Vector2i:
	return Vector2i(
		_floor_div_block_to_zone(block_id.x),
		_floor_div_block_to_zone(block_id.y),
	)


func _floor_div_patch_to_block(v: int) -> int:
	return BucketAssigner._floor_div(v * BucketAssigner.PATCH_DIV, BucketAssigner.BLOCK_DIV)


func _floor_div_block_to_zone(v: int) -> int:
	return BucketAssigner._floor_div(v * BucketAssigner.BLOCK_DIV, BucketAssigner.ZONE_DIV)
