class_name HexTopology
extends RefCounted

const HexGrid = preload("res://scripts/world/hex_grid.gd")
const HexStateRes = preload("res://scripts/world/hex_state.gd")

const CLIFF_THRESHOLD := 10.0
const SETTLEABLE_MAX_SLOPE := 16.0
const ELEVATION_SCALE := 40.0


static func apply(hexes: Dictionary) -> void:
	for coords in hexes:
		var hex: HexStateRes = hexes[coords]
		if hex.is_water():
			continue
		var max_delta := 0.0
		var cliff_mask := 0
		var neighbors := HexGrid.neighbors(coords)
		for i in neighbors.size():
			var n: Vector2i = neighbors[i]
			if not hexes.has(n):
				continue
			var neighbor: HexStateRes = hexes[n]
			var delta: float = abs(hex.elevation - neighbor.elevation)
			max_delta = maxf(max_delta, delta)
			if delta >= CLIFF_THRESHOLD and not neighbor.is_water():
				cliff_mask |= 1 << i
		hex.slope_grade = max_delta
		hex.cliff_edges = cliff_mask


static func is_settleable(hex) -> bool:
	if hex == null:
		return false
	if hex.is_water():
		return false
	if hex.cliff_edges != 0:
		return false
	if hex.slope_grade >= SETTLEABLE_MAX_SLOPE:
		return false
	return true
