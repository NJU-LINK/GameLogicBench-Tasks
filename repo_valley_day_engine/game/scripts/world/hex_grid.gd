class_name HexGrid
extends RefCounted

## Coordinate helpers for Godot's stacked-offset hex layout (flat-top, vertical axis).
## map_to_local / local_to_map and hex_corners match TileSet for the project's tileset settings.

const TILE_SIZE := Vector2i(110, 94)
const HEX_OVERLAP := 0.25

static var _cached_tileset: TileSet


static func create_tileset() -> TileSet:
	if _cached_tileset != null:
		return _cached_tileset
	var tileset := TileSet.new()
	tileset.tile_shape = TileSet.TILE_SHAPE_HEXAGON
	tileset.tile_layout = TileSet.TILE_LAYOUT_STACKED_OFFSET
	tileset.tile_offset_axis = TileSet.TILE_OFFSET_AXIS_VERTICAL
	tileset.tile_size = TILE_SIZE
	var source := TileSetAtlasSource.new()
	var image := Image.create(TILE_SIZE.x, TILE_SIZE.y, false, Image.FORMAT_RGBA8)
	image.fill(Color(0.18, 0.32, 0.16, 1.0))
	source.texture = ImageTexture.create_from_image(image)
	source.texture_region_size = TILE_SIZE
	source.create_tile(Vector2i.ZERO)
	tileset.add_source(source, 0)
	_cached_tileset = tileset
	return tileset


static func axial_to_map(q: int, r: int) -> Vector2i:
	return Vector2i(q, r + (q + (q & 1)) / 2)


static func map_to_axial(coords: Vector2i) -> Vector2i:
	var q := coords.x
	var r := coords.y - (coords.x + (coords.x & 1)) / 2
	return Vector2i(q, r)


static func cube_distance(a: Vector2i, b: Vector2i) -> int:
	var ac := map_to_axial(a)
	var bc := map_to_axial(b)
	var aq := ac.x
	var ar := ac.y
	var a_s := -aq - ar
	var bq := bc.x
	var br := bc.y
	var b_s := -bq - br
	return maxi(maxi(abs(aq - bq), abs(ar - br)), abs(a_s - b_s))


static func cells_in_radius(center: Vector2i, radius: int) -> Array[Vector2i]:
	var out: Array[Vector2i] = []
	var center_axial := map_to_axial(center)
	for dq in range(-radius, radius + 1):
		for dr in range(-radius, radius + 1):
			var ds := -dq - dr
			if maxi(maxi(abs(dq), abs(dr)), abs(ds)) > radius:
				continue
			var axial: Vector2i = Vector2i(center_axial.x + dq, center_axial.y + dr)
			out.append(axial_to_map(axial.x, axial.y))
	return out


static func cells_in_rect(world_rect: Rect2, margin_hexes: int = 2) -> Array[Vector2i]:
	var out: Array[Vector2i] = []
	var min_map := local_to_map(world_rect.position) - Vector2i(margin_hexes, margin_hexes)
	var max_map := local_to_map(world_rect.end) + Vector2i(margin_hexes, margin_hexes)
	for x in range(min_map.x, max_map.x + 1):
		for y in range(min_map.y, max_map.y + 1):
			var coords := Vector2i(x, y)
			var center := map_to_local(coords)
			if world_rect.grow(float(TILE_SIZE.x)).has_point(center):
				out.append(coords)
	return out


static func neighbors(coords: Vector2i) -> Array[Vector2i]:
	var axial := map_to_axial(coords)
	var q := axial.x
	var r := axial.y
	return [
		axial_to_map(q + 1, r),
		axial_to_map(q + 1, r - 1),
		axial_to_map(q, r - 1),
		axial_to_map(q - 1, r),
		axial_to_map(q - 1, r + 1),
		axial_to_map(q, r + 1),
	]


static func map_to_local(coords: Vector2i) -> Vector2:
	var w := float(TILE_SIZE.x)
	var h := float(TILE_SIZE.y)
	var x := w * 0.5 + float(coords.x) * (w * 0.75)
	var y: float
	if coords.x & 1:
		y = h * 0.5 + float(coords.y) * h
	else:
		y = h * (float(coords.y) + 1.0)
	return Vector2(x, y)


static func local_to_map(local_pos: Vector2) -> Vector2i:
	var w := float(TILE_SIZE.x)
	var h := float(TILE_SIZE.y)
	var col := int(round((local_pos.x - w * 0.5) / (w * 0.75)))
	var row: int
	if col & 1:
		row = int(round((local_pos.y - h * 0.5) / h))
	else:
		row = int(round(local_pos.y / h)) - 1
	return Vector2i(col, row)


static func hex_shape_polygon_normalized() -> PackedVector2Array:
	# Matches TileSet.get_tile_shape_polygon() for hex + vertical offset axis.
	var overlap := HEX_OVERLAP
	var horizontal := PackedVector2Array([
		Vector2(0.0, -0.5),
		Vector2(-0.5, overlap - 0.5),
		Vector2(-0.5, 0.5 - overlap),
		Vector2(0.0, 0.5),
		Vector2(0.5, 0.5 - overlap),
		Vector2(0.5, overlap - 0.5),
	])
	var out := PackedVector2Array()
	for point in horizontal:
		out.append(Vector2(point.y, point.x))
	return out


static func hex_corners(center: Vector2) -> PackedVector2Array:
	var w := float(TILE_SIZE.x)
	var h := float(TILE_SIZE.y)
	var out := PackedVector2Array()
	for norm in hex_shape_polygon_normalized():
		out.append(center + Vector2(norm.x * w, norm.y * h))
	return out
