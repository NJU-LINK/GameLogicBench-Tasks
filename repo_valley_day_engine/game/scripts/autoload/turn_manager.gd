extends Node

signal turn_started(turn_number: int)
signal turn_ended(turn_number: int)

enum Phase { PLAYER, RESOLUTION }

var turn_number: int = 1
var phase: Phase = Phase.PLAYER


func _ready() -> void:
	_start_turn()


func end_turn() -> void:
	advance_days(1)


func skip_days(day_count: int) -> void:
	advance_days(day_count)


## Work the queue across days until an unordered plot needs the player, or
## the queue empties with something waiting on attention.
func advance_until_actionable(max_days: int = 364) -> void:
	if phase != Phase.PLAYER:
		return
	if not GameState.has_pending_orders() and GameState.needs_attention():
		GameState.player_message("The claim needs work — queue a chore.")
		return
	GameState.begin_day_batch(max_days)
	var days := 0
	while days < max_days:
		_work_and_resolve_one_day()
		days += 1
		if GameState.game_lost:
			break
		if not GameState.has_pending_orders() and GameState.needs_attention():
			break
	GameState._batch_stats["days"] = days
	GameState.end_day_batch()
	if GameState.needs_attention():
		GameState.player_message("Stopped — the claim needs you.")
	elif days >= max_days:
		GameState.player_message("A full year passed with nothing pressing.")
	_start_turn()


func advance_days(day_count: int) -> void:
	if phase != Phase.PLAYER or day_count < 1:
		return
	if day_count > 1:
		GameState.begin_day_batch(day_count)
	for _i in day_count:
		_work_and_resolve_one_day()
		if GameState.game_lost:
			break
	if day_count > 1:
		GameState.end_day_batch()
	_start_turn()


## Spend the current day's labour on the order queue, then resolve the day and
## refresh labour for the next morning.
func _work_and_resolve_one_day() -> void:
	GameState.work_today()
	turn_ended.emit(turn_number)
	turn_number += 1
	GameState.refresh_labor()


func _start_turn() -> void:
	phase = Phase.PLAYER
	turn_started.emit(turn_number)


func reset_for_test(turn: int = 1) -> void:
	turn_number = turn
	phase = Phase.PLAYER


func begin_game_scene() -> void:
	# Autoload turn state survives scene changes; reopen the day for the player.
	phase = Phase.PLAYER
	turn_started.emit(turn_number)
