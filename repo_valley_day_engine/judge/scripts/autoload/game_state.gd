extends Node

const PlotState = preload("res://scripts/farming/plot_state.gd")
const FieldRes = preload("res://scripts/farming/field.gd")
const CropDefinition = preload("res://scripts/farming/crop_definition.gd")
const WorkZoneRes = preload("res://scripts/work/work_zone.gd")
const HexSim = preload("res://scripts/world/hex_sim.gd")
const HexStateRes = preload("res://scripts/world/hex_state.gd")
const HexTopology = preload("res://scripts/world/hex_topology.gd")
const StructureRes = preload("res://scripts/world/structure.gd")
const HexEdge = preload("res://scripts/world/hex_edge.gd")
const Trader = preload("res://scripts/systems/trader.gd")
const Person = preload("res://scripts/entities/person.gd")
const Holding = preload("res://scripts/entities/holding.gd")
const Agent = preload("res://scripts/entities/agent.gd")
const PersonSystem = preload("res://scripts/systems/person_system.gd")
const DayEngine = preload("res://scripts/systems/day_engine.gd")
const SaveManager = preload("res://scripts/autoload/save_manager.gd")
const FarmBuilding = preload("res://scripts/world/farm_building.gd")
const TerrainClassifier = preload("res://scripts/world/terrain_classifier.gd")
const MapGenerator = preload("res://scripts/world/map_generator.gd")
const HexGrid = preload("res://scripts/world/hex_grid.gd")
const WestTheme = preload("res://scripts/theme/west_theme.gd")
const ScenarioCatalog = preload("res://scripts/scenarios/scenario_catalog.gd")

signal season_changed(season: int, year: int)
signal weather_changed(weather: int)
signal resources_changed()
signal plot_changed(coords: Vector2i)
signal log_added(message: String)
signal day_batch_finished(days: int)
signal game_over(reason: String)
signal victory_achieved(reason: String)
signal game_started()

enum Season { SPRING, SUMMER, AUTUMN, WINTER }
enum Weather { CLEAR, RAIN, DROUGHT, FROST }

const DAYS_PER_SEASON := 91
const DAYS_PER_YEAR := DAYS_PER_SEASON * 4
const FOOD_PER_PERSON_DAY := 1
const WATER_PER_PERSON_DAY := 1
const FIREWOOD_PER_WINTER_DAY := 1
const SAVE_VERSION := 4

const LABOR_HEAD := 10
const SPOUSE_LABOR := 10
const CHILD_LABOR := 5
const TASK_COST := {
	"forage": 1,
	"clear": 3,
	"collect_water": 1,
	"trap": 2,
	"chop_firewood": 2,
	"plant_field": 4,
	"tend_field": 2,
	"harvest_field": 3,
	"build": 8,
	"fence": 2,
}
const ZONE_PRIORITY: Array[int] = [
	WorkZoneRes.ZoneType.COLLECT_WATER,
	WorkZoneRes.ZoneType.TRAP,
	WorkZoneRes.ZoneType.FORAGE,
	WorkZoneRes.ZoneType.CLEAR,
	WorkZoneRes.ZoneType.BUILD,
]

var rng := RandomNumberGenerator.new()
var year: int = 1
var season: Season = Season.SUMMER
var weather: Weather = Weather.CLEAR
var food_consumption_accumulator: float = 0.0
var water_consumption_accumulator: float = 0.0
var resources: Dictionary = {
	"food": 20,
	"water": 15,
	"firewood": 5,
	"wood": 10,
	"berries": 0,
	"roots": 0,
	"mushrooms": 0,
	"meat": 0,
	"coins": 15,
	"tools": 2,
	"corn_seed": 4,
	"bean_seed": 4,
}
var fields: Dictionary = {}
var work_zones: Dictionary = {}
var fences: Dictionary = {}
var active_zone_id: String = ""
var active_field_id: String = ""
var zone_paint_mode: int = WorkZoneRes.ZoneType.FORAGE
var settlement_chosen: bool = false
var plots: Dictionary = {}
var crops: Dictionary = {}
var orders: Dictionary = {}
var labor_per_day: int = LABOR_HEAD
var labor_pool: int = LABOR_HEAD
var log_lines: PackedStringArray = []
var home_hex: Vector2i = Vector2i.ZERO
var hex_sim: HexSim
var buildings: Dictionary = {}
var structures: Dictionary = {}
var cleared_woods: Array[Vector2i] = []
var persons: Array = []
var holdings: Array = []
var agents: Array = []
var person_system: PersonSystem = PersonSystem.new()
var day_engine = null
var player_holding: Holding
var game_active: bool = false
var game_lost: bool = false
var game_won: bool = false
var consecutive_hungry_days: int = 0
var last_game_over_reason: String = ""
var last_victory_reason: String = ""
var proved_hexes: Dictionary = {}
var _map: TileMapLayer
var _world_generated: bool = false
var _batch_mode: bool = false
var _batch_stats: Dictionary = {}
var _pending_load: Dictionary = {}
var _worked_today: bool = false
var active_scenario = null


func _ready() -> void:
	day_engine = DayEngine.new(self)
	if active_scenario == null:
		active_scenario = ScenarioCatalog.get_default()
	_register_crops()
	TurnManager.turn_ended.connect(_on_day_ended)


func start_new_game(seed: int = -1) -> void:
	if seed < 0:
		seed = int(Time.get_ticks_msec())
	year = 1
	season = Season.SUMMER
	weather = Weather.CLEAR
	food_consumption_accumulator = 0.0
	water_consumption_accumulator = 0.0
	resources = {
		"food": 20,
		"water": 15,
		"firewood": 5,
		"wood": 10,
		"berries": 0,
		"roots": 0,
		"mushrooms": 0,
		"meat": 0,
		"coins": 15,
		"tools": 2,
		"corn_seed": 4,
		"bean_seed": 4,
	}
	fields.clear()
	work_zones.clear()
	fences.clear()
	plots.clear()
	orders.clear()
	structures.clear()
	log_lines.clear()
	buildings.clear()
	cleared_woods.clear()
	_batch_mode = false
	_batch_stats = {}
	_worked_today = false
	home_hex = Vector2i.ZERO
	settlement_chosen = false
	_world_generated = false
	game_lost = false
	game_won = false
	consecutive_hungry_days = 0
	last_game_over_reason = ""
	last_victory_reason = ""
	proved_hexes.clear()
	game_active = true
	_pending_load = {}
	active_scenario = ScenarioCatalog.get_default()
	rng.seed = seed
	if crops.is_empty():
		_register_crops()
	TurnManager.reset_for_test()
	_roll_weather()


func begin_settlement(coords: Vector2i) -> void:
	ensure_world_map()
	home_hex = coords
	settlement_chosen = true
	year = 1
	season = Season.SUMMER
	TurnManager.reset_for_test(1)
	_setup_household()
	if hex_sim != null:
		var hex = hex_sim.get_hex(coords)
		if hex != null:
			hex.ownership = "player"
			hex.cleared = true
			hex.veg_class = HexStateRes.VegClass.GRASS
			hex.sync_terrain()
			hex_sim.mark_dirty(coords)
			hex_sim.flush_aggregates()
	_place_starting_shelter()
	_log(active_scenario.opening_log)
	refresh_labor()


func has_save() -> bool:
	return SaveManager.exists()


func can_continue() -> bool:
	if not SaveManager.exists():
		return false
	var data := SaveManager.read()
	if data.is_empty():
		return false
	return not bool(data.get("game_lost", false)) and not bool(data.get("game_won", false))


func save_game() -> bool:
	if not game_active:
		return false
	return SaveManager.write({
		"version": SAVE_VERSION,
		"seed": rng.seed,
		"turn": TurnManager.turn_number,
		"year": year,
		"season": season,
		"weather": weather,
		"food_accum": food_consumption_accumulator,
		"water_accum": water_consumption_accumulator,
		"resources": resources.duplicate(),
		"home_x": home_hex.x,
		"home_y": home_hex.y,
		"settlement_chosen": settlement_chosen,
		"hexes": hex_sim.serialize_hexes() if hex_sim != null else [],
		"fields": _serialize_fields(),
		"work_zones": _serialize_work_zones(),
		"structures": _serialize_structures(),
		"fences": HexEdge.to_dict(fences),
		"persons": _serialize_persons(),
		"active_field_id": active_field_id,
		"active_zone_id": active_zone_id,
		"hungry_days": consecutive_hungry_days,
		"game_lost": game_lost,
		"game_won": game_won,
		"game_over_reason": last_game_over_reason,
		"victory_reason": last_victory_reason,
		"proved_hexes": Array(proved_hexes.keys()),
		"labor_pool": labor_pool,
		"labor_per_day": labor_per_day,
		"log": Array(log_lines),
		"scenario_id": active_scenario.id if active_scenario != null else ScenarioCatalog.DEFAULT_SCENARIO_ID,
	})


func _serialize_fields() -> Array:
	var out: Array = []
	for field_id in fields:
		out.append(fields[field_id].to_dict())
	return out


func _serialize_work_zones() -> Array:
	var out: Array = []
	for zone_id in work_zones:
		out.append(work_zones[zone_id].to_dict())
	return out


func _serialize_structures() -> Array:
	var out: Array = []
	for coords in structures:
		out.append(structures[coords].to_dict())
	return out


func _serialize_persons() -> Array:
	var out: Array = []
	for person in persons:
		out.append({
			"id": person.id,
			"name": person.display_name,
			"health": person.health,
			"alive": person.alive,
			"labor": person.daily_labor,
		})
	return out


func load_game() -> bool:
	var data := SaveManager.read()
	if data.is_empty():
		return false
	_pending_load = data
	game_active = true
	return true


func apply_loaded_state() -> void:
	if _pending_load.is_empty():
		return
	var data := _migrate_loaded_data(_pending_load.duplicate(true))
	_pending_load = {}
	rng.seed = int(data.get("seed", 12345))
	TurnManager.reset_for_test(int(data.get("turn", 1)))
	year = int(data.get("year", 1))
	season = data.get("season", Season.SUMMER) as Season
	weather = data.get("weather", Weather.CLEAR) as Weather
	food_consumption_accumulator = float(data.get("food_accum", 0.0))
	water_consumption_accumulator = float(data.get("water_accum", 0.0))
	resources = data.get("resources", resources).duplicate()
	home_hex = Vector2i(int(data.get("home_x", 0)), int(data.get("home_y", 0)))
	settlement_chosen = bool(data.get("settlement_chosen", true))
	consecutive_hungry_days = int(data.get("hungry_days", 0))
	game_lost = bool(data.get("game_lost", false))
	game_won = bool(data.get("game_won", false))
	last_game_over_reason = str(data.get("game_over_reason", ""))
	last_victory_reason = str(data.get("victory_reason", ""))
	proved_hexes.clear()
	for key in data.get("proved_hexes", []):
		proved_hexes[str(key)] = true
	log_lines = PackedStringArray()
	for line in data.get("log", []):
		log_lines.append(str(line))
	fields.clear()
	for entry in data.get("fields", []):
		var field = FieldRes.from_dict(entry)
		fields[field.id] = field
	work_zones.clear()
	for entry in data.get("work_zones", []):
		var zone = WorkZoneRes.from_dict(entry)
		work_zones[zone.id] = zone
	structures.clear()
	buildings.clear()
	for entry in data.get("structures", []):
		var s = StructureRes.from_dict(entry)
		structures[s.coords] = s
	fences = HexEdge.from_dict_array(data.get("fences", []))
	active_field_id = str(data.get("active_field_id", ""))
	active_zone_id = str(data.get("active_zone_id", ""))
	plots.clear()
	orders.clear()
	_setup_household()
	for entry in data.get("persons", []):
		for person in persons:
			if person.id == str(entry.get("id", "")):
				person.health = int(entry.get("health", 100))
				person.alive = bool(entry.get("alive", true))
	labor_per_day = int(data.get("labor_per_day", household_labor()))
	labor_pool = int(data.get("labor_pool", labor_per_day))
	active_scenario = ScenarioCatalog.get_scenario(str(data.get("scenario_id", ScenarioCatalog.DEFAULT_SCENARIO_ID)))
	hex_sim = HexSim.new()
	hex_sim.load_hexes(data.get("hexes", []))
	_world_generated = true
	game_started.emit()


func begin_day_batch(day_count: int) -> void:
	_batch_mode = day_count > 1
	_batch_stats = {
		"days": day_count,
		"food_consumed": 0,
		"hungry_days": 0,
		"crops_killed": 0,
		"growth_days": 0,
		"drought_stalls": 0,
	}


func end_day_batch() -> void:
	if not _batch_mode:
		return
	var stats: Dictionary = _batch_stats
	_batch_mode = false
	if stats.get("days", 0) <= 0:
		return
	var msg := "Passed %d days: %d food eaten" % [stats["days"], stats["food_consumed"]]
	if stats["hungry_days"] > 0:
		msg += ", %d hungry days" % stats["hungry_days"]
	if stats["crops_killed"] > 0:
		msg += ", %d crop frost losses" % stats["crops_killed"]
	if stats["drought_stalls"] > 0:
		msg += ", %d drought stalls" % stats["drought_stalls"]
	_log(msg)
	day_batch_finished.emit(stats["days"])


func init_plots_from_map(tile_map: TileMapLayer) -> void:
	_map = tile_map
	MapGenerator.prepare_tile_map(tile_map)
	if not _pending_load.is_empty():
		rng.seed = int(_pending_load.get("seed", rng.seed))
	if not _pending_load.is_empty():
		apply_loaded_state()
		_rebuild_world_from_save()
		game_started.emit()
		return
	if not settlement_chosen and SceneRouter.entering_new_game:
		ensure_world_map()
		return
	if not _world_generated:
		generate_world_map()
	if settlement_chosen and persons.is_empty():
		_setup_household()
	_rebuild_world_from_map(tile_map)
	if settlement_chosen:
		_log("Summer %d (%s). Prepare the claim for winter." % [
			scenario_calendar_year(),
			WestTheme.era_name(scenario_calendar_year()),
		])
	if game_active and settlement_chosen:
		game_started.emit()


func generate_world_map() -> void:
	hex_sim = HexSim.new()
	var hexes := MapGenerator.generate_world(rng)
	hex_sim.build_from_hex_dict(hexes, home_hex)
	_world_generated = true


func ensure_world_map() -> void:
	if not _world_generated or hex_sim == null:
		generate_world_map()


func is_settleable(coords: Vector2i) -> bool:
	if hex_sim == null:
		return false
	var hex = hex_sim.get_hex(coords)
	return HexTopology.is_settleable(hex)


func world_coords() -> Array[Vector2i]:
	var out: Array[Vector2i] = []
	if hex_sim == null:
		return out
	for coords in hex_sim.hexes:
		out.append(coords)
	return out


func visible_coords(world_rect: Rect2) -> Array[Vector2i]:
	var out: Array[Vector2i] = []
	if hex_sim == null:
		return out
	for coords in HexGrid.cells_in_rect(world_rect, 2):
		if hex_sim.hexes.has(coords):
			out.append(coords)
	return out


func map_to_world(coords: Vector2i) -> Vector2:
	if _map != null and _map.tile_set != null:
		return _map.map_to_local(coords)
	return HexGrid.map_to_local(coords)


func _setup_household() -> void:
	persons.clear()
	holdings.clear()
	agents.clear()

	player_holding = Holding.new()
	player_holding.id = "player_farm"
	player_holding.name = "Your claim"
	player_holding.owner_id = "player"
	player_holding.home_hex = home_hex
	holdings.append(player_holding)

	var spouse := Person.new()
	spouse.id = "spouse"
	spouse.display_name = "Spouse"
	spouse.hex_coords = home_hex
	spouse.daily_labor = SPOUSE_LABOR
	spouse.rules = [{"action": "tend", "probability": 0.6}, {"action": "idle", "probability": 0.4}]
	persons.append(spouse)

	var child := Person.new()
	child.id = "child"
	child.display_name = "Child"
	child.hex_coords = home_hex
	child.daily_labor = CHILD_LABOR
	child.rules = [{"action": "tend", "probability": 0.35}, {"action": "idle", "probability": 0.65}]
	persons.append(child)

	var player_agent := Agent.new()
	player_agent.id = "player"
	player_agent.name = "You"
	player_agent.holding_id = player_holding.id
	player_agent.hex_coords = home_hex
	player_agent.is_player = true
	agents.append(player_agent)

	_setup_neighbor_holding()
	refresh_labor()


func _setup_neighbor_holding() -> void:
	if hex_sim == null:
		return
	var candidate: Vector2i = home_hex + Vector2i(12, 4)
	if hex_sim.get_hex(candidate) == null:
		for coords in hex_sim.hexes:
			if coords.distance_to(home_hex) > 8 and coords.distance_to(home_hex) < 20:
				candidate = coords
				break
	var neighbor := Holding.new()
	neighbor.id = "del_valle_rancho"
	neighbor.name = "Del Valle rancho"
	neighbor.owner_id = "del_valle"
	neighbor.home_hex = candidate
	holdings.append(neighbor)
	var steward := Agent.new()
	steward.id = "del_valle_steward"
	steward.name = "Hacienda del Valle steward"
	steward.holding_id = neighbor.id
	steward.hex_coords = candidate
	steward.is_player = false
	agents.append(steward)
	if hex_sim.get_hex(candidate) != null:
		hex_sim.get_hex(candidate).ownership = "del_valle"
		hex_sim.get_hex(candidate).population = 3


func _rebuild_world_from_map(_tile_map: TileMapLayer) -> void:
	if hex_sim == null:
		generate_world_map()
	_setup_neighbor_holding()


func _rebuild_world_from_save() -> void:
	if hex_sim == null:
		generate_world_map()
	_setup_neighbor_holding()
	_sync_fields_to_hexes()


func _place_starting_shelter() -> void:
	structures.clear()
	buildings.clear()
	var shelter = StructureRes.new()
	shelter.kind = StructureRes.Kind.SHELTER
	shelter.display_name = "Dugout"
	shelter.coords = home_hex
	structures[home_hex] = shelter
	var hex = get_hex(home_hex)
	if hex != null:
		hex.structure_id = "shelter"
		hex_sim.mark_dirty(home_hex)
		hex_sim.flush_aggregates()


func _sync_hex_from_plots() -> void:
	if hex_sim == null:
		return
	for coords in plots:
		var hex = hex_sim.get_hex(coords)
		if hex == null:
			continue
		var plot: PlotState = plots[coords]
		hex.terrain = HexStateRes.TERRAIN_FARMLAND
		hex.food = 0
		if not plot.is_empty():
			var crop: CropDefinition = get_crop(plot.crop_id)
			if crop != null:
				hex.food = crop.yield_food if plot.is_mature(crop) else 0
		hex.population = 1 if coords == home_hex else 0
		hex_sim.mark_dirty(coords)
	var home = hex_sim.get_hex(home_hex)
	if home != null:
		home.population = 1 + persons.size()
	hex_sim.flush_aggregates()


func _log(message: String) -> void:
	log_lines.append(message)
	if log_lines.size() > 12:
		log_lines.remove_at(0)
	log_added.emit(message)


func player_message(message: String) -> void:
	_log(message)


func reset_for_test(seed: int = 12345) -> void:
	start_new_game(seed)
	ensure_world_map()
	var settle := _first_settleable_hex()
	begin_settlement(settle)


func _first_settleable_hex() -> Vector2i:
	for coords in hex_sim.hexes:
		if is_settleable(coords):
			return coords
	return Vector2i.ZERO


func resolve_day(ended_day: int) -> void:
	_resolve_day(ended_day)


func _register_crops() -> void:
	var corn := CropDefinition.new()
	corn.id = "corn"
	corn.display_name = "Corn"
	corn.plant_seasons = [Season.SPRING, Season.AUTUMN]
	corn.grow_days = 28
	corn.yield_food = 8
	corn.seed_resource = "corn_seed"
	corn.frost_tolerant = false
	crops[corn.id] = corn

	var beans := CropDefinition.new()
	beans.id = "beans"
	beans.display_name = "Beans"
	beans.plant_seasons = [Season.SPRING, Season.SUMMER]
	beans.grow_days = 21
	beans.yield_food = 5
	beans.seed_resource = "bean_seed"
	beans.frost_tolerant = true
	crops[beans.id] = beans


func is_farm_plot(coords: Vector2i) -> bool:
	var hex = get_hex(coords)
	return hex != null and hex.field_id != ""


func is_home_hex(coords: Vector2i) -> bool:
	return coords == home_hex


func get_building(coords: Vector2i):
	return structures.get(coords)


func get_structure(coords: Vector2i):
	return structures.get(coords)


func can_claim_plot(_coords: Vector2i) -> bool:
	return false


func can_clear_wood(coords: Vector2i) -> bool:
	var hex = get_hex(coords)
	return hex != null and hex.standing_timber > 0.0


func try_clear_wood(coords: Vector2i) -> String:
	return try_chop_firewood(coords)


func try_chop_firewood(coords: Vector2i) -> String:
	if game_lost:
		return "Game over."
	var result := hex_sim.apply_work(coords, "chop_firewood", {})
	if not result.get("ok", false):
		return str(result.get("reason", "Cannot chop here."))
	_apply_work_yields(result)
	resources_changed.emit()
	plot_changed.emit(coords)
	_log("Chopped firewood at (%d,%d)." % [coords.x, coords.y])
	return "ok"


func try_claim_plot(_coords: Vector2i) -> String:
	return "Claiming is replaced by work zones."


func hex_terrain(coords: Vector2i) -> int:
	if hex_sim == null:
		return HexStateRes.TERRAIN_GRASS
	var hex = hex_sim.get_hex(coords)
	if hex == null:
		return HexStateRes.TERRAIN_GRASS
	return hex.terrain


func get_hex(coords: Vector2i):
	if hex_sim == null:
		return null
	return hex_sim.get_hex(coords)


func hex_status(coords: Vector2i) -> String:
	var hex = get_hex(coords)
	if hex == null:
		return "Unknown hex."
	var parts: PackedStringArray = []
	parts.append("Elev %.0f m · slope %.1f" % [hex.elevation, hex.slope_grade])
	parts.append("Soil %s · fertility %.0f%%" % [_soil_name(hex.soil_type), hex.fertility * 100.0])
	if hex.is_water():
		parts.append("Water (depth %.1f)" % hex.water_depth)
	elif hex.is_riparian:
		parts.append("Acequia runoff — good for water")
	elif hex.is_spring:
		parts.append("Spring seep — haul water here")
	if hex.has_forage():
		parts.append("Gathering available")
	elif hex.forage_depleted:
		parts.append("Picked clean this season")
	if hex.veg_class == HexStateRes.VegClass.SHRUB:
		parts.append("Mesquite scrub")
	if hex.standing_timber > 0:
		parts.append("Standing timber: %d" % int(hex.standing_timber))
	if hex.field_id != "":
		parts.append("In field %s" % hex.field_id)
	if hex.structure_id != "":
		parts.append("Structure: %s" % hex.structure_id)
	if hex.lore_tag != HexStateRes.LoreTag.NONE:
		var lore := WestTheme.lore_line(hex.lore_tag)
		if not lore.is_empty():
			parts.append(lore)
	return "\n".join(parts)


func _soil_name(soil: int) -> String:
	match soil:
		HexStateRes.SoilType.CLAY: return "clay"
		HexStateRes.SoilType.SAND: return "sand"
		HexStateRes.SoilType.PEAT: return "peat"
		HexStateRes.SoilType.ROCKY: return "rocky"
	return "loam"


func is_adjacent_to_holding(coords: Vector2i) -> bool:
	if hex_sim == null:
		return false
	for neighbor in HexGrid.neighbors(coords):
		var hex = get_hex(neighbor)
		if hex != null and (hex.ownership == "player" or hex.field_id != ""):
			return true
	return coords in HexGrid.neighbors(home_hex)


func get_plot(coords: Vector2i) -> PlotState:
	return plots.get(coords)


func get_crop(crop_id: String) -> CropDefinition:
	return crops.get(WestTheme.normalize_crop_id(crop_id))


func _migrate_loaded_data(data: Dictionary) -> Dictionary:
	var version := int(data.get("version", 1))
	if version >= SAVE_VERSION:
		return data
	var resources_data: Dictionary = data.get("resources", {}).duplicate()
	for old_key in WestTheme.SEED_ALIASES:
		if resources_data.has(old_key):
			var new_key: String = WestTheme.SEED_ALIASES[old_key]
			resources_data[new_key] = int(resources_data.get(new_key, 0)) + int(resources_data[old_key])
			resources_data.erase(old_key)
	data["resources"] = resources_data
	var fields_data: Array = data.get("fields", [])
	for i in fields_data.size():
		var entry: Dictionary = fields_data[i]
		if entry.has("crop_id"):
			entry["crop_id"] = WestTheme.normalize_crop_id(str(entry.get("crop_id", "")))
		fields_data[i] = entry
	data["fields"] = fields_data
	var zones_data: Array = data.get("work_zones", [])
	for i in zones_data.size():
		var entry: Dictionary = zones_data[i]
		if entry.has("crop_id") and not str(entry.get("crop_id", "")).is_empty():
			entry["crop_id"] = WestTheme.normalize_crop_id(str(entry.get("crop_id", "")))
		zones_data[i] = entry
	data["version"] = SAVE_VERSION
	if version < 4:
		data["game_won"] = bool(data.get("game_won", false))
		data["victory_reason"] = str(data.get("victory_reason", ""))
		if not data.has("proved_hexes"):
			data["proved_hexes"] = []
	return data


# --- Labour pool ---------------------------------------------------------

func household_labor() -> int:
	return day_engine.household_labor()


func refresh_labor() -> void:
	day_engine.refresh_labor()


func task_cost(type: String) -> int:
	return int(TASK_COST.get(type, 1))


# --- Work zones ----------------------------------------------------------

func _next_zone_id() -> String:
	return "zone_%d" % (work_zones.size() + 1)


func create_zone(type: int, crop_id: String = "", structure_kind: String = "") -> String:
	var zone := WorkZoneRes.new()
	zone.id = _next_zone_id()
	zone.type = type as WorkZoneRes.ZoneType
	zone.crop_id = crop_id
	zone.structure_kind = structure_kind
	work_zones[zone.id] = zone
	active_zone_id = zone.id
	return zone.id


func active_zone():
	return work_zones.get(active_zone_id)


func add_hex_to_active_zone(coords: Vector2i) -> bool:
	var zone = active_zone()
	if zone == null:
		return false
	if coords in zone.hexes:
		return false
	if not hex_sim.hexes.has(coords):
		return false
	zone.hexes.append(coords)
	plot_changed.emit(coords)
	return true


func remove_hex_from_zones(coords: Vector2i) -> void:
	for zone_id in work_zones:
		var zone = work_zones[zone_id]
		var idx: int = zone.hexes.find(coords)
		if idx >= 0:
			zone.hexes.remove_at(idx)
	plot_changed.emit(coords)


func zone_count() -> int:
	return work_zones.size()


func zone_label(zone) -> String:
	match zone.type:
		WorkZoneRes.ZoneType.FORAGE:
			return "gather"
		WorkZoneRes.ZoneType.CLEAR:
			return "clear brush"
		WorkZoneRes.ZoneType.TRAP:
			return "set snares"
		WorkZoneRes.ZoneType.COLLECT_WATER:
			return "haul water"
		WorkZoneRes.ZoneType.BUILD:
			if zone.structure_kind == "shelter":
				return "raise dugout"
			if zone.structure_kind == "house":
				return "build cabin"
			return "raise %s" % zone.structure_kind
	return "work"


func has_pending_orders() -> bool:
	return not work_zones.is_empty()


func work_today() -> void:
	day_engine.work_today()


func _zone_action_for(zone) -> String:
	match zone.type:
		WorkZoneRes.ZoneType.FORAGE: return "forage"
		WorkZoneRes.ZoneType.CLEAR: return "clear"
		WorkZoneRes.ZoneType.COLLECT_WATER: return "collect_water"
		WorkZoneRes.ZoneType.TRAP: return "trap"
		WorkZoneRes.ZoneType.BUILD: return "build"
	return ""


func _apply_work_yields(result: Dictionary) -> void:
	for key in result.get("yields", {}):
		resources[key] = resources.get(key, 0) + int(result["yields"][key])
	if result.has("wood"):
		resources["wood"] = resources.get("wood", 0) + int(result["wood"])
	if result.has("firewood"):
		resources["firewood"] = resources.get("firewood", 0) + int(result["firewood"])
	if result.has("water"):
		resources["water"] = resources.get("water", 0) + int(result["water"])
	if result.has("meat"):
		resources["meat"] = resources.get("meat", 0) + int(result["meat"])


func _do_build(coords: Vector2i, kind_name: String) -> void:
	var kind := StructureRes.Kind.SHELTER
	match kind_name:
		"barn": kind = StructureRes.Kind.BARN
		"shed": kind = StructureRes.Kind.SHED
		"trap": kind = StructureRes.Kind.TRAP
		"well": kind = StructureRes.Kind.WELL
		"house": kind = StructureRes.Kind.HOUSE
	var s = StructureRes.new()
	s.kind = kind
	s.coords = coords
	match kind_name:
		"house": s.display_name = "Cabin"
		"trap": s.display_name = "Snare"
		_: s.display_name = kind_name.capitalize()
	structures[coords] = s
	var hex = get_hex(coords)
	if hex != null:
		hex.structure_id = kind_name
		if kind == StructureRes.Kind.TRAP:
			hex.structure_id = "trap"


func buy_resource(resource: String, amount: int = 1) -> bool:
	if Trader.try_buy(resources, resource, amount):
		resources_changed.emit()
		_log("Bought %d %s." % [amount, resource])
		return true
	return false


func sell_resource(resource: String, amount: int = 1) -> bool:
	if Trader.try_sell(resources, resource, amount):
		resources_changed.emit()
		_log("Sold %d %s." % [amount, resource])
		return true
	return false


func has_shelter() -> bool:
	for coords in structures:
		var s = structures[coords]
		if s.kind == StructureRes.Kind.SHELTER or s.kind == StructureRes.Kind.HOUSE:
			return true
	return false


func has_cabin() -> bool:
	for coords in structures:
		var s = structures[coords]
		if s.kind == StructureRes.Kind.HOUSE:
			return true
	return false


func _prove_up_requirements() -> Dictionary:
	var scenario = active_scenario if active_scenario != null else ScenarioCatalog.get_default()
	return {
		"years": int(scenario.prove_up_years),
		"field_hexes": int(scenario.required_field_hexes),
		"requires_dwelling": bool(scenario.requires_dwelling),
	}


func prove_up_status() -> Dictionary:
	var req := _prove_up_requirements()
	var dwelling_done := has_cabin() if req["requires_dwelling"] else true
	var cultivated := proved_hexes.size()
	var ready: bool = (
		not game_lost
		and not game_won
		and year >= req["years"]
		and dwelling_done
		and cultivated >= req["field_hexes"]
	)
	return {
		"residence_years": year,
		"required_years": req["years"],
		"dwelling_done": dwelling_done,
		"cultivated_count": cultivated,
		"required_acres": req["field_hexes"],
		"ready": ready,
	}


func objective_summary() -> String:
	var status := prove_up_status()
	var cabin_text := "cabin: yes" if status["dwelling_done"] else "cabin: no"
	return "Prove up: %d/%d yrs · %s · %d/%d acres" % [
		status["residence_years"],
		status["required_years"],
		cabin_text,
		status["cultivated_count"],
		status["required_acres"],
	]


func can_prove_up() -> bool:
	return prove_up_status().get("ready", false)


func prove_up() -> bool:
	if not can_prove_up():
		return false
	game_won = true
	var scenario = active_scenario if active_scenario != null else ScenarioCatalog.get_default()
	last_victory_reason = scenario.victory_log
	if last_victory_reason.is_empty():
		last_victory_reason = "The homestead claim is proved."
	_log(last_victory_reason)
	victory_achieved.emit(last_victory_reason)
	return true


# --- Fields --------------------------------------------------------------

func create_field(crop_id: String = "") -> String:
	var field = FieldRes.new()
	field.id = "field_%d" % (fields.size() + 1)
	field.crop_id = crop_id
	fields[field.id] = field
	active_field_id = field.id
	return field.id


func ensure_active_field() -> String:
	if active_field_id != "" and fields.has(active_field_id):
		return active_field_id
	return create_field()


func add_hex_to_field(field_id: String, coords: Vector2i) -> bool:
	if not fields.has(field_id):
		return false
	var hex = get_hex(coords)
	if hex == null or hex.is_water():
		return false
	var field = fields[field_id]
	if coords in field.hexes:
		return false
	field.hexes.append(coords)
	hex.field_id = field_id
	hex.cleared = true
	hex.sync_terrain()
	hex_sim.mark_dirty(coords)
	hex_sim.flush_aggregates()
	plot_changed.emit(coords)
	return true


func add_hex_to_active_field(coords: Vector2i) -> bool:
	return add_hex_to_field(ensure_active_field(), coords)


func plant_field(field_id: String, crop_id: String) -> String:
	if game_lost:
		return "Game over."
	if not fields.has(field_id):
		return "No field."
	var field = fields[field_id]
	if field.hexes.is_empty():
		return "Paint field hexes first."
	if not field.is_empty():
		return "Field already planted."
	var crop := get_crop(crop_id)
	if crop == null:
		return "Unknown crop."
	if season not in crop.plant_seasons:
		return "%s cannot be planted in %s." % [crop.display_name, season_name()]
	if resources.get(crop.seed_resource, 0) < 1:
		return "Not enough seed."
	var cost: int = day_engine._field_labor_cost("plant_field", field)
	if labor_pool < cost:
		return "Need %d labor to plant." % cost
	resources[crop.seed_resource] -= 1
	field.crop_id = crop.id
	field.growth_days = 0
	field.tended = false
	field.planted_turn = TurnManager.turn_number
	labor_pool -= cost
	resources_changed.emit()
	_log("Planted %s across %d hexes." % [crop.display_name, field.hex_count()])
	return "ok"


func _hex_key(coords: Vector2i) -> String:
	return "%d,%d" % [coords.x, coords.y]


func toggle_fence(a: Vector2i, b: Vector2i) -> void:
	var key := HexEdge.edge_key(a, b)
	if fences.has(key):
		fences.erase(key)
	else:
		fences[key] = {"level": 1, "gate": false}
	plot_changed.emit(a)
	plot_changed.emit(b)


func _sync_fields_to_hexes() -> void:
	if hex_sim == null:
		return
	for field_id in fields:
		var field = fields[field_id]
		for coords in field.hexes:
			var hex = get_hex(coords)
			if hex != null:
				hex.field_id = field_id
	hex_sim.flush_aggregates()


# --- Legacy order stubs (UI compat) --------------------------------------

func order_for(_coords: Vector2i) -> Dictionary:
	return {}


func order_count() -> int:
	return work_zones.size()


func ensure_zone(type: int, structure_kind: String = "") -> String:
	for zone_id in work_zones:
		var zone = work_zones[zone_id]
		if zone.type == type and (structure_kind == "" or zone.structure_kind == structure_kind):
			active_zone_id = zone_id
			return zone_id
	return create_zone(type, "", structure_kind)


func assign_zone_hex(coords: Vector2i, type: int, structure_kind: String = "") -> String:
	if game_lost:
		return "Game over."
	ensure_zone(type, structure_kind)
	if not add_hex_to_active_zone(coords):
		return "Cannot add hex to zone."
	plot_changed.emit(coords)
	return "ok"


func assign_order(coords: Vector2i, type: String, crop_id: String = "") -> String:
	match type:
		"forage":
			return assign_zone_hex(coords, WorkZoneRes.ZoneType.FORAGE)
		"clear":
			return assign_zone_hex(coords, WorkZoneRes.ZoneType.CLEAR)
		"collect_water":
			return assign_zone_hex(coords, WorkZoneRes.ZoneType.COLLECT_WATER)
		"trap":
			return assign_zone_hex(coords, WorkZoneRes.ZoneType.TRAP, "trap")
		"build_shelter":
			return assign_zone_hex(coords, WorkZoneRes.ZoneType.BUILD, "shelter")
		"build_cabin":
			return assign_zone_hex(coords, WorkZoneRes.ZoneType.BUILD, "house")
		"build_barn":
			return assign_zone_hex(coords, WorkZoneRes.ZoneType.BUILD, "barn")
		"field":
			return "ok" if add_hex_to_active_field(coords) else "Cannot add to field."
		"plant":
			return plant_field(active_field_id, crop_id)
	return "Unknown action."


func has_order(coords: Vector2i) -> bool:
	return _hex_in_zone(coords)


func cancel_order(coords: Vector2i) -> bool:
	remove_hex_from_zones(coords)
	return true


func order_label(coords: Vector2i) -> String:
	for zone_id in work_zones:
		var zone = work_zones[zone_id]
		if coords in zone.hexes:
			return zone_label(zone)
	return ""


func needs_attention() -> bool:
	if hex_sim == null:
		return false
	for coords in hex_sim.hexes:
		var hex = hex_sim.hexes[coords]
		if hex.has_forage() and not _hex_in_zone(coords):
			return true
	for field_id in fields:
		var field = fields[field_id]
		if field.is_empty():
			continue
		var crop := get_crop(field.crop_id)
		if field.is_mature(crop):
			return true
	return false


func _hex_in_zone(coords: Vector2i) -> bool:
	for zone_id in work_zones:
		if coords in work_zones[zone_id].hexes:
			return true
	return false


func family_summary() -> String:
	var names: PackedStringArray = []
	for person in persons:
		if person.alive:
			names.append("%s (%d%%)" % [person.display_name, person.health])
	if names.is_empty():
		return "Family: none surviving"
	return "Family: " + ", ".join(names)


func holdings_summary() -> String:
	if player_holding == null:
		return ""
	var parts: PackedStringArray = []
	for holding in holdings:
		if holding.id == player_holding.id:
			parts.append("Claim (%d,%d)" % [home_hex.x, home_hex.y])
		else:
			parts.append("Neighbor — %s" % holding.name)
	return " · ".join(parts)


func resources_summary() -> String:
	var seed_total := int(resources.get("corn_seed", 0)) + int(resources.get("bean_seed", 0))
	return "%s %d · %s %d · %s %d · %s %d · %s %d · Seeds %d · Tools %d" % [
		WestTheme.resource_name("food"),
		total_food(),
		WestTheme.resource_name("water"),
		resources.get("water", 0),
		WestTheme.resource_name("firewood"),
		resources.get("firewood", 0),
		WestTheme.resource_name("wood"),
		resources.get("wood", 0),
		WestTheme.resource_name("coins"),
		resources.get("coins", 0),
		seed_total,
		resources.get("tools", 0),
	]


func total_food() -> int:
	return (
		resources.get("food", 0)
		+ resources.get("berries", 0)
		+ resources.get("roots", 0)
		+ resources.get("mushrooms", 0)
		+ resources.get("meat", 0) * 2
	)


func living_count() -> int:
	var count := 1
	for person in persons:
		if person.alive:
			count += 1
	return count


func season_name() -> String:
	match season:
		Season.SPRING: return "Spring"
		Season.SUMMER: return "Summer"
		Season.AUTUMN: return "Autumn"
		Season.WINTER: return "Winter"
	return "?"


func weather_name() -> String:
	match weather:
		Weather.CLEAR: return "Clear"
		Weather.RAIN: return "Rain"
		Weather.DROUGHT: return "Drought"
		Weather.FROST: return "Frost"
	return "?"


func day_in_season(turn_number: int) -> int:
	return ((turn_number - 1) % DAYS_PER_SEASON) + 1


func scenario_calendar_year() -> int:
	if active_scenario == null:
		return 1863 + year - 1
	return active_scenario.calendar_year(year)


func scenario_menu_line() -> String:
	if active_scenario == null:
		return ScenarioCatalog.get_default().menu_line()
	return active_scenario.menu_line()


func scenario_settlement_title() -> String:
	if active_scenario == null:
		return ScenarioCatalog.get_default().settlement_title
	return active_scenario.settlement_title


func calendar_label(turn_number: int) -> String:
	_sync_calendar_from_turn(turn_number)
	return "%d · %s · Day %d/%d · %s" % [
		scenario_calendar_year(),
		season_name(),
		day_in_season(turn_number),
		DAYS_PER_SEASON,
		weather_name(),
	]


func render_level_name(zoom: float) -> String:
	if zoom < 0.07:
		return "Zone view"
	if zoom < 0.2:
		return "Block view"
	if zoom < 0.55:
		return "Patch view"
	return "Hex view"


func _sync_calendar_from_turn(turn_number: int) -> void:
	var idx := maxi(turn_number - 1, 0)
	var new_season := (idx / DAYS_PER_SEASON) % 4 as Season
	var new_year := 1 + idx / DAYS_PER_YEAR
	if new_season != season or new_year != year:
		season = new_season
		year = new_year
		season_changed.emit(season, year)


func try_plant(coords: Vector2i, crop_id: String) -> String:
	return _spend_labor("plant", _do_plant(coords, crop_id))


func _do_plant(coords: Vector2i, crop_id: String) -> String:
	if game_lost:
		return "Game over."
	var plot := get_plot(coords)
	if plot == null:
		return "Not a farm plot."
	if not plot.is_empty():
		return "Plot already has a crop."
	var crop: CropDefinition = get_crop(crop_id)
	if crop == null:
		return "Unknown crop."
	if season not in crop.plant_seasons:
		return "%s cannot be planted in %s." % [crop.display_name, season_name()]
	if weather == Weather.FROST and not crop.frost_tolerant:
		return "Too cold to plant."
	var seed_key: String = crop.seed_resource
	if resources.get(seed_key, 0) < 1:
		return "Not enough seed."
	resources[seed_key] -= 1
	plot.crop_id = crop_id
	plot.growth_days = 0
	plot.tended = false
	_mark_plot_changed(coords)
	_log("Planted %s." % crop.display_name)
	return "ok"


func try_tend(coords: Vector2i) -> String:
	return _spend_labor("tend", _do_tend(coords))


func _do_tend(coords: Vector2i) -> String:
	if game_lost:
		return "Game over."
	var plot := get_plot(coords)
	if plot == null or plot.is_empty():
		return "Nothing to tend here."
	plot.tended = true
	_mark_plot_changed(coords)
	_log("Tended the %s." % get_crop(plot.crop_id).display_name)
	return "ok"


func try_harvest(coords: Vector2i) -> String:
	return _spend_labor("harvest", _do_harvest(coords))


func _do_harvest(coords: Vector2i) -> String:
	if game_lost:
		return "Game over."
	var plot := get_plot(coords)
	if plot == null or plot.is_empty():
		return "Nothing to harvest."
	var crop: CropDefinition = get_crop(plot.crop_id)
	if crop == null:
		return "Unknown crop on plot."
	if not plot.is_mature(crop):
		return "%s is not ready (%d/%d days)." % [
			crop.display_name, plot.growth_days, crop.grow_days
		]
	resources["food"] += crop.yield_food
	var name := crop.display_name
	plot.clear()
	_mark_plot_changed(coords)
	_log("Harvested %s (+ %d food)." % [name, crop.yield_food])
	return "ok"


## Charge the labour pool when an immediate action succeeded. `result` is the
## outcome of the matching `_do_*` call; labour is only spent on "ok".
func _spend_labor(type: String, result: String) -> String:
	if result == "ok":
		labor_pool = maxi(0, labor_pool - task_cost(type))
	return result


func _mark_plot_changed(coords: Vector2i) -> void:
	resources_changed.emit()
	plot_changed.emit(coords)
	_sync_hex_from_plots()


func plot_work_type(coords: Vector2i) -> String:
	if not is_farm_plot(coords):
		if can_clear_wood(coords):
			return "clear_wood"
		return "claim" if can_claim_plot(coords) else "none"
	var plot := get_plot(coords)
	if plot == null:
		return "none"
	if plot.is_empty():
		return "plant" if _can_plant_any_crop() else "none"
	var crop: CropDefinition = get_crop(plot.crop_id)
	if plot.is_mature(crop):
		return "harvest"
	if _needs_tend(plot, crop):
		return "tend"
	return "none"


func plot_growth_ratio(coords: Vector2i) -> float:
	var plot := get_plot(coords)
	if plot == null or plot.is_empty():
		return 0.0
	var crop: CropDefinition = get_crop(plot.crop_id)
	if crop == null or crop.grow_days <= 0:
		return 0.0
	return clampf(float(plot.growth_days) / float(crop.grow_days), 0.0, 1.0)


func plot_status(coords: Vector2i) -> String:
	return hex_status(coords)


func has_actionable_work() -> bool:
	return needs_attention() or has_pending_orders()


func _plot_has_work(_plot: PlotState) -> bool:
	return false


func _can_plant_any_crop() -> bool:
	for crop_id in crops:
		if _can_plant_crop(crop_id):
			return true
	return false


func _can_plant_crop(crop_id: String) -> bool:
	var crop: CropDefinition = get_crop(crop_id)
	if crop == null:
		return false
	if season not in crop.plant_seasons:
		return false
	if weather == Weather.FROST and not crop.frost_tolerant:
		return false
	return resources.get(crop.seed_resource, 0) >= 1


func _needs_tend(plot: PlotState, crop: CropDefinition) -> bool:
	if crop == null:
		return false
	if plot.tended or plot.is_mature(crop):
		return false
	if weather == Weather.DROUGHT:
		return true
	if weather == Weather.FROST and not crop.frost_tolerant:
		return true
	return false


func _on_day_ended(ended_day: int) -> void:
	if game_lost:
		return
	_resolve_day(ended_day)


func _resolve_day(ended_day: int) -> void:
	day_engine._resolve_day(ended_day)


func _reset_seasonal_forage() -> void:
	if hex_sim == null:
		return
	for coords in hex_sim.hexes:
		var hex = hex_sim.hexes[coords]
		if hex.forage_mask != 0:
			hex.forage_depleted = false


func _all_family_dead() -> bool:
	for person in persons:
		if person.alive:
			return false
	return true


func _roll_weather() -> void:
	var roll := rng.randf()
	match season:
		Season.SPRING:
			if roll < 0.35:
				weather = Weather.RAIN
			elif roll < 0.75:
				weather = Weather.CLEAR
			elif roll < 0.9:
				weather = Weather.DROUGHT
			else:
				weather = Weather.FROST
		Season.SUMMER:
			if roll < 0.25:
				weather = Weather.RAIN
			elif roll < 0.55:
				weather = Weather.CLEAR
			else:
				weather = Weather.DROUGHT
		Season.AUTUMN:
			if roll < 0.3:
				weather = Weather.RAIN
			elif roll < 0.7:
				weather = Weather.CLEAR
			elif roll < 0.85:
				weather = Weather.DROUGHT
			else:
				weather = Weather.FROST
		Season.WINTER:
			if roll < 0.2:
				weather = Weather.CLEAR
			elif roll < 0.45:
				weather = Weather.RAIN
			else:
				weather = Weather.FROST
	weather_changed.emit(weather)


func _place_starting_buildings() -> void:
	if not buildings.is_empty():
		return
	var house := FarmBuilding.new()
	house.id = "farmhouse"
	house.display_name = "Farmhouse"
	house.kind = FarmBuilding.Kind.HOUSE
	house.hex_coords = home_hex
	buildings[home_hex] = house
	for coords in plots:
		if coords == home_hex:
			continue
		var barn := FarmBuilding.new()
		barn.id = "barn"
		barn.display_name = "Barn"
		barn.kind = FarmBuilding.Kind.BARN
		barn.hex_coords = coords
		buildings[coords] = barn
		break


func _apply_cleared_woods() -> void:
	if hex_sim == null:
		return
	for coords in cleared_woods:
		var hex = hex_sim.get_hex(coords)
		if hex == null:
			continue
		hex.terrain = HexStateRes.TERRAIN_GRASS
		hex.forest = 0.0
		hex_sim.mark_dirty(coords)


func _serialize_buildings() -> Array:
	var out: Array = []
	for coords in buildings:
		var b: FarmBuilding = buildings[coords]
		out.append({
			"x": coords.x,
			"y": coords.y,
			"id": b.id,
			"name": b.display_name,
			"kind": b.kind,
		})
	return out


func _serialize_coords_list(coords_list: Array[Vector2i]) -> Array:
	var out: Array = []
	for coords in coords_list:
		out.append({"x": coords.x, "y": coords.y})
	return out


func _load_buildings_from_save(data: Array) -> void:
	buildings.clear()
	for entry in data:
		var coords := Vector2i(int(entry["x"]), int(entry["y"]))
		var b := FarmBuilding.new()
		b.id = str(entry.get("id", ""))
		b.display_name = str(entry.get("name", ""))
		b.kind = int(entry.get("kind", FarmBuilding.Kind.HOUSE))
		b.hex_coords = coords
		buildings[coords] = b
