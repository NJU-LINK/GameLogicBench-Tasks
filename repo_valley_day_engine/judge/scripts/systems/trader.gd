extends RefCounted

const WestTheme = preload("res://scripts/theme/west_theme.gd")

## Fixed buy/sell prices in dollars (stored as coins).
const PRICES := {
	"food": {"buy": 3, "sell": 2},
	"water": {"buy": 2, "sell": 1},
	"firewood": {"buy": 4, "sell": 2},
	"wood": {"buy": 3, "sell": 2},
	"berries": {"buy": 2, "sell": 1},
	"roots": {"buy": 2, "sell": 1},
	"mushrooms": {"buy": 3, "sell": 2},
	"meat": {"buy": 5, "sell": 3},
	"corn_seed": {"buy": 4, "sell": 2},
	"bean_seed": {"buy": 3, "sell": 2},
	"tools": {"buy": 8, "sell": 4},
}


static func _price_key(resource: String) -> String:
	return WestTheme.normalize_seed_key(resource)


static func buy_price(resource: String) -> int:
	var entry: Dictionary = PRICES.get(_price_key(resource), {})
	return int(entry.get("buy", 0))


static func sell_price(resource: String) -> int:
	var entry: Dictionary = PRICES.get(_price_key(resource), {})
	return int(entry.get("sell", 0))


static func try_buy(resources: Dictionary, resource: String, amount: int = 1) -> bool:
	var key := _price_key(resource)
	var cost := buy_price(key) * amount
	if cost <= 0:
		return false
	if resources.get("coins", 0) < cost:
		return false
	resources["coins"] -= cost
	resources[key] = resources.get(key, 0) + amount
	return true


static func try_sell(resources: Dictionary, resource: String, amount: int = 1) -> bool:
	var key := _price_key(resource)
	var price := sell_price(key)
	if price <= 0:
		return false
	if resources.get(key, 0) < amount:
		return false
	resources[key] -= amount
	resources["coins"] = resources.get("coins", 0) + price * amount
	return true
