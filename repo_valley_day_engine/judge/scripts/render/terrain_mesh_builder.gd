class_name TerrainMeshBuilder
extends RefCounted

const TerrainLayout = preload("res://scripts/render/terrain_layout.gd")
const HexGrid = preload("res://scripts/world/hex_grid.gd")
const WestTheme = preload("res://scripts/theme/west_theme.gd")

const GROUND_Y := -3.0


static func build_hex_mesh(coords_list: Array[Vector2i]) -> ArrayMesh:
	var tool := SurfaceTool.new()
	tool.begin(Mesh.PRIMITIVE_TRIANGLES)
	for coords in coords_list:
		_add_hex(tool, coords)
	tool.generate_normals()
	return tool.commit()


static func _add_hex(tool: SurfaceTool, coords: Vector2i) -> void:
	var hex: HexState = GameState.get_hex(coords)
	if hex == null:
		return
	var neighbors := HexGrid.neighbors(coords)
	var center := TerrainLayout.hex_to_world_3d(coords, hex.elevation)
	var top_color := _top_color(hex)
	var side_color := top_color.darkened(0.18)
	if hex.is_water():
		_add_flat_hex(tool, coords, hex.elevation, top_color)
		return
	var edge_segments: Array[Array] = _edge_segments(coords, hex)
	_add_top_fan(tool, center, edge_segments, top_color)
	_add_boundary_skirts(tool, hex, neighbors, edge_segments, side_color)


static func _add_flat_hex(tool: SurfaceTool, coords: Vector2i, elevation: float, color: Color) -> void:
	var center := TerrainLayout.hex_to_world_3d(coords, elevation)
	var hex := HexState.new()
	hex.elevation = elevation
	var edge_segments: Array[Array] = _edge_segments(coords, hex)
	_add_top_fan(tool, center, edge_segments, color)


static func _edge_segments(coords: Vector2i, hex: HexState) -> Array[Array]:
	var corners := TerrainLayout.side_corners(coords)
	var segments: Array[Array] = []
	for edge_i in range(6):
		var next_i := (edge_i + 1) % 6
		var edge_y := TerrainLayout.edge_height(hex.elevation, hex.elevation)
		var start := Vector3(corners[edge_i].x, edge_y, corners[edge_i].y)
		var end := Vector3(corners[next_i].x, edge_y, corners[next_i].y)
		segments.append([start, end])
	return segments


static func _add_top_fan(
	tool: SurfaceTool,
	center: Vector3,
	edge_segments: Array[Array],
	color: Color
) -> void:
	for edge_i in range(6):
		var segment: Array = edge_segments[edge_i]
		var top_a: Vector3 = segment[0]
		var top_b: Vector3 = segment[1]
		_add_triangle(tool, center, top_b, top_a, color)


## Skirts only on map edges, water, and cliffs; regular neighbors share flat top edges.
static func _add_boundary_skirts(
	tool: SurfaceTool,
	hex: HexState,
	neighbors: Array[Vector2i],
	edge_segments: Array[Array],
	side_color: Color
) -> void:
	for edge_i in range(6):
		if not _rim_needs_skirt(hex, neighbors, edge_i):
			continue
		var segment: Array = edge_segments[edge_i]
		var top_a: Vector3 = segment[0]
		var top_b: Vector3 = segment[1]
		var is_cliff := (hex.cliff_edges & (1 << edge_i)) != 0
		var face_color := WestTheme.COLOR_CLIFF if is_cliff else side_color
		var base_y := _skirt_base_y(top_a.y, top_b.y, neighbors, edge_i)
		var base_a := Vector3(top_a.x, base_y, top_a.z)
		var base_b := Vector3(top_b.x, base_y, top_b.z)
		_add_quad(tool, top_a, top_b, base_b, base_a, face_color)


static func _rim_needs_skirt(hex: HexState, neighbors: Array[Vector2i], edge_i: int) -> bool:
	var neighbor_coords: Vector2i = neighbors[edge_i]
	if GameState.hex_sim == null or not GameState.hex_sim.hexes.has(neighbor_coords):
		return true
	var neighbor: HexState = GameState.get_hex(neighbor_coords)
	if neighbor == null:
		return true
	if neighbor.is_water():
		return true
	if (hex.cliff_edges & (1 << edge_i)) != 0:
		return true
	return false


static func _skirt_base_y(top_a_y: float, top_b_y: float, neighbors: Array[Vector2i], edge_i: int) -> float:
	var lowest := mini(top_a_y, top_b_y)
	var neighbor: HexState = GameState.get_hex(neighbors[edge_i])
	if neighbor != null:
		lowest = mini(lowest, TerrainLayout.edge_height(neighbor.elevation, neighbor.elevation))
	return mini(GROUND_Y, lowest - 6.0)


static func _add_quad(
	tool: SurfaceTool,
	a: Vector3,
	b: Vector3,
	c: Vector3,
	d: Vector3,
	color: Color
) -> void:
	_add_triangle(tool, a, b, c, color)
	_add_triangle(tool, a, c, d, color)


static func _add_triangle(tool: SurfaceTool, a: Vector3, b: Vector3, c: Vector3, color: Color) -> void:
	var normal := (b - a).cross(c - a)
	if normal.length_squared() < 0.0001:
		return
	tool.set_color(color)
	tool.set_normal(normal.normalized())
	tool.add_vertex(a)
	tool.set_color(color)
	tool.add_vertex(b)
	tool.set_color(color)
	tool.add_vertex(c)


static func _top_color(hex: HexState) -> Color:
	var elev_shade := clampf(0.5 - hex.elevation / 80.0, -0.25, 0.25)
	var base := WestTheme.COLOR_GRASS.darkened(-elev_shade)
	if hex.is_water():
		base = WestTheme.COLOR_WATER
	elif hex.veg_class == HexState.VegClass.WOODLAND:
		base = WestTheme.COLOR_WOOD
	elif hex.field_id != "":
		base = WestTheme.COLOR_FIELD
	elif hex.cleared:
		base = WestTheme.COLOR_CLEARED
	return base
