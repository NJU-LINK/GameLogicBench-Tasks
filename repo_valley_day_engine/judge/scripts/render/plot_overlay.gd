extends Node2D

const HexGrid = preload("res://scripts/world/hex_grid.gd")
const WestTheme = preload("res://scripts/theme/west_theme.gd")

var selected_hex: Vector2i = Vector2i(999999, 999999)
var selected_hexes: Array[Vector2i] = []
var _hex_view: bool = true
var _font: Font
var _camera: Camera2D


func setup(_map: TileMapLayer, camera: Camera2D = null) -> void:
	_font = ThemeDB.fallback_font
	_camera = camera


func set_hex_view(enabled: bool) -> void:
	_hex_view = enabled
	visible = enabled
	queue_redraw()


func set_selected(coords: Vector2i) -> void:
	set_selected_hexes([coords])


func set_selected_hexes(hexes: Array[Vector2i]) -> void:
	selected_hexes = hexes
	if hexes.size() > 0:
		selected_hex = hexes[0]
	else:
		selected_hex = Vector2i(999999, 999999)
	queue_redraw()


func refresh() -> void:
	queue_redraw()


func _visible_coords() -> Array[Vector2i]:
	if _camera != null:
		var vp := get_viewport_rect().size
		var half := vp / (_camera.zoom * 2.0)
		var center := _camera.get_screen_center_position()
		return GameState.visible_coords(Rect2(center - half, half * 2.0))
	return GameState.world_coords()


func _draw() -> void:
	if not _hex_view or GameState.hex_sim == null:
		return
	for coords in _visible_coords():
		_draw_hex(coords)
	for coords in GameState.structures:
		_draw_structure(coords, GameState.structures[coords])
	for coords in selected_hexes:
		_draw_selection(coords)


func _draw_hex(coords: Vector2i) -> void:
	var hex: HexState = GameState.get_hex(coords)
	if hex == null:
		return
	var elev_shade := clampf(0.5 - hex.elevation / 80.0, -0.25, 0.25)
	var base := WestTheme.with_alpha(WestTheme.COLOR_GRASS.darkened(-elev_shade), 0.35)
	if hex.is_water():
		base = WestTheme.with_alpha(WestTheme.COLOR_WATER, 0.75)
	elif hex.veg_class == HexState.VegClass.WOODLAND:
		base = WestTheme.with_alpha(WestTheme.COLOR_WOOD, 0.72)
	elif hex.field_id != "":
		base = WestTheme.with_alpha(WestTheme.COLOR_FIELD, 0.62)
	_draw_hex_fill(coords, base)
	if hex.cliff_edges != 0:
		_draw_cliff_edges(coords, hex.cliff_edges)
	if hex.river_flow >= 0:
		_draw_flow_arrow(coords, hex.river_flow)
	if hex.has_forage():
		_draw_forage_marker(coords, hex.forage_mask)
	if coords == GameState.home_hex:
		_draw_hex_border(coords, WestTheme.with_alpha(WestTheme.COLOR_HOME, 0.5), 2.5, false)
	var zone_label := GameState.order_label(coords)
	if not zone_label.is_empty():
		_draw_hex_border(coords, WestTheme.with_alpha(WestTheme.COLOR_ZONE, 0.95), 2.5, true)
		_draw_label(coords, zone_label, 10, Vector2(0, -18))


func _draw_cliff_edges(coords: Vector2i, mask: int) -> void:
	var points := _hex_points(coords)
	for i in points.size():
		if mask & (1 << i):
			var a: Vector2 = points[i]
			var b: Vector2 = points[(i + 1) % points.size()]
			draw_line(a, b, WestTheme.with_alpha(WestTheme.COLOR_CLIFF, 0.95), 5.0)


func _draw_flow_arrow(coords: Vector2i, direction: int) -> void:
	var center := _hex_center(coords)
	var neighbors := HexGrid.neighbors(coords)
	if direction < 0 or direction >= neighbors.size():
		return
	var target := GameState.map_to_world(neighbors[direction])
	var dir := (target - center).normalized()
	draw_line(center, center + dir * 18.0, WestTheme.COLOR_RIVER, 2.0)


func _draw_forage_marker(coords: Vector2i, mask: int) -> void:
	var center := _hex_center(coords)
	if mask & HexState.FORAGE_BERRIES:
		draw_circle(center + Vector2(-10, -8), 4.0, WestTheme.with_alpha(WestTheme.COLOR_FORAGE, 0.9))
	if mask & HexState.FORAGE_ROOTS:
		draw_circle(center + Vector2(8, 6), 3.5, Color(0.7, 0.5, 0.25))
	if mask & HexState.FORAGE_MUSHROOMS:
		draw_circle(center + Vector2(0, 10), 3.5, Color(0.8, 0.3, 0.3))


func _draw_structure(coords: Vector2i, structure: Structure) -> void:
	var center := _hex_center(coords)
	match structure.kind:
		Structure.Kind.SHELTER, Structure.Kind.HOUSE:
			draw_rect(Rect2(center.x - 14, center.y - 8, 28, 18), WestTheme.with_alpha(WestTheme.COLOR_SHELTER, 0.95))
			_draw_label(coords, structure.display_name, 9, Vector2(0, 24))
		Structure.Kind.BARN, Structure.Kind.SHED:
			draw_rect(Rect2(center.x - 16, center.y - 10, 32, 20), WestTheme.with_alpha(WestTheme.COLOR_BARN, 0.95))
		Structure.Kind.TRAP:
			draw_rect(Rect2(center.x - 8, center.y - 8, 16, 16), WestTheme.with_alpha(WestTheme.COLOR_TRAP, 0.95))
			_draw_label(coords, "SNARE", 9)
		Structure.Kind.WELL:
			draw_circle(center, 8.0, WestTheme.with_alpha(WestTheme.COLOR_WELL, 0.95))


func _draw_selection(coords: Vector2i) -> void:
	_draw_hex_border(coords, WestTheme.COLOR_SELECT, 4.0, false)


func _draw_hex_fill(coords: Vector2i, color: Color) -> void:
	draw_colored_polygon(_hex_points(coords), color)


func _draw_hex_border(coords: Vector2i, color: Color, width: float, dashed: bool) -> void:
	var points := _hex_points(coords)
	points.append(points[0])
	if dashed:
		for i in points.size() - 1:
			if i % 2 == 0:
				draw_line(points[i], points[i + 1], color, width)
	else:
		draw_polyline(points, color, width)


func _draw_label(coords: Vector2i, text: String, size: int, offset: Vector2 = Vector2.ZERO) -> void:
	if _font == null or text.is_empty():
		return
	var center := _hex_center(coords) + offset
	var text_size := _font.get_string_size(text, HORIZONTAL_ALIGNMENT_CENTER, -1, size)
	draw_string(
		_font,
		center - text_size * 0.5,
		text,
		HORIZONTAL_ALIGNMENT_LEFT,
		-1,
		size,
		WestTheme.COLOR_LABEL
	)


func _hex_center(coords: Vector2i) -> Vector2:
	return GameState.map_to_world(coords)


func _hex_points(coords: Vector2i) -> PackedVector2Array:
	return HexGrid.hex_corners(_hex_center(coords))
