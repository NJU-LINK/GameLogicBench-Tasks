extends Node2D

enum RenderLevel { HEX, PATCH, BLOCK, ZONE }

const ZOOM_PATCH := 0.55
const ZOOM_BLOCK := 0.2
const ZOOM_ZONE := 0.07

const HexGrid = preload("res://scripts/world/hex_grid.gd")
const AggregateBucket = preload("res://scripts/world/aggregate_bucket.gd")
const WestTheme = preload("res://scripts/theme/west_theme.gd")

const TERRAIN_COLORS := {
	0: WestTheme.COLOR_AGG_GRASS,
	1: WestTheme.COLOR_AGG_FIELD,
}

var tile_map: TileMapLayer
var camera: Camera2D
var render_level: RenderLevel = RenderLevel.HEX


func setup(map: TileMapLayer, cam: Camera2D) -> void:
	tile_map = map
	camera = cam
	z_index = 5


func _process(_delta: float) -> void:
	if camera == null:
		return
	var zoom := camera.zoom.x
	var next := RenderLevel.HEX
	if zoom < ZOOM_ZONE:
		next = RenderLevel.ZONE
	elif zoom < ZOOM_BLOCK:
		next = RenderLevel.BLOCK
	elif zoom < ZOOM_PATCH:
		next = RenderLevel.PATCH
	if next != render_level:
		render_level = next
		queue_redraw()
	if tile_map != null:
		tile_map.visible = false


func _draw() -> void:
	if render_level == RenderLevel.HEX or GameState.hex_sim == null:
		return
	var cache := GameState.hex_sim.aggregate
	var store: Dictionary
	var cell_size: float
	match render_level:
		RenderLevel.PATCH:
			store = cache.patches
			cell_size = float(HexGrid.TILE_SIZE.x) * 10.0
		RenderLevel.BLOCK:
			store = cache.blocks
			cell_size = float(HexGrid.TILE_SIZE.x) * 100.0
		RenderLevel.ZONE:
			store = cache.zones
			cell_size = float(HexGrid.TILE_SIZE.x) * 1000.0
		_:
			return
	for key in store:
		var bucket: AggregateBucket = store[key]
		if bucket.plot_count <= 0 and bucket.population <= 0:
			continue
		var origin := Vector2(bucket.id.x * cell_size, bucket.id.y * cell_size)
		var color: Color = TERRAIN_COLORS.get(bucket.terrain, TERRAIN_COLORS[0])
		if bucket.plot_count > 0:
			color = color.lerp(WestTheme.COLOR_AGG_HIGHLIGHT, 0.35)
		draw_rect(Rect2(origin, Vector2(cell_size, cell_size)), color)
