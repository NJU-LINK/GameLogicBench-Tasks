class_name HexHydrology
extends RefCounted

const HexGrid = preload("res://scripts/world/hex_grid.gd")
const HexStateRes = preload("res://scripts/world/hex_state.gd")

const FLOW_MIN_DROP := 0.8


static func apply(hexes: Dictionary) -> void:
	for coords in hexes:
		var hex: HexStateRes = hexes[coords]
		if hex.is_water():
			hex.water_depth = maxf(hex.water_depth, 1.0)
			continue
		var best_dir := -1
		var best_drop := 0.0
		var neighbors := HexGrid.neighbors(coords)
		for i in neighbors.size():
			var n: Vector2i = neighbors[i]
			if not hexes.has(n):
				continue
			var neighbor: HexStateRes = hexes[n]
			var drop: float = hex.elevation - neighbor.elevation
			if drop > best_drop:
				best_drop = drop
				best_dir = i
		if best_drop >= FLOW_MIN_DROP:
			hex.river_flow = best_dir
		else:
			hex.river_flow = -1
		if hex.moisture > 0.25 or hex.is_riparian:
			hex.is_riparian = hex.moisture > 0.2
	for coords in hexes:
		var hex: HexStateRes = hexes[coords]
		if hex.river_flow < 0:
			continue
		var neighbors := HexGrid.neighbors(coords)
		if hex.river_flow >= neighbors.size():
			continue
		var downstream: Vector2i = neighbors[hex.river_flow]
		if hexes.has(downstream):
			var down: HexStateRes = hexes[downstream]
			down.is_riparian = true
			down.moisture = maxf(down.moisture, 0.15)
