extends RefCounted

const BucketAssigner = preload("res://scripts/world/bucket_assigner.gd")
const AggregateCache = preload("res://scripts/world/aggregate_cache.gd")
const HexStateRes = preload("res://scripts/world/hex_state.gd")
const HexTopology = preload("res://scripts/world/hex_topology.gd")

var hexes: Dictionary = {}
var aggregate: AggregateCache = AggregateCache.new()
var field_coords: Dictionary = {}
var rng := RandomNumberGenerator.new()


func build_from_hex_dict(source: Dictionary, settlement_hex: Vector2i = Vector2i.ZERO) -> void:
	hexes.clear()
	field_coords.clear()
	for coords in source:
		var src = source[coords]
		var hex = HexStateRes.from_dict(src.to_dict())
		hex.coords = coords
		hex.patch_id = BucketAssigner.patch_id_from_coords(coords)
		hex.block_id = BucketAssigner.block_id_from_coords(coords)
		hex.zone_id = BucketAssigner.zone_id_from_coords(coords)
		hex.chunk_id = Vector2i(coords.x >> 4, coords.y >> 4)
		if coords == settlement_hex:
			hex.ownership = "player"
			hex.population = 1
		hexes[coords] = hex
		aggregate.mark_hex_dirty(hex)
	flush_aggregates(field_coords)


func load_hexes(serialized: Array) -> void:
	hexes.clear()
	field_coords.clear()
	for entry in serialized:
		var hex = HexStateRes.from_dict(entry)
		hex.coords = Vector2i(int(entry.get("x", 0)), int(entry.get("y", 0)))
		hexes[hex.coords] = hex
		if hex.field_id != "":
			field_coords[hex.coords] = true
		aggregate.mark_hex_dirty(hex)
	flush_aggregates(field_coords)


func serialize_hexes() -> Array:
	var out: Array = []
	for coords in hexes:
		out.append(hexes[coords].to_dict())
	return out


func mark_dirty(coords: Vector2i) -> void:
	var hex = get_hex(coords)
	if hex == null:
		return
	hex.dirty = true
	aggregate.mark_hex_dirty(hex)


func get_hex(coords: Vector2i):
	return hexes.get(coords)


func flush_aggregates(plot_coords: Dictionary = {}) -> void:
	var plots := plot_coords if not plot_coords.is_empty() else field_coords
	aggregate.flush(hexes, plots)
	for coords in hexes:
		hexes[coords].dirty = false


func seed_rng(s: int) -> void:
	rng.seed = s


func apply_work(coords: Vector2i, action: String, context: Dictionary = {}) -> Dictionary:
	var hex = get_hex(coords)
	if hex == null:
		return {"ok": false, "reason": "No hex."}
	var result := {"ok": true, "reason": ""}
	match action:
		"forage":
			result = _work_forage(hex)
		"clear":
			result = _work_clear(hex)
		"collect_water":
			result = _work_water(hex)
		"trap":
			result = _work_trap(hex, context)
		"chop_firewood":
			result = _work_firewood(hex)
		"build":
			result = _work_build(hex)
		_:
			result = {"ok": false, "reason": "Unknown action."}
	if result.get("ok", false):
		hex.sync_terrain()
		mark_dirty(coords)
		flush_aggregates(field_coords)
	return result


func _work_forage(hex) -> Dictionary:
	if not hex.has_forage():
		return {"ok": false, "reason": "Nothing to forage."}
	var yields: Dictionary = {}
	if hex.forage_mask & HexStateRes.FORAGE_BERRIES:
		yields["berries"] = 1 + int(hex.fertility * 2)
	if hex.forage_mask & HexStateRes.FORAGE_ROOTS:
		yields["roots"] = 1
	if hex.forage_mask & HexStateRes.FORAGE_MUSHROOMS:
		yields["mushrooms"] = 1
	hex.forage_depleted = true
	hex.veg_density = maxf(0.0, hex.veg_density - 0.15)
	return {"ok": true, "yields": yields}


func _work_clear(hex) -> Dictionary:
	if hex.is_water():
		return {"ok": false, "reason": "Cannot clear water."}
	var wood_gain := int(hex.standing_timber)
	hex.standing_timber = 0.0
	hex.veg_density = maxf(0.0, hex.veg_density - 0.4)
	hex.rockiness = minf(1.0, hex.rockiness + 0.1)
	if hex.veg_density < 0.2:
		hex.veg_class = HexStateRes.VegClass.GRASS
		hex.cleared = true
		hex.forage_depleted = false
	return {"ok": true, "wood": wood_gain}


func _work_water(hex) -> Dictionary:
	if not hex.is_riparian and not hex.is_spring:
		return {"ok": false, "reason": "No water source."}
	return {"ok": true, "water": 2}


func _work_trap(hex, _context: Dictionary) -> Dictionary:
	if hex.structure_id != "trap":
		hex.structure_id = "trap"
		return {"ok": true, "built_trap": true}
	return {"ok": true, "meat": rng.randi_range(0, 2)}


func _work_build(hex) -> Dictionary:
	if hex.is_water():
		return {"ok": false, "reason": "Cannot build on water."}
	if not HexTopology.is_settleable(hex):
		return {"ok": false, "reason": "Ground too steep to build."}
	if hex.structure_id != "":
		return {"ok": false, "reason": "Structure already here."}
	return {"ok": true}


func _work_firewood(hex) -> Dictionary:
	if hex.standing_timber < 1.0:
		return {"ok": false, "reason": "No timber."}
	var amount := mini(3, int(hex.standing_timber))
	hex.standing_timber -= float(amount)
	return {"ok": true, "firewood": amount, "wood": amount}
