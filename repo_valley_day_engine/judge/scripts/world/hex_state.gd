class_name HexState
extends RefCounted

const TERRAIN_GRASS := 0
const TERRAIN_FARMLAND := 1
const TERRAIN_WOOD := 2
const TERRAIN_WATER := 3

enum SoilType { LOAM, CLAY, SAND, PEAT, ROCKY }
enum VegClass { BARE, GRASS, SHRUB, WOODLAND, WETLAND }
enum LoreTag { NONE, MISSION_TRAIL, ACEQUIA, HUNTING_GROUND, WAGON_RUT }

const FORAGE_BERRIES := 1
const FORAGE_ROOTS := 2
const FORAGE_MUSHROOMS := 4

var coords: Vector2i = Vector2i.ZERO
var patch_id: Vector2i = Vector2i.ZERO
var block_id: Vector2i = Vector2i.ZERO
var zone_id: Vector2i = Vector2i.ZERO
var chunk_id: Vector2i = Vector2i.ZERO

## Legacy terrain class for rendering aggregates.
var terrain: int = TERRAIN_GRASS
var forest: float = 0.0
var population: int = 0
var food: int = 0
var ownership: String = ""
var dirty: bool = false

## Topology (meters).
var elevation: float = 0.0
var slope_grade: float = 0.0
var cliff_edges: int = 0

## Hydrology.
var moisture: float = 0.0
var water_depth: float = 0.0
var river_flow: int = -1
var is_spring: bool = false
var is_riparian: bool = false

## Ground (mutable).
var soil_type: SoilType = SoilType.LOAM
var rockiness: float = 0.2
var fertility: float = 0.5

## Vegetation (mutable).
var veg_class: VegClass = VegClass.GRASS
var veg_density: float = 0.5
var standing_timber: float = 0.0
var forage_mask: int = 0
var forage_depleted: bool = false

## Human use.
var cleared: bool = false
var trail_level: int = 0
var structure_id: String = ""
var field_id: String = ""
var lore_tag: LoreTag = LoreTag.NONE


func is_water() -> bool:
	return terrain == TERRAIN_WATER or water_depth > 0.5


func is_passable() -> bool:
	return not is_water() and cliff_edges == 0


func has_forage() -> bool:
	return forage_mask != 0 and not forage_depleted


func terrain_class_from_state() -> int:
	if is_water():
		return TERRAIN_WATER
	if cleared or field_id != "":
		return TERRAIN_FARMLAND
	if veg_class == VegClass.WOODLAND:
		return TERRAIN_WOOD
	return TERRAIN_GRASS


func sync_terrain() -> void:
	terrain = terrain_class_from_state()
	forest = veg_density if veg_class == VegClass.WOODLAND else 0.0


func to_dict() -> Dictionary:
	return {
		"x": coords.x, "y": coords.y,
		"terrain": terrain, "forest": forest, "population": population, "food": food,
		"ownership": ownership,
		"elevation": elevation, "slope_grade": slope_grade, "cliff_edges": cliff_edges,
		"moisture": moisture, "water_depth": water_depth, "river_flow": river_flow,
		"is_spring": is_spring, "is_riparian": is_riparian,
		"soil_type": soil_type, "rockiness": rockiness, "fertility": fertility,
		"veg_class": veg_class, "veg_density": veg_density, "standing_timber": standing_timber,
		"forage_mask": forage_mask, "forage_depleted": forage_depleted,
		"cleared": cleared, "trail_level": trail_level,
		"structure_id": structure_id, "field_id": field_id,
		"lore_tag": lore_tag,
	}


static func from_dict(data: Dictionary):
	var hex = new()
	hex.coords = Vector2i(int(data.get("x", 0)), int(data.get("y", 0)))
	hex.terrain = int(data.get("terrain", TERRAIN_GRASS))
	hex.forest = float(data.get("forest", 0.0))
	hex.population = int(data.get("population", 0))
	hex.food = int(data.get("food", 0))
	hex.ownership = str(data.get("ownership", ""))
	hex.elevation = float(data.get("elevation", 0.0))
	hex.slope_grade = float(data.get("slope_grade", 0.0))
	hex.cliff_edges = int(data.get("cliff_edges", 0))
	hex.moisture = float(data.get("moisture", 0.0))
	hex.water_depth = float(data.get("water_depth", 0.0))
	hex.river_flow = int(data.get("river_flow", -1))
	hex.is_spring = bool(data.get("is_spring", false))
	hex.is_riparian = bool(data.get("is_riparian", false))
	hex.soil_type = int(data.get("soil_type", SoilType.LOAM)) as SoilType
	hex.rockiness = float(data.get("rockiness", 0.2))
	hex.fertility = float(data.get("fertility", 0.5))
	hex.veg_class = int(data.get("veg_class", VegClass.GRASS)) as VegClass
	hex.veg_density = float(data.get("veg_density", 0.5))
	hex.standing_timber = float(data.get("standing_timber", 0.0))
	hex.forage_mask = int(data.get("forage_mask", 0))
	hex.forage_depleted = bool(data.get("forage_depleted", false))
	hex.cleared = bool(data.get("cleared", false))
	hex.trail_level = int(data.get("trail_level", 0))
	hex.structure_id = str(data.get("structure_id", ""))
	hex.field_id = str(data.get("field_id", ""))
	hex.lore_tag = int(data.get("lore_tag", LoreTag.NONE)) as LoreTag
	return hex
