extends RefCounted
# DayEngine -- the per-day settlement engine for Valley Claim (PROPER reference: upstream logic).
# Holds the GameState as `host`; reads/writes host world state and calls host support methods.

const WestTheme = preload("res://scripts/theme/west_theme.gd")

var host

func _init(host_ref) -> void:
	host = host_ref


# --- Labour pool ---------------------------------------------------------

func household_labor() -> int:
	var total: int = host.LABOR_HEAD
	for person in host.persons:
		total += int(person.daily_labor)
	return total


func refresh_labor() -> void:
	host.labor_per_day = household_labor()
	host.labor_pool = host.labor_per_day
	host._worked_today = false


# --- Work a day ----------------------------------------------------------

func work_today() -> void:
	if host._worked_today or host.game_lost or host.game_won:
		return
	host._worked_today = true
	_work_fields()
	_work_fields()
	_work_zones()


func _work_zones() -> void:
	if host.game_lost:
		return
	for ztype in host.ZONE_PRIORITY:
		for zone_id in host.work_zones:
			var zone = host.work_zones[zone_id]
			if zone.type != ztype:
				continue
			# one chore per zone per day — work the zone's first hex and move on
			for coords in zone.hexes.duplicate().slice(0, 1):
				if host.labor_pool <= 0:
					return
				_advance_zone_hex(zone, coords)


func _advance_zone_hex(zone, coords: Vector2i) -> void:
	var action: String = host._zone_action_for(zone)
	if action == "":
		return
	var cost: int = host.task_cost(action)
	var need: int = cost - zone.work
	if need <= 0:
		need = cost
	var spend: int = mini(host.labor_pool, need)
	zone.work += spend
	host.labor_pool -= spend
	if zone.work < cost:
		return
	zone.work = 0
	var result: Dictionary = host.hex_sim.apply_work(coords, action, {"zone": zone})
	if result.get("ok", false):
		host._apply_work_yields(result)
		if action == "build":
			host._do_build(coords, zone.structure_kind)
		if result.get("built_trap", false):
			host._log("Set a snare at (%d,%d)." % [coords.x, coords.y])
		elif action == "trap":
			host._log("Checked snares at (%d,%d)." % [coords.x, coords.y])
		else:
			host._log("Worked %s at (%d,%d)." % [host.zone_label(zone), coords.x, coords.y])
	host.plot_changed.emit(coords)
	host.resources_changed.emit()


func _work_fields() -> void:
	return
	for field_id in host.fields:
		var field = host.fields[field_id]
		if field.is_empty():
			continue
		var crop = host.get_crop(field.crop_id)
		if crop == null:
			continue
		if field.is_mature(crop):
			continue
		elif _field_needs_tend(field, crop) and host.labor_pool >= _field_labor_cost("tend_field", field):
			field.tended = true
			host.labor_pool -= _field_labor_cost("tend_field", field)
			host._log("Tended %s field." % crop.display_name)


func _field_labor_cost(action: String, field) -> int:
	return host.task_cost(action) * maxi(1, field.hex_count() / 2)


func _field_needs_tend(field, crop) -> bool:
	if field.tended or field.is_mature(crop):
		return false
	if host.weather == host.Weather.DROUGHT:
		return true
	if host.weather == host.Weather.FROST and not crop.frost_tolerant:
		return true
	return false


func _harvest_field(field_id: String) -> void:
	var field = host.fields[field_id]
	var crop = host.get_crop(field.crop_id)
	if crop == null or not field.is_mature(crop):
		return
	var yield_amount: int = crop.yield_food * maxi(1, field.hex_count() / 2)
	var cost: int = _field_labor_cost("harvest_field", field)
	host.labor_pool -= cost
	host.resources["food"] += yield_amount
	for coords in field.hexes:
		host.proved_hexes[host._hex_key(coords)] = true
	field.clear_crop()
	for coords in field.hexes:
		var hex = host.get_hex(coords)
		if hex != null:
			hex.field_id = field.id
	host.resources_changed.emit()
	host._log("Harvested %s (+ %d food)." % [crop.display_name, yield_amount])


# --- Resolve a day -------------------------------------------------------

func _resolve_day(ended_day: int) -> void:
	if host.game_lost or host.game_won:
		return
	host._sync_calendar_from_turn(ended_day)
	_advance_fields()
	host.person_system.resolve_day(host.persons, host.rng, host)
	_consume_household_daily()
	_check_family_vitality()
	var next_day := ended_day + 1
	var prev_season = host.season
	var prev_year = host.year
	host._sync_calendar_from_turn(next_day)
	if host.season != prev_season or host.year != prev_year:
		if not host._batch_mode:
			host._log("%s of %d begins — %s." % [
				host.season_name(),
				host.scenario_calendar_year(),
				WestTheme.era_name(host.scenario_calendar_year()),
			])
		host._reset_seasonal_forage()
	host._roll_weather()
	refresh_labor()


func _advance_fields() -> void:
	for field_id in host.fields:
		var field = host.fields[field_id]
		if field.is_empty():
			continue
		var crop = host.get_crop(field.crop_id)
		if crop == null:
			continue
		if field.is_mature(crop):
			continue
		if host.weather == host.Weather.DROUGHT and not field.tended:
			if host._batch_mode:
				host._batch_stats["drought_stalls"] += 1
			continue
		field.growth_days += 1
		if host._batch_mode:
			host._batch_stats["growth_days"] += 1


func _consume_household_daily() -> void:
	var mouths: int = host.living_count()
	host.food_consumption_accumulator += float(mouths) * host.FOOD_PER_PERSON_DAY
	host.water_consumption_accumulator += float(mouths) * host.WATER_PER_PERSON_DAY
	var food_consumed := 0
	while host.food_consumption_accumulator >= 1.0 - 0.001:
		_spend_food_unit()
		host.food_consumption_accumulator -= 1.0
		food_consumed += 1
	while host.water_consumption_accumulator >= 1.0 - 0.001:
		host.resources["water"] = host.resources.get("water", 0) - 1
		host.water_consumption_accumulator -= 1.0
	if host.season == host.Season.WINTER:
		host.resources["firewood"] = host.resources.get("firewood", 0) - host.FIREWOOD_PER_WINTER_DAY * mouths
	if host._batch_mode:
		host._batch_stats["food_consumed"] += food_consumed
		if host.total_food() < 0:
			host._batch_stats["hungry_days"] += 1
	host.resources_changed.emit()


func _spend_food_unit() -> void:
	if host.resources.get("meat", 0) > 0:
		host.resources["meat"] -= 1
		return
	for key in ["food", "berries", "roots", "mushrooms"]:
		if host.resources.get(key, 0) > 0:
			host.resources[key] -= 1
			return


func _check_family_vitality() -> void:
	if host.game_lost:
		return
	var starving: bool = host.total_food() < host.living_count()
	var thirsty: bool = host.resources.get("water", 0) < host.living_count()
	var exposed: bool = host.season == host.Season.WINTER and (not host.has_shelter() or host.resources.get("firewood", 0) <= 0)
	if starving or thirsty:
		if not host._batch_mode:
			host._log("The family lacks provisions or water.")
	for person in host.persons:
		if not person.alive:
			continue
		if starving or thirsty:
			person.health -= 15
		elif exposed:
			person.health -= 10
		if person.health <= 0:
			person.alive = false
			person.health = 0
			if not host._batch_mode:
				host._log("%s has died." % person.display_name)
	if host._all_family_dead():
		host.game_lost = true
		host.last_game_over_reason = "The claim failed. The family did not survive the winter."
		host._log(host.last_game_over_reason)
		host.game_over.emit(host.last_game_over_reason)
