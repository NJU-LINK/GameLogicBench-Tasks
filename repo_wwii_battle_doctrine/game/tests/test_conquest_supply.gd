extends SceneTree

const ConquestManager := preload("res://scripts/scenario/conquest_manager.gd")
const ConquestSupply := preload("res://scripts/scenario/conquest_supply.gd")

func _init() -> void:
	var pass_count := 0
	var fail_count := 0

	if _test_supply_status_uses_rail_and_ports():
		pass_count += 1
	else:
		fail_count += 1

	if _test_end_turn_reinforcement_uses_supply_status():
		pass_count += 1
	else:
		fail_count += 1

	if _test_owner_without_source_is_unsupplied_on_logistics_maps():
		pass_count += 1
	else:
		fail_count += 1

	print("ConquestSupply tests: %d pass, %d fail" % [pass_count, fail_count])
	quit(0 if fail_count == 0 else 1)

func _test_supply_status_uses_rail_and_ports() -> bool:
	var regions := _supply_regions()
	var status := ConquestSupply.status_by_region(regions)
	if not bool(status.get("source", false)) \
			or not bool(status.get("rail_hub", false)) \
			or not bool(status.get("port_a", false)) \
			or not bool(status.get("port_b", false)):
		printerr("FAIL: supply should travel through owned rail and port links")
		return false
	if bool(status.get("far_road", true)):
		printerr("FAIL: far road-only region should exceed supply cost budget")
		return false
	if not bool(status.get("neutral_gap", false)):
		printerr("FAIL: neutral regions should be treated as self-supplied")
		return false
	var legacy := {
		"a": {"id": "a", "owner": "x", "production": 2, "neighbors": ["b"]},
		"b": {"id": "b", "owner": "x", "production": 2, "neighbors": ["a"]},
	}
	if not bool(ConquestSupply.status_by_region(legacy).get("b", false)):
		printerr("FAIL: legacy maps without supply sources should remain fully supplied")
		return false
	return true

func _test_owner_without_source_is_unsupplied_on_logistics_maps() -> bool:
	var regions := {
		"source": {"id": "source", "owner": "p", "production": 2, "supply_source": true, "neighbors": []},
		"cutoff": {"id": "cutoff", "owner": "x", "production": 4, "neighbors": []},
	}
	var status := ConquestSupply.status_by_region(regions)
	if bool(status.get("cutoff", true)):
		printerr("FAIL: logistics maps should not supply an owner after all supply sources are lost")
		return false
	return true

func _test_end_turn_reinforcement_uses_supply_status() -> bool:
	var state := {"version": 2, "campaigns": {}}
	var map_data := _supply_map()
	ConquestManager.set_player_country(state, map_data, "p")
	var conquest := ConquestManager.conquest_state(state, map_data)
	conquest["regions"]["rail_hub"]["strength"] = 10
	conquest["regions"]["far_road"]["strength"] = 10
	conquest["regions"]["enemy"]["strength"] = 1
	var before_supplied := int(conquest["regions"]["rail_hub"].get("strength", 0))
	var before_unsupplied := int(conquest["regions"]["far_road"].get("strength", 0))
	var step := ConquestManager.end_turn(state, map_data)
	var after_supplied := int(ConquestManager.region_state(state, map_data, "rail_hub").get("strength", 0))
	var after_unsupplied := int(ConquestManager.region_state(state, map_data, "far_road").get("strength", 0))
	if String(step.get("status", "")) != "done":
		printerr("FAIL: supply end_turn test should complete, got %s" % str(step))
		return false
	if after_supplied - before_supplied != 2:
		printerr("FAIL: supplied region should reinforce by production/2, got %d" % (after_supplied - before_supplied))
		return false
	if after_unsupplied - before_unsupplied != 1:
		printerr("FAIL: unsupplied region should reinforce by production/4, got %d" % (after_unsupplied - before_unsupplied))
		return false
	return true

func _supply_map() -> Dictionary:
	return {
		"start_country": "p",
		"map_width": 8,
		"map_height": 2,
		"countries": {
			"p": {"name_zh": "P", "color": "#ffffff"},
			"enemy": {"name_zh": "E", "color": "#ff0000"},
			"neutral": {"name_zh": "N", "color": "#777777"},
		},
		"regions": [
			{"id": "source", "name_zh": "Source", "short_name_zh": "Src", "owner": "p", "x": 0, "y": 0, "production": 6, "supply_source": true, "rail_neighbors": ["rail_hub"], "neighbors": ["rail_hub"]},
			{"id": "rail_hub", "name_zh": "Rail", "short_name_zh": "Rail", "owner": "p", "x": 1, "y": 0, "production": 4, "rail_neighbors": ["source"], "neighbors": ["source", "port_a", "road_1"]},
			{"id": "port_a", "name_zh": "Port A", "short_name_zh": "PA", "owner": "p", "x": 2, "y": 0, "production": 2, "port": true, "neighbors": ["rail_hub", "port_b"]},
			{"id": "port_b", "name_zh": "Port B", "short_name_zh": "PB", "owner": "p", "x": 3, "y": 0, "production": 2, "port": true, "neighbors": ["port_a"]},
			{"id": "road_1", "name_zh": "Road 1", "short_name_zh": "R1", "owner": "p", "x": 4, "y": 0, "production": 1, "neighbors": ["rail_hub", "road_2"]},
			{"id": "road_2", "name_zh": "Road 2", "short_name_zh": "R2", "owner": "p", "x": 5, "y": 0, "production": 1, "neighbors": ["road_1", "far_road"]},
			{"id": "far_road", "name_zh": "Far", "short_name_zh": "Far", "owner": "p", "x": 6, "y": 0, "production": 4, "neighbors": ["road_2"]},
			{"id": "neutral_gap", "name_zh": "Neutral", "short_name_zh": "N", "owner": "neutral", "x": 7, "y": 0, "production": 1, "neighbors": []},
			{"id": "enemy", "name_zh": "Enemy", "short_name_zh": "E", "owner": "enemy", "x": 7, "y": 1, "production": 1, "neighbors": []},
		],
	}

func _supply_regions() -> Dictionary:
	var out := {}
	for region in _supply_map().get("regions", []):
		var r: Dictionary = region
		out[String(r.get("id", ""))] = r.duplicate(true)
	return out
