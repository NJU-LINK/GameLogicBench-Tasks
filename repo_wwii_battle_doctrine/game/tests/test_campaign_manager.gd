extends SceneTree

const CampaignManager := preload("res://scripts/scenario/campaign_manager.gd")
const LoungeManager := preload("res://scripts/scenario/lounge_manager.gd")

class StubUnit:
	var faction_id: String
	var display_name: String
	var xp: int
	var rank: int
	var general_id: String
	var redraws := 0
	var alive := true

	func _init(_faction_id: String, _display_name: String, _xp: int, _rank: int, _general_id: String) -> void:
		faction_id = _faction_id
		display_name = _display_name
		xp = _xp
		rank = _rank
		general_id = _general_id

	func is_alive() -> bool:
		return alive

	func queue_redraw() -> void:
		redraws += 1

func _init() -> void:
	var pass_count := 0
	var fail_count := 0

	var state := {"version": 2, "campaigns": {}}
	var east_order := ["03_stalingrad_1942", "04_kursk_1943"]
	var west_order := ["05_bastogne_1944"]
	if CampaignManager.current_scenario_id(state, "east", east_order) == "03_stalingrad_1942" \
			and CampaignManager.current_scenario_id(state, "west", west_order) == "05_bastogne_1944":
		pass_count += 1
	else:
		fail_count += 1
		printerr("FAIL: per-series current scenario should start at first scenario")

	var survivor := StubUnit.new("soviet", "近衛步", 4, 1, "chuikov")
	CampaignManager.complete_scenario(state, "east", east_order, "03_stalingrad_1942", [survivor])
	var east_state: Dictionary = CampaignManager.campaign_state(state, "east", east_order)
	var west_state: Dictionary = CampaignManager.campaign_state(state, "west", west_order)
	if int(east_state.get("progress", 0)) == 1 \
			and int(west_state.get("progress", 0)) == 0 \
			and CampaignManager.current_scenario_id(state, "east", east_order) == "04_kursk_1943":
		pass_count += 1
	else:
		fail_count += 1
		printerr("FAIL: completing one series should not advance another")

	var fresh := StubUnit.new("soviet", "近衛步", 0, 0, "")
	CampaignManager.apply_roster_to_units(state, "east", east_order, [fresh])
	if fresh.xp == 4 and fresh.rank == 1 and fresh.general_id == "chuikov" and fresh.redraws == 1:
		pass_count += 1
	else:
		fail_count += 1
		printerr("FAIL: campaign roster should restore xp/rank/general")

	CampaignManager.complete_scenario(
		state,
		"east",
		east_order,
		"04_kursk_1943",
		[],
		[{"type": "conquest_reduce_enemy_strength", "amount": 1}]
	)
	east_state = CampaignManager.campaign_state(state, "east", east_order)
	if int(east_state.get("progress", 0)) == 2 \
			and int(east_state.get("bonus_points", 0)) == 0 \
			and LoungeManager.total_points(state) == 7:
		pass_count += 1
	else:
		fail_count += 1
		printerr("FAIL: campaign completion should ignore cross-battle strategic effects")

	var old_save := {
		"version": 1,
		"progress": 2,
		"roster": {"axis": {"Pz.IV": {"xp": 3, "rank": 1, "general_id": "guderian"}}},
	}
	var migrated := CampaignManager._normalise_state(old_save)
	var blitz_state: Dictionary = CampaignManager.campaign_state(
		migrated, "blitzkrieg_early_war", [
			"blitz_00_poland_1939",
			"01_sedan_1940",
			"blitz_02_dunkirk_1940",
			"02_kiev_1941",
			"blitz_03_moscow_1941",
		]
	)
	var eastern_state: Dictionary = CampaignManager.campaign_state(
		migrated, "eastern_front", ["03_stalingrad_1942", "04_kursk_1943"]
	)
	if int(blitz_state.get("progress", 0)) == 0 \
			and int(eastern_state.get("progress", 0)) == 0 \
			and Dictionary(blitz_state.get("roster", {})).is_empty() \
			and not migrated.has("progress") \
			and not migrated.has("roster"):
		pass_count += 1
	else:
		fail_count += 1
		printerr("FAIL: old linear save should not affect series state")

	print("CampaignManager tests: %d pass, %d fail" % [pass_count, fail_count])
	quit(0 if fail_count == 0 else 1)
