class_name ReinforcementSpawner
extends RefCounted

static func spawn_for_turn(
	scenario: Dictionary,
	factions: Dictionary,
	hex_map,
	units: Array,
	spawned_reinforcements: Dictionary,
	faction_id: String,
	turn_number: int,
	unit_factory = null,
) -> Array:
	var factory = unit_factory if unit_factory != null else load("res://scripts/units/unit_factory.gd")
	var reinforcements: Array = scenario.get("reinforcements", [])
	var fresh: Array = []
	for i in range(reinforcements.size()):
		if spawned_reinforcements.has(i):
			continue
		var r: Dictionary = reinforcements[i]
		if int(r.get("at_turn", -1)) != turn_number:
			continue
		if String(r.get("faction", "")) != faction_id:
			continue
		var unit = factory.create_unit(r, factions)
		if unit == null:
			spawned_reinforcements[i] = true
			continue
		if hex_map.occupants.get(unit.coord) != null:
			push_warning("[Reinforcement] spawn hex %s occupied; skipping" % [unit.coord])
			unit.queue_free()
			spawned_reinforcements[i] = true
			continue
		var registered: Variant = hex_map.register_unit(unit)
		if registered == false:
			unit.queue_free()
			spawned_reinforcements[i] = true
			continue
		units.append(unit)
		unit.reset_for_new_turn()
		spawned_reinforcements[i] = true
		fresh.append(unit)
	return fresh
