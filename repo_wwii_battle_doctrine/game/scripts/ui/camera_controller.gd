class_name CameraController
extends Camera2D

# WASD / arrow-key pan, mouse-drag pan, wheel zoom.

@export var pan_speed: float = 600.0
@export var zoom_min: float = 0.4
@export var zoom_max: float = 2.5
@export var zoom_step: float = 0.1
@export var overview_padding: float = 0.92

var _dragging := false
var _drag_start_mouse := Vector2.ZERO
var _drag_start_pos := Vector2.ZERO

func _ready() -> void:
	make_current()

func _process(delta: float) -> void:
	var input_vec := Vector2.ZERO
	input_vec.x = Input.get_action_strength("ui_camera_pan_right") - Input.get_action_strength("ui_camera_pan_left")
	input_vec.y = Input.get_action_strength("ui_camera_pan_down") - Input.get_action_strength("ui_camera_pan_up")
	if input_vec != Vector2.ZERO:
		position += input_vec.normalized() * pan_speed * delta / zoom.x

func _unhandled_input(event: InputEvent) -> void:
	if event is InputEventMouseButton:
		var mb := event as InputEventMouseButton
		if mb.button_index == MOUSE_BUTTON_WHEEL_UP and mb.pressed:
			_apply_zoom(zoom_step)
		elif mb.button_index == MOUSE_BUTTON_WHEEL_DOWN and mb.pressed:
			_apply_zoom(-zoom_step)
		elif mb.button_index == MOUSE_BUTTON_MIDDLE:
			if mb.pressed:
				_dragging = true
				_drag_start_mouse = mb.position
				_drag_start_pos = position
			else:
				_dragging = false
	elif event is InputEventMouseMotion and _dragging:
		var mm := event as InputEventMouseMotion
		position = _drag_start_pos - (mm.position - _drag_start_mouse) / zoom.x

func _apply_zoom(delta_zoom: float) -> void:
	var new_zoom_v: float = clamp(zoom.x + delta_zoom, zoom_min, zoom_max)
	zoom = Vector2(new_zoom_v, new_zoom_v)

func fit_world_rect(world_rect: Rect2, screen_rect: Rect2 = Rect2(), max_initial_zoom: float = 1.0) -> void:
	if world_rect.size.x <= 0.0 or world_rect.size.y <= 0.0:
		return
	var viewport_rect := get_viewport_rect()
	if screen_rect.size.x <= 0.0 or screen_rect.size.y <= 0.0:
		screen_rect = viewport_rect
	var fit_size: Vector2 = screen_rect.size * clamp(overview_padding, 0.1, 1.0)
	var fit_zoom: float = min(fit_size.x / world_rect.size.x, fit_size.y / world_rect.size.y)
	var zoom_v: float = clamp(min(max_initial_zoom, fit_zoom), zoom_min, zoom_max)
	zoom = Vector2(zoom_v, zoom_v)
	var world_center := world_rect.position + world_rect.size * 0.5
	var viewport_center := viewport_rect.size * 0.5
	var screen_center := screen_rect.position + screen_rect.size * 0.5
	position = world_center - (screen_center - viewport_center) / zoom_v

# Center a world point at the middle of screen_rect (the visible map area,
# minus the HUD panels) without changing zoom. Used by the Tab unit-cycle.
func focus_on(world_pos: Vector2, screen_rect: Rect2 = Rect2()) -> void:
	var viewport_rect := get_viewport_rect()
	if screen_rect.size.x <= 0.0 or screen_rect.size.y <= 0.0:
		screen_rect = viewport_rect
	var viewport_center := viewport_rect.size * 0.5
	var screen_center := screen_rect.position + screen_rect.size * 0.5
	position = world_pos - (screen_center - viewport_center) / zoom.x
