extends Node

# Press F12 anywhere in the game to dump the current viewport to PNG.
# Files go to user://screenshots/ which on Linux is
# ~/.local/share/godot/app_userdata/WorldWarII/screenshots/.
# Copy them into docs/screenshots/ before committing.

const OUT_DIR := "user://screenshots/"

func _ready() -> void:
	DirAccess.make_dir_recursive_absolute(ProjectSettings.globalize_path(OUT_DIR))

func _input(event: InputEvent) -> void:
	if event is InputEventKey:
		var ke := event as InputEventKey
		if ke.pressed and not ke.echo and ke.keycode == KEY_F12:
			_capture()

func _capture() -> void:
	var vp := get_viewport()
	if vp == null:
		return
	var img := vp.get_texture().get_image()
	if img == null:
		return
	var stamp := Time.get_datetime_string_from_system().replace(":", "-").replace("T", "_")
	var path := OUT_DIR + "ww2_%s.png" % stamp
	var err := img.save_png(path)
	if err == OK:
		print("[Screenshot] saved → %s" % ProjectSettings.globalize_path(path))
	else:
		printerr("[Screenshot] save failed: %d" % err)
