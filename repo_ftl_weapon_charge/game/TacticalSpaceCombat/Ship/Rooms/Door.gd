extends Area2D

signal opened

var is_open := false: set = set_is_open
var rooms := []

var _units := 0

@onready var sprite: Sprite2D = $Sprite2D
@onready var timer: Timer = $Timer


func _ready() -> void:
	connect("area_entered", Callable(self, "_on_area_entered_exited").bind(true))
	connect("area_exited", Callable(self, "_on_area_entered_exited").bind(false))
	timer.connect("timeout", Callable(self, "set_is_open").bind(true))


func _on_area_entered_exited(area: Area2D, has_entered: bool) -> void:
	if area.is_in_group("unit"):
		_units += 1 if has_entered else -1

		match [_units, has_entered]:
			[0, false]:
				self.is_open = false

			[1, true]:
				timer.start()

	elif area.is_in_group("room") and has_entered:
		rooms.push_back(area)


func set_is_open(value: bool) -> void:
	is_open = value
	if is_open:
		sprite.frame = 1
		emit_signal("opened")
	else:
		sprite.frame = 0
