class_name ControllerPlayer
extends Controller

var _ui_weapon_button: Button
var _ui_weapon_progress_bar: ProgressBar


func setup(ui_weapon: VBoxContainer) -> void:
	_ui_weapon_button = ui_weapon.get_node("Button")
	_ui_weapon_progress_bar = ui_weapon.get_node("ProgressBar")
	_ui_weapon_progress_bar.min_value = Weapon.MIN_CHARGE
	_ui_weapon_progress_bar.max_value = Weapon.MAX_CHARGE

	_ui_weapon_button.connect("gui_input", Callable(self, "_on_UIWeaponButton_gui_input"))
	_ui_weapon_button.connect("toggled", Callable(self, "_on_UIWeaponButton_toggled"))
	_ui_weapon_button.text = weapon.weapon_name


func _process(_delta: float) -> void:
	if _ui_weapon_progress_bar != null:
		_ui_weapon_progress_bar.value = weapon._charge


func _input(event: InputEvent) -> void:
	if (
		event.is_action("right_click")
		and _ui_weapon_button.button_pressed
		and Input.get_current_cursor_shape() == Input.CURSOR_CROSS
	):
		_ui_weapon_button.button_pressed = false


func _on_UIWeaponButton_gui_input(event: InputEvent) -> void:
	if event.is_action_pressed("right_click"):
		_ui_weapon_button.button_pressed = false


func _on_UIWeaponButton_toggled(is_pressed: bool) -> void:
	var cursor_shape := Input.CURSOR_CROSS if is_pressed else Input.CURSOR_ARROW
	Input.set_default_cursor_shape(cursor_shape)
